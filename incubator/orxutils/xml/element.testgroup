#!/usr/bin/rexx
/*
  SVN Revision: $Rev: 3371 $
  Change Date:  $Date: 2008-09-21 00:33:29 -0400 (Sun, 21 Sep 2008) $
*/
/*----------------------------------------------------------------------------*/
/*                                                                            */
/* Copyright (c) 2005-2010 Rexx Language Association. All rights reserved.    */
/*                                                                            */
/* This program and the accompanying materials are made available under       */
/* the terms of the Common Public License v1.0 which accompanies this         */
/* distribution. A copy is also available at the following address:           */
/* http://www.oorexx.org/license.html                                         */
/*                                                                            */
/* Redistribution and use in source and binary forms, with or                 */
/* without modification, are permitted provided that the following            */
/* conditions are met:                                                        */
/*                                                                            */
/* Redistributions of source code must retain the above copyright             */
/* notice, this list of conditions and the following disclaimer.              */
/* Redistributions in binary form must reproduce the above copyright          */
/* notice, this list of conditions and the following disclaimer in            */
/* the documentation and/or other materials provided with the distribution.   */
/*                                                                            */
/* Neither the name of Rexx Language Association nor the names                */
/* of its contributors may be used to endorse or promote products             */
/* derived from this software without specific prior written permission.      */
/*                                                                            */
/* THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS        */
/* "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT          */
/* LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS          */
/* FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT   */
/* OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,      */
/* SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED   */
/* TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA,        */
/* OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY     */
/* OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING    */
/* NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS         */
/* SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.               */
/*                                                                            */
/*----------------------------------------------------------------------------*/
  parse source . . fileSpec;

  group = .TestGroup~new(fileSpec)
  group~add(.element.testGroup)

  if group~isAutomatedTest then return group

  testResult = group~suite~execute~~print

return testResult
-- End of entry point.

::requires "ooTest.frm"     -- load the ooRexxUnit classes
::requires "xmldom.cls"

::class "element.testGroup" subclass ooTestCase public
::method init
  expose dom document
  forward class(super) continue
  dom = .ooRexxDOM~implementation
  document = dom~createDocument

::attribute dom
::attribute document

::method testBaseElement
  doc = self~document

  element = doc~createElement("FOO")

  self~assertTrue(element~isA(.Element))
  self~assertTrue(element~isA(.Node))
  self~assertNull(element~namespaceURI)
  self~assertEquals("FOO", element~localName)
  self~assertEquals(.node~ELEMENT_NODE, element~nodeType)
  self~assertEquals("FOO", element~nodeName)
  self~assertEquals("FOO", element~tagName)
  self~assertNull(element~baseURI)
  self~assertNull(element~prefix)
  self~assertEquals("", element~getAttribute("XYZ"))
  self~assertNull(element~getAttributeNode("XYZ"))
  self~assertFalse(element~hasAttributes)
  self~assertFalse(element~hasAttribute("XYZ"))
  self~assertEquals(0, element~childElementCount)
  self~assertNull(element~firstElementChild)
  self~assertNull(element~lastElementChild)
  attrs = element~attributes
  self~assertEquals(0, attrs~length)
  self~assertEquals(0, attrs~items)
  self~assertNull(element~nextElementSibling)
  self~assertNull(element~previousElementSibling)

  element~rename("BAR")
  self~assertEquals("BAR", element~localName)
  self~assertEquals("BAR", element~nodeName)
  self~assertEquals("BAR", element~tagName)
  element~prefix = "foo"
  self~assertEquals("BAR", element~localName)
  self~assertEquals("foo:BAR", element~nodeName)
  self~assertEquals("foo:BAR", element~tagName)

::method testBaseAttribute

  doc = self~document
  element = doc~createElement("FOO")

  self~assertFalse(element~hasAttributes)
  self~assertFalse(element~hasAttribute("XYZ"))
  self~assertEquals("", element~getAttribute("XYZ"))
  self~assertNull(element~getAttributeNode("XYZ"))
  -- just make sure a remove on an empty list doesn't cause an error
  element~removeAttribute("XYZ")

  attr = element~setAttribute("ABC", "123")

  -- verify we got a good attribute node back
  self~assertTrue(attr~isA(.attr))
  -- the attribute list is created lazily...ensure the unknown
  -- attributes are still unknown

  self~assertTrue(element~hasAttributes)
  self~assertFalse(element~hasAttribute("XYZ"))
  self~assertEquals("", element~getAttribute("XYZ"))
  self~assertNull(element~getAttributeNode("XYZ"))

  -- now check the added attribute
  self~assertTrue(element~hasAttribute("ABC"))
  self~assertEquals("123", element~getAttribute("ABC"))
  self~assertEquals("123", element~getAttributeNode("ABC")~value)

  -- remove the added attribute (should return the node)
  attr = element~removeAttribute("ABC")
  -- verify we got a good attribute node back
  self~assertTrue(attr~isA(.attr))

  -- make sure it is really gone
  self~assertFalse(element~hasAttributes)
  self~assertFalse(element~hasAttribute("ABC"))
  self~assertEquals("", element~getAttribute("ABC"))
  self~assertNull(element~getAttributeNode("ABC"))
  -- set to a new value
  element~setAttribute("ABC", "456")

  self~assertEquals("456", element~getAttribute("ABC"))
  -- get the attribute node
  attr = element~getAttributeNode("ABC")

  -- verify the parent child relationship
  self~assertSame(element, attr~element)
  self~assertSame(element, attr~ownerelement)

  -- change the attribute node value...this should show up
  -- in the element
  attr~value = "789"

  self~assertEquals("789", element~getAttribute("ABC"))
  -- remove the attribute by the node
  element~removeAttributeNode(attr)
  -- should be done now
  self~assertEquals("", element~getAttribute("ABC"))
  -- this should not be owned now
  self~assertNull(attr~element)
  self~assertNull(attr~ownerelement)
  -- add this back directly using the attribute node
  element~setAttributeNode(attr)
  self~assertEquals("789", element~getAttribute("ABC"))
  -- verify the parent/child relationship
  self~assertSame(element, attr~element)
  self~assertSame(element, attr~ownerelement)

  -- rename the attribute
  attr~rename("DEF")
  -- verify this renamed correctly
  self~assertEquals("", element~getAttribute("ABC"))
  self~assertEquals("789", element~getAttribute("DEF"))

  -- verify the parent child relationship
  self~assertSame(element, attr~element)
  self~assertSame(element, attr~ownerelement)


-- various tests for text content
::method testTextContent

  doc = self~document
  element = doc~createElement("FOO")

  -- should be zero initially
  self~assertEquals("", element~textContent)

  element~textContent = 123
  self~assertEquals(123, element~textContent)

  text = element~firstChild

  -- this should be a text node as the first child now
  self~assertTrue(text~isA(.Text))
  self~assertTrue(text~isA(.Node))
  self~assertEquals(.node~TEXT_NODE, text~nodeType)
  self~assertEquals(123, text~textContent)
  self~assertSame(element, text~parentNode)

  newText = doc~createTextNode("ABC")
  element~appendChild(newText)

  -- should be two text nodes now, and the text content
  -- should concatenate the values
  self~assertEquals("123ABC", element~textContent)

  -- this should remove all of the text nodes
  element~textContent = ""
  self~assertEquals("", element~textContent)

  -- now add back in and delete by assigning to .nil
  element~textContent = "DEF"
  self~assertEquals("DEF", element~textContent)

  -- this should remove all of the text nodes again
  element~textContent = .nil
  self~assertEquals("", element~textContent)


::method testChildren

  doc = self~document
  -- this is the parent node we'll be adding everything else to
  element = doc~createElement("PARENT")

  self~assertNull(element~firstChild)
  self~assertNull(element~lastChild)
  self~assertFalse(element~hasChildNodes)
  self~assertEquals(0, element~length)
  self~assertNull(element~item(0))
  self~assertNull(element[0])

  nodeList = element~childNodes

  self~assertTrue(nodeList~isA(.NodeList))
  self~assertEquals(0, nodeList~length)
  self~assertNull(nodeList~item(0))
  self~assertNull(nodeList[0])
  self~assertEquals(0, nodeList~items)

  -- validate the makearray results with no children
  array = element~makearray
  self~assertEquals(0, array~items)

  -- time to start adding children to see how things change
  child1 = doc~createElement("CHILD1")
  -- quick check of some of the node relationship values before
  -- we insert this
  self~assertNull(child1~parentNode)
  self~assertNull(child1~nextSibling)
  self~assertNull(child1~previousSibling)

  element~appendChild(child1)

  self~assertSame(child1, element~firstChild)
  self~assertSame(child1, element~lastChild)
  self~assertSame(element, child1~parentNode)
  self~assertNull(child1~nextSibling)
  self~assertNull(child1~previousSibling)
  self~assertTrue(element~hasChildNodes)
  self~assertEquals(1, element~length)
  self~assertSame(child1, element~item(0))
  self~assertSame(child1, element[0])
  self~assertNull(element~item(1))

  -- we don't request a new nodelist...this should see the
  -- updates using the existing reference

  self~assertEquals(1, nodeList~length)
  self~assertSame(child1, nodeList~item(0))
  self~assertSame(child1, nodeList[0])
  self~assertEquals(1, nodeList~items)

  -- check the make array result
  array = element~makearray
  self~assertEquals(1, array~items)
  self~assertSame(child1, array[1])

  -- add a second node, and check everything is still ok
  child2 = doc~createElement("CHILD2")
  element~appendChild(child2)

  self~assertSame(child1, element~firstChild)
  self~assertSame(child2, element~lastChild)
  self~assertEquals(2, element~length)
  self~assertSame(child1, element~item(0))
  self~assertSame(child2, element~item(1))

  self~assertSame(child2, child1~nextSibling)
  self~assertNull(child1~previousSibling)

  self~assertSame(child1, child2~previousSibling)
  self~assertNull(child2~nextSibling)

  -- check the make array result
  array = element~makearray
  self~assertEquals(2, array~items)
  self~assertSame(child1, array[1])
  self~assertSame(child2, array[2])

  -- Now some insertions.  We'll insert several here, and check the
  -- results at the end
  child3 = doc~createElement("CHILD3")
  -- new first element
  element~insertBefore(child3, child1)
  child4 = doc~createElement("CHILD4")
  -- between 1 and 2
  element~insertBefore(child4, child2)

  child5 = doc~createElement("CHILD5")
  -- between appended to the end
  element~insertBefore(child5, .nil)

  -- we should have 5 elements now, in the
  -- order 3, 1, 4, 2, 5
  self~assertEquals(5, element~length)
  self~assertSame(child3, nodeList[0])
  self~assertSame(child1, nodeList[1])
  self~assertSame(child4, nodeList[2])
  self~assertSame(child2, nodeList[3])
  self~assertSame(child5, nodeList[4])

  -- ok, now this gets fun.  The DOM spec says
  -- an inserted element will be removed from its current
  -- parent, so we'll use insert to move things around a little

  -- now 1, 4, 2, 5, 3
  element~insertBefore(child3)
  -- now 1, 2, 4, 5, 3
  element~insertBefore(child2, child4)
  -- now 1, 2, 3, 4, 5
  element~insertBefore(child3, child4)

  -- now verify the order
  self~assertEquals(5, element~length)
  self~assertSame(child1, nodeList[0])
  self~assertSame(child2, nodeList[1])
  self~assertSame(child3, nodeList[2])
  self~assertSame(child4, nodeList[3])
  self~assertSame(child5, nodeList[4])

  -- now some removals/replacements
  self~assertSame(child1, element~replaceChild(child5, child1))
  self~assertEquals(4, element~length)
  self~assertSame(child5, element~firstChild)
  self~assertSame(child4, element~lastChild)

  -- remove from the middle
  self~assertSame(child2, element~removeChild(child2))
  self~assertEquals(3, element~length)

  -- the end
  self~assertSame(child4, element~removeChild(child4))
  self~assertEquals(2, element~length)

  -- the beginning
  self~assertSame(child5, element~removeChild(child5))
  self~assertEquals(1, element~length)

  -- and the last one
  self~assertSame(child3, element~removeChild(child3))
  self~assertEquals(0, element~length)

  self~assertNull(element~firstChild)
  self~assertNull(element~lastChild)

::method testElementNamespace

  doc = self~document
  -- this is the parent node we'll be adding everything else to
  element = doc~createElement("FOO")

  child1 = doc~createElementNS("http://www.oorexx.org/xml/unittest.xml", "oorexx:foo")

  self~assertEquals("http://www.oorexx.org/xml/unittest.xml", child1~namespaceURI)
  self~assertEquals("oorexx:foo", child1~nodeName)
  self~assertEquals("oorexx:foo", child1~tagName)
  self~assertEquals("foo", child1~localName)
  self~assertEquals("oorexx", child1~prefix)

  -- DOM 3 allows the prefix to be explicitly set
  child1~prefix = "rexx"

  self~assertEquals("rexx:foo", child1~nodeName)
  self~assertEquals("rexx:foo", child1~tagName)
  self~assertEquals("foo", child1~localName)
  self~assertEquals("rexx", child1~prefix)

::method testAttributeNamespace

  doc = self~document
  -- this is the parent node we'll be adding everything else to
  element = doc~createElement("FOO")

  attr = element~setAttributeNS("http://www.oorexx.org/xml/unittest.xml", "oorexx:foo", "bar")

  self~assertTrue(attr~isa(.attr))
  self~assertEquals("bar", attr~value)
  self~assertEquals("bar", attr~nodevalue)
  self~assertEquals("oorexx:foo", attr~nodename)
  -- NB:  attributes support name, but not tagname
  self~assertEquals("oorexx:foo", attr~name)
  self~assertEquals("foo", attr~localname)
  self~assertEquals("oorexx", attr~prefix)
  self~assertEquals("http://www.oorexx.org/xml/unittest.xml", attr~namespaceURI)

  -- this should be locateable via the fully qualified name, but not
  -- by the local name
  self~assertEquals("bar", element~getAttribute("oorexx:foo"))
  self~assertEquals("", element~getAttribute("foo"))
  self~assertSame(attr, element~getAttributeNode("oorexx:foo"))
  self~assertNull(element~getAttributeNode("foo"))
  -- now search by the NS version

  self~assertTrue(element~hasAttribute("oorexx:foo"))
  self~assertTrue(element~hasAttributeNS("http://www.oorexx.org/xml/unittest.xml", "foo"))
  self~assertEquals("bar", element~getAttributeNS("http://www.oorexx.org/xml/unittest.xml", "foo"))
  self~assertSame(attr, element~getAttributeNodeNS("http://www.oorexx.org/xml/unittest.xml", "foo"))

  -- get the attribute list and retry these queries
  attributes = element~attributes
  self~assertSame(attr, attributes~getNamedItem("oorexx:foo"))
  self~assertSame(attr, attributes~getNamedItemNS("http://www.oorexx.org/xml/unittest.xml", "foo"))

  self~assertSame(attr, element~removeAttribute("oorexx:foo"))
  self~assertEquals("", element~getAttribute("oorexx:foo"))

  -- add back in
  self~assertNull(element~setAttributeNodeNS(attr))
  self~assertEquals("bar", element~getAttributeNS("http://www.oorexx.org/xml/unittest.xml", "foo"))

  -- create a replacement attribute
  attr2 = doc~createAttributeNS("http://www.oorexx.org/xml/unittest.xml", "oorexx:foo")
  attr2~value = "123"
  -- and replace
  self~assertSame(attr, element~setAttributeNodeNS(attr2))

  self~assertEquals("123", element~getAttributeNS("http://www.oorexx.org/xml/unittest.xml", "foo"))

  -- remove using the namespace
  self~assertSame(attr2, element~removeAttributeNS("http://www.oorexx.org/xml/unittest.xml", "foo"))
  self~assertEquals("", element~getAttributeNS("http://www.oorexx.org/xml/unittest.xml", "foo"))
  -- now test sets and removes using the attribute list

  self~assertNull(attributes~setNamedItemNS(attr))
  self~assertEquals("bar", element~getAttributeNS("http://www.oorexx.org/xml/unittest.xml", "foo"))
  self~assertSame(attr, attributes~removeNamedItemNS("http://www.oorexx.org/xml/unittest.xml", "foo"))
  self~assertEquals("", element~getAttributeNS("http://www.oorexx.org/xml/unittest.xml", "foo"))
