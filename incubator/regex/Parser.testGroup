#!/usr/bin/rexx
/*----------------------------------------------------------------------------*/
/*                                                                            */
/* Copyright (c) 2018 Rexx Language Association. All rights reserved.         */
/*                                                                            */
/* This program and the accompanying materials are made available under       */
/* the terms of the Common Public License v1.0 which accompanies this         */
/* distribution. A copy is also available at the following address:           */
/* http://www.oorexx.org/license.html                                         */
/*                                                                            */
/* Redistribution and use in source and binary forms, with or                 */
/* without modification, are permitted provided that the following            */
/* conditions are met:                                                        */
/*                                                                            */
/* Redistributions of source code must retain the above copyright             */
/* notice, this list of conditions and the following disclaimer.              */
/* Redistributions in binary form must reproduce the above copyright          */
/* notice, this list of conditions and the following disclaimer in            */
/* the documentation and/or other materials provided with the distribution.   */
/*                                                                            */
/* Neither the name of Rexx Language Association nor the names                */
/* of its contributors may be used to endorse or promote products             */
/* derived from this software without specific prior written permission.      */
/*                                                                            */
/* THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS        */
/* "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT          */
/* LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS          */
/* FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT   */
/* OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,      */
/* SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED   */
/* TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA,        */
/* OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY     */
/* OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING    */
/* NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS         */
/* SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.               */
/*                                                                            */
/*----------------------------------------------------------------------------*/
  parse source . . fileSpec;

  group = .TestGroup~new(fileSpec)
  group~add(.parser.testGroup)
  group~add(.parser.mutable.testGroup)

  if group~isAutomatedTest then return group

  testResult = group~suite~execute~~print

return testResult
-- End of entry point.

::requires "ooTest.frm"     -- load the ooRexxUnit classes
::requires "regex.cls"
::class "parser.mutable.testGroup" subclass "parser.testGroup" public
-- method used for test polymorphism.  Depending on the class of the
-- test group, this will either return a string or a mutablebuffer instance
-- for match, startswith, and find tests
::method td
  use arg d
  return .mutablebuffer~new(d)   -- this is the mutable buffer form

::class "parser.testGroup" subclass ooTestCase public

-- method used for test polymorphism.  Depending on the class of the
-- test group, this will either return a string or a mutablebuffer instance
-- for match, startswith, and find tests
::method td
  use arg d
  return d   -- this is the string form

-- matching methods used to handle both string and mutablebuffer variations
::method matches
  use arg pattern, string
  return pattern~matches(self~td(string))

::method startsWith
  use arg pattern, string
  return pattern~startsWith(self~td(string))

::method find
  use arg pattern, string
  return pattern~find(self~td(string))

::method findStart
  use arg pattern, string, start
  return pattern~find(self~td(string), start)

::method findStartLength
  use arg pattern, string, start, length
  return pattern~find(self~td(string), start, length)

::method verifyParserState
  use strict arg parser, matchStart, matchEnd, matchText, sectionStart, sectionEnd, section, remainder

  self~assertSame(matchStart, parser~matchStart, "Incorrect matchStart")
  self~assertSame(matchEnd, parser~matchEnd, "Incorrect matchEnd")
  self~assertSame(matchEnd - matchStart, parser~matchLength, "Incorrect matchLength")
  self~assertSame(matchText, parser~matchText, "Incorrect matchText")
  self~assertSame(sectionStart, parser~sectionStart, "Incorrect sectionStart")
  self~assertSame(sectionEnd, parser~sectionEnd, "Incorrect sectionEnd")
  self~assertSame(sectionEnd - sectionStart, parser~sectionLength, "Incorrect sectionLength")
  self~assertSame(section, parser~section, "Incorrect section text")
  self~assertSameList(section~subWords, parser~sectionWords, "Incorrect section words")
  self~assertSame(remainder, parser~remainder, "Incorrect remainder text")

  self~assertTrue(self~verifyMatchResult(parser~matchResult, matchStart, matchEnd, matchText), "Incorrect match result information")

-- utilty method to verify a match result
::method verifyMatchResult
  use arg r, start, end, text

  res = r~matched & r~start == start & r~end == end & r~text == text & r~length == text~length
  -- if this fails, redo the comparisons with trace on to help diagnose the failure
  if \res then do
      -- rerun the compare with tracing so we know what is off
      trace i
      return r~matched & r~start == start & r~end == end & r~text == text & r~length == text~length
  end
  return .true

-- utilty method to verify a nomatch result
::method verifyNoMatchResult
  use arg r, text

  res = \r~matched & r~start == 0 & r~end == 0 & r~text == "" & r~length == 0
  -- if this fails, redo the comparisons with trace on to help diagnose the failure
  if \res then do
      -- rerun the compare with tracing so we know what is off
      trace i
      return \r~matched & r~start == 0 & r~end == 0 & r~text == "" & r~length == 0
  end
  return .true


-- positive tests of the Parser class
::method testParser

  td = self~td("Now is the time")
  parser = .Parser~new(td)

  -- check initial state, which is the entire string
  -- is ready to be chopped up
  self~VerifyParserState(parser, 1, 1, "", 1, td~length + 1, td~string, "")

  self~assertFalse(parser~find('THE'))

  -- nothing should move
  self~VerifyParserState(parser, 1, 1, "", 1, td~length + 1, td~string, "")

  -- get a match that actually creates movement
  self~assertTrue(parser~find('the'))
  self~VerifyParserState(parser, 8, 11, "the", 1, 8, "Now is ", " time")

  -- an unsuccessful move leaves everything the same
  self~assertFalse(parser~find('xyzzy'))
  self~VerifyParserState(parser, 8, 11, "the", 1, 8, "Now is ", " time")

  -- now a second match move
  self~assertTrue(parser~find('im'))
  self~VerifyParserState(parser, 13, 15, "im", 11, 13, " t", "e")

  -- now repeat using a caseless search
  parser = .Parser~new(self~td("Now is the time"))

  self~assertTrue(parser~caselessFind('THE'))
  self~VerifyParserState(parser, 8, 11, "the", 1, 8, "Now is ", " time")
  self~assertTrue(parser~caselessFind('Im'))
  self~VerifyParserState(parser, 13, 15, "im", 11, 13, " t", "e")

  -- equivalent to above but using the regex methods instead. This
  -- uses just a simple search pattern that is equivalent to just a search,
  -- performs the search using a compiled regex expression

  parser = .Parser~new(td)

  -- check initial state, which is the entire string
  -- is ready to be chopped up
  self~VerifyParserState(parser, 1, 1, "", 1, td~length + 1, td~string, "")

  self~assertFalse(parser~findRegex('THE'))

  -- nothing should move
  self~VerifyParserState(parser, 1, 1, "", 1, td~length + 1, td~string, "")

  -- get a match that actually creates movement
  self~assertTrue(parser~findRegex('the'))
  self~VerifyParserState(parser, 8, 11, "the", 1, 8, "Now is ", " time")

  -- an unsuccessful move leaves everything the same
  self~assertFalse(parser~findRegex('xyzzy'))
  self~VerifyParserState(parser, 8, 11, "the", 1, 8, "Now is ", " time")

  -- now a second match move
  self~assertTrue(parser~findRegex('im'))
  self~VerifyParserState(parser, 13, 15, "im", 11, 13, " t", "e")

  -- repeat the regex using pre compiled patterns instead
  parser = .Parser~new(td)

  -- check initial state, which is the entire string
  -- is ready to be chopped up
  self~VerifyParserState(parser, 1, 1, "", 1, td~length + 1, td~string, "")

  self~assertFalse(parser~findRegex(.pattern~compile('THE')))

  -- nothing should move
  self~VerifyParserState(parser, 1, 1, "", 1, td~length + 1, td~string, "")

  -- get a match that actually creates movement
  self~assertTrue(parser~findRegex(.pattern~compile('the')))
  self~VerifyParserState(parser, 8, 11, "the", 1, 8, "Now is ", " time")

  -- an unsuccessful move leaves everything the same
  self~assertFalse(parser~findRegex(.pattern~compile('xyzzy')))
  self~VerifyParserState(parser, 8, 11, "the", 1, 8, "Now is ", " time")

  -- now a second match move
  self~assertTrue(parser~findRegex(.pattern~compile('im')))
  self~VerifyParserState(parser, 13, 15, "im", 11, 13, " t", "e")

  -- now some relative movements
  parser = .Parser~new(td)
  parser~move(1)

  self~VerifyParserState(parser, 2, 2, "", 1, 2, "N", td~string~substr(2))
  -- step forward a little more
  parser~move(4)
  self~VerifyParserState(parser, 6, 6, "", 2, 6, "ow i", td~string~substr(6))
  -- backwards movement...the section will be the portion in the back movement
  parser~move(-2)
  self~VerifyParserState(parser, 4, 4, "", 4, 6, " i", td~string~substr(6))
  -- move negatively beyond the end just goes to the beginning
  parser~move(-4)
  self~VerifyParserState(parser, 1, 1, "", 1, 4, "Now", td~string~substr(4))
  -- now move beyond the end of the string
  parser~move(50)
  self~VerifyParserState(parser, 16, 16, "", 1, 16, "Now is the time", "")

  -- now the samething using absolute positioning
  parser = .Parser~new(td)
  parser~moveTo(2)

  self~VerifyParserState(parser, 2, 2, "", 1, 2, "N", td~string~substr(2))
  -- step forward a little more
  parser~moveTo(6)
  self~VerifyParserState(parser, 6, 6, "", 2, 6, "ow i", td~string~substr(6))
  -- backwards movement...the section will be the portion in the back movement
  parser~moveTo(4)
  self~VerifyParserState(parser, 4, 4, "", 4, 6, " i", td~string~substr(6))
  -- zero is not valid so we'll just reset to column 1
  parser~moveTo(1)
  self~VerifyParserState(parser, 1, 1, "", 1, 4, "Now", td~string~substr(4))
  -- now move beyond the end of the string
  parser~moveTo(50)
  self~VerifyParserState(parser, 16, 16, "", 1, 16, "Now is the time", "")

-- positive tests of the PatternLibrary class
::method testPatternLibrary

  l1 = .PatternLibrary~new
  l1~addFamily("foo", "bar")
  l1~addFamily("vowels", "aeiou")
  l1~addPattern("foo", "xyzzy")

  self~assertEquals("bar", l1~resolveFamily("foo"))
  self~assertNull(l1~resolveFamily("FooBar"))

  p1 = l1~resolvePattern("foo")
  self~assertIsa(p1, .Pattern)

-- tests of doing replacements with the parser
::method testReplacement

  td = self~td("Now is the time")
  parser = .Parser~new(td)

  -- get a match that actually creates movement
  self~assertTrue(parser~find('the'))
  self~VerifyParserState(parser, 8, 11, "the", 1, 8, "Now is ", " time")

  -- replaces with a similar size string
  self~replace("THE")
  self~VerifyParserState(parser, 8, 11, "THE", 1, 8, "Now is ", " time")
