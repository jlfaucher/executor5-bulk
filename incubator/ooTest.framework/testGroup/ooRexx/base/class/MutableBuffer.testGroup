#!/usr/bin/rexx
/*
   name:             MutableBuffer.testGroup
   author:           Moritz Hoffmann
   date:             2007-10-10
   version:          1

-- line commented lines are ignored, when building the directory of infos from this header
   changed:          2007-10-10, -- MH, initial release, copied from Mutable-
            BufferSection class
   languageLevel:    6.02
   purpose:          Test the methods of the class MutableBuffer.
   license:          CPL 1.0 (Common Public License v1.0, see below)
   link:
*/
/*----------------------------------------------------------------------------*/
/*                                                                            */
/* Copyright (c) 2005 Rexx Language Association. All rights reserved.         */
/*                                                                            */
/* This program and the accompanying materials are made available under       */
/* the terms of the Common Public License v1.0 which accompanies this         */
/* distribution. A copy is also available at the following address:           */
/* http://www.ibm.com/developerworks/oss/CPLv1.0.htm                          */
/*                                                                            */
/* Redistribution and use in source and binary forms, with or                 */
/* without modification, are permitted provided that the following            */
/* conditions are met:                                                        */
/*                                                                            */
/* Redistributions of source code must retain the above copyright             */
/* notice, this list of conditions and the following disclaimer.              */
/* Redistributions in binary form must reproduce the above copyright          */
/* notice, this list of conditions and the following disclaimer in            */
/* the documentation and/or other materials provided with the distribution.   */
/*                                                                            */
/* Neither the name of Rexx Language Association nor the names                */
/* of its contributors may be used to endorse or promote products             */
/* derived from this software without specific prior written permission.      */
/*                                                                            */
/* THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS        */
/* "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT          */
/* LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS          */
/* FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT   */
/* OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,      */
/* SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED   */
/* TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA,        */
/* OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY     */
/* OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING    */
/* NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS         */
/* SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.               */
/*                                                                            */
/*----------------------------------------------------------------------------*/
  parse source . . fileSpec

  lines = .array~new
  do i = 1 until lines[i] = "*/"
    lines[i] = sourceline(i)
  end

  group = .TestGroup~new(fileSpec, lines)
  group~add(.MutableBuffer.testGroup)

  if group~isAutomatedTest then return group

  testResult = group~suite~run~~print

return testResult
-- End of entry point.

::requires 'ooTest.frm'     -- load the ooRexxUnit classes

::class "MutableBuffer.testGroup" subclass ooTestCase public

::method "testInstance1"
   b = .nil
   b = .mutableBuffer~new("1234")
   self~assertNotNull("Failed to get instance", b)
   self~assertEquals("Wrong data contained","1234",b~string)
   self~assertEquals("Wrong length",4,b~length)
::method "testInstance2"
   self~expectSyntax(93.938)
   b = .mutableBuffer~new(.nil)
::method "testInstance3"
   self~expectSyntax(93.938)
   b = .mutableBuffer~new("",.nil)
::method "testInstance4"
   self~expectSyntax(93.938)
   b = .mutableBuffer~new(.nil,.nil)
::method "testInstance5"
   self~expectSyntax(93.923)
   b = .mutableBuffer~new("",-1)
::method "testInstance6"
   b = .mutableBuffer~new(123,1)
   self~AssertEquals("wrong String value", b~string, 123)

::METHOD "testAppend"
   b = .mutableBuffer~new("123")
   self~AssertEquals("Length incorrect before",3,b~length)
   b~append("abc")
   self~AssertEquals("Append failed 1","123abc",b~string)
   self~AssertEquals("Length incorrect after",6,b~length)
::METHOD "testAppendSyntax1"
   b = .mutableBuffer~new
   self~expectSyntax(93.903)
   b~append()
::METHOD "testAppendSyntax2"
   b = .mutableBuffer~new
   self~expectSyntax(93.938)
   b~append(.nil)

::METHOD "testInsert"
   b = .mutableBuffer~new("123456")
   self~AssertEquals("Length incorrect before",6,b~length)
   b~insert("abc")
   self~AssertEquals("Append failed 1","abc123456",b~string)
   b~insert("abc",7,,"+")
   self~AssertEquals("Append failed 2","abc1234abc56",b~string)
   b~insert("abc",1,0,"+")
   self~AssertEquals("Append failed 3","abc1234abc56",b~string)
   self~AssertEquals("Length incorrect after",12,b~length)

::METHOD "testInsertSyntax1"
   b = .mutableBuffer~new("123456")
   self~expectSyntax(93.903)
   b~insert()
::METHOD "testInsertSyntax2"
   b = .mutableBuffer~new("123456")
   self~expectSyntax(93.923)
   b~insert("",-1)
::METHOD "testInsertSyntax3"
   b = .mutableBuffer~new("123456")
   self~expectSyntax(93.938)
   b~insert(.nil)
::METHOD "testInsertSyntax4"
   b = .mutableBuffer~new("123456")
   self~expectSyntax(93.938)
   b~insert("",,,.nil)
::METHOD "testInsertSyntax5"
   b = .mutableBuffer~new("123456")
   self~expectSyntax(93.938)
   b~insert("",.nil,.nil,.nil)
::METHOD "testInsertSyntax6"
   b = .mutableBuffer~new("123456")
   self~expectSyntax(93.923)
   b~insert("",0,-1,0)
::METHOD "testInsertSyntax7"
   b = .mutableBuffer~new("123456")
   self~expectSyntax(93.922)
   b~insert("",0,11,"  ")


::METHOD "testOverlay"
   b = .mutableBuffer~new("1234567890")
   self~AssertEquals("Length incorrect before",10,b~length)
   b~overlay("abc")
   self~AssertEquals("Overlay failed 1","abc4567890",b~string)
   b~overlay("abcABCabc")
   self~AssertEquals("Overlay failed 2", "abcABCabc0",b~string)
   b~overlay("abcABCabc",2)
   self~AssertEquals("Overlay failed 3", "aabcABCabc",b~string)
   b~overlay("abc",1,0,"+")
   self~AssertEquals("Overlay failed 4", "aabcABCabc",b~string)
   b~overlay("abc",12,4,"+")
   self~AssertEquals("Overlay failed 4", "aabcABCabc+abc+",b~string)
   self~AssertEquals("Length incorrect after",15,b~length)
::METHOD "testOverlaySyntax1"
   b = .mutableBuffer~new("123456")
   self~expectSyntax(93.903)
   b~overlay()
::METHOD "testOverlaySyntax2"
   b = .mutableBuffer~new("123456")
   self~expectSyntax(93.924)
   b~overlay("",-1)
::METHOD "testOverlaySyntax3"
   b = .mutableBuffer~new("123456")
   self~expectSyntax(93.938)
   b~overlay(.nil)
::METHOD "testOverlaySyntax4"
   b = .mutableBuffer~new("123456")
   self~expectSyntax(93.938)
   b~overlay("",,,.nil)
::METHOD "testOverlaySyntax5"
   b = .mutableBuffer~new("123456")
   self~expectSyntax(93.938)
   b~overlay("",.nil,.nil,.nil)
::METHOD "testOverlaySyntax6"
   b = .mutableBuffer~new("123456")
   self~expectSyntax(93.924)
   b~overlay("",0,-1,0)
::METHOD "testOverlaySyntax7"
   b = .mutableBuffer~new("123456")
   self~expectSyntax(93.922)
   b~overlay("",,10,"  ")

::METHOD "testDelete"
   b = .mutableBuffer~new("1234567890")
   self~AssertEquals("Length incorrect before",10,b~length)
   b~delete(11)
   self~AssertEquals("Delete failed 1","1234567890",b~string)
   b~delete(1,2)
   self~AssertEquals("Delete failed 2","34567890",b~string)
   self~AssertEquals("Length incorrect after",8,b~length)
::METHOD "testDeleteSyntax1"
   b = .mutableBuffer~new("1234567890")
   self~expectSyntax(93.903)
   b~delete()
::METHOD "testDeleteSyntax2"
   b = .mutableBuffer~new("1234567890")
   self~expectSyntax(93.938)
   b~delete(.nil)
::METHOD "testDeleteSyntax3"
   b = .mutableBuffer~new("1234567890")
   self~expectSyntax(93.924)
   b~delete(0)
::METHOD "testDeleteSyntax4"
   b = .mutableBuffer~new("1234567890")
   self~expectSyntax(93.938)
   b~delete(1,.nil)
::METHOD "testDeleteSyntax5"
   b = .mutableBuffer~new("1234567890")
   self~expectSyntax(93.923)
   b~delete(1,-1)

::METHOD "testPos"
   b = .mutableBuffer~new("1234567890")

   self~assertEquals("Wrong position 1",4,b~pos("4"))
   self~assertEquals("Wrong position 2",4,b~pos("4",2))
   self~assertEquals("Wrong position 3",0,b~pos("4",99))
   self~assertEquals("Wrong position 4",0,b~pos("x"))
   self~assertEquals("Wrong position 5",4,b~pos("456"))
::METHOD "testPosSyntax1"
   b = .mutableBuffer~new("1234567890")
   self~expectSyntax(93.903)
   b~pos()

::METHOD "testPosSyntax2"
   b = .mutableBuffer~new("1234567890")
   self~expectSyntax(93.938)
   b~pos(.nil)

::METHOD "testPosSyntax3"
   b = .mutableBuffer~new("1234567890")
   self~expectSyntax(93.924)
   b~pos("1",-1)

::METHOD "testPosSyntax4"
   b = .mutableBuffer~new("1234567890")
   self~expectSyntax(93.938)
   b~pos("1",.nil)

::METHOD "testLastpos"
   b = .mutableBuffer~new("01234567890")

   self~assertEquals("Wrong position 1",5,b~lastpos("4"))
   self~assertEquals("Wrong position 2",6,b~lastpos("56"))
   self~assertEquals("Wrong position 3",0,b~lastpos("6",2))
   self~assertEquals("Wrong position 4",9,b~lastpos("8",99))
   self~assertEquals("Wrong position 5",0,b~lastpos("x",99))
::METHOD "testLastPosSyntax1"
   b = .mutableBuffer~new("1234567890")
   self~expectSyntax(93.903)
   b~lastPos()

::METHOD "testLastPosSyntax2"
   b = .mutableBuffer~new("1234567890")
   self~expectSyntax(93.938)
   b~lastPos(.nil)

::METHOD "testLastPosSyntax3"
   b = .mutableBuffer~new("1234567890")
   self~expectSyntax(93.924)
   b~lastPos("1",-1)

::METHOD "testLastPosSyntax4"
   b = .mutableBuffer~new("1234567890")
   self~expectSyntax(93.938)
   b~lastPos("1",.nil)

::METHOD "testSubChar"
   b = .mutableBuffer~new("1234567890")

   self~assertEquals("Wrong char 1",4,b~subchar(4))
   self~assertEquals("Wrong char 2",,b~subchar(99))
::METHOD "testSubCharSyntax1"
   b = .mutableBuffer~new("1234567890")
   self~expectSyntax(93.903)
   b~subChar()

::METHOD "testSubCharSyntax2"
   b = .mutableBuffer~new("1234567890")
   self~expectSyntax(93.924)
   b~subChar(0)

::METHOD "testSubCharSyntax3"
   b = .mutableBuffer~new("1234567890")
   self~expectSyntax(93.938)
   b~subChar(.nil)

::METHOD "testSubCharSyntax4"
   b = .mutableBuffer~new("1234567890")
   self~expectSyntax(93.905)
   b~subChar("a")

::METHOD "testSubStr"
   b = .mutableBuffer~new("1234567890")

   self~assertEquals("Wrong string 1","234567890",b~substr(2))
   self~assertEquals("Wrong string 2","1",b~substr(1,1))
   self~assertEquals("Wrong string 3","4",b~substr(4,1,"#"))
   self~assertEquals("Wrong string 4","0#",b~substr(10,2,"#"))
::METHOD "testSubStrSyntax1"
   b = .mutableBuffer~new("1234567890")
   self~expectSyntax(93.903)
   b~subStr()

::METHOD "testSubStrSyntax2"
   b = .mutableBuffer~new("1234567890")
   self~expectSyntax(93.938)
   b~subStr(.nil)

::METHOD "testSubStrSyntax3"
   b = .mutableBuffer~new("1234567890")
   self~expectSyntax(93.924)
   b~subStr(0)

::METHOD "testSubStrSyntax4"
   b = .mutableBuffer~new("1234567890")
   self~expectSyntax(93.938)
   b~subStr(.nil,0)

::METHOD "testSubStrSyntax5"
   b = .mutableBuffer~new("1234567890")
   self~expectSyntax(93.938)
   b~subStr(1,.nil)

::METHOD "testSubStrSyntax6"
   b = .mutableBuffer~new("1234567890")
   self~expectSyntax(93.923)
   b~subStr(1,-1)

::METHOD "testSubStrSyntax7"
   b = .mutableBuffer~new("1234567890")
   self~expectSyntax(93.938)
   b~subStr(1,1,.nil)

::METHOD "testSubStrSyntax8"
   b = .mutableBuffer~new("1234567890")
   self~expectSyntax(93.922)
   b~subStr(1,1,"  ")
::METHOD "testGetBufferSize"
   b = .mutableBuffer~new("")
   self~assertEquals("Buffer size incorrect 1",256,b~getBufferSize)
   b = .mutableBuffer~new("",1)
   self~assertEquals("Buffer size incorrect 2",1,b~getBufferSize)
   b~append("  ")
   self~assertEquals("Buffer size incorrect 3",2,b~getBufferSize)

::METHOD "testSetBufferSize"
   b = .mutableBuffer~new("1234567890")
   b~setBufferSize(5)
   self~assertEquals("Buffer not truncated","12345",b~string)
   self~assertEquals("Buffer returning wrong buffer size",5,b~getBufferSize)

   b = .mutablebuffer~new("123")
   b~setBufferSize(0)
   self~assertEquals("MutableBuffer string should be empty string","",b~string)
   self~assertEquals("MutableBuffer buffer size should be 256 after setting to 0",256,b~getBufferSize)

   b = .mutablebuffer~new("My local work")
   b~setBufferSize(109873451)
   self~assertEquals("Set buffer to large size, string should not change","My local work",b~string)
   self~assertEquals("Buffer size should be 109873451 after setting to 109873451",109873451,b~getBufferSize)

   /* This test works (as it should.)  Deferring its inclusion while I think
    * about a mechanism to exclude some tests at run time.  MarkM

   b~setBufferSize(999999999)
   self~assertEquals("Set buffer to large size, string should not change","My local work",b~string)
   self~assertEquals("Buffer size should be 999999999 after setting to 999999999",999999999,b~getBufferSize)
   */

   /* This test does not work.  In StringClassUtil.cpp the get_length() function
    * uses DEFAULT_DIGITS instead of the current numeric digits:
    *
    *   value = REQUIRED_LONG(argument, DEFAULT_DIGITS, position);
    *
    * I want to wait until after 3.2.0 is relesed to discuss it with Rick. MarkM

   numeric digits 11
   b~setBufferSize(2147483647)
   self~assertEquals("Set buffer to vey large size, string should not change","My local work",b~string)
   self~assertEquals("Buffer size should be 2147483647 after setting to 2147483647",2147483647,b~getBufferSize)
   */

::METHOD "testSetBufferSizeSyntax1"
   b = .mutablebuffer~new
   self~expectSyntax(93.903)
   b~setBufferSize()
::METHOD "testSetBufferSizeSyntax2"
   b = .mutablebuffer~new
   self~expectSyntax(93.905)
   b~setBufferSize("")
::METHOD "testSetBufferSizeSyntax3"
   b = .mutablebuffer~new
   self~expectSyntax(93.938)
   b~setBufferSize(.nil)
::METHOD "testSetBufferSizeSyntax4"
   b = .mutablebuffer~new
   self~expectSyntax(93.923)
   b~setBufferSize(-1)

/* This test does not work because of the numeric digits issue discussed several
 * lines above.  MarkM
::METHOD "testSetBufferSizeSyntax5"
   b = .mutablebuffer~new
   numeric digits 11
   self~expectSyntax(93.923)
   b~setBufferSize(-2147483648)
*/
