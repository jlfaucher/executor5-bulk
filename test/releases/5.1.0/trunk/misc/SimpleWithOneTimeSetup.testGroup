#!/usr/bin/env rexx
/*----------------------------------------------------------------------------*/
/*                                                                            */
/* Copyright (c) 2007 - 2018 Rexx Language Association. All rights reserved.  */
/*                                                                            */
/* This program and the accompanying materials are made available under       */
/* the terms of the Common Public License v1.0 which accompanies this         */
/* distribution. A copy is also available at the following address:           */
/* http://www.oorexx.org/license.html                                         */
/*                                                                            */
/* Redistribution and use in source and binary forms, with or                 */
/* without modification, are permitted provided that the following            */
/* conditions are met:                                                        */
/*                                                                            */
/* Redistributions of source code must retain the above copyright             */
/* notice, this list of conditions and the following disclaimer.              */
/* Redistributions in binary form must reproduce the above copyright          */
/* notice, this list of conditions and the following disclaimer in            */
/* the documentation and/or other materials provided with the distribution.   */
/*                                                                            */
/* Neither the name of Rexx Language Association nor the names                */
/* of its contributors may be used to endorse or promote products             */
/* derived from this software without specific prior written permission.      */
/*                                                                            */
/* THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS        */
/* "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT          */
/* LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS          */
/* FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT   */
/* OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,      */
/* SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED   */
/* TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA,        */
/* OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY     */
/* OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING    */
/* NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS         */
/* SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.               */
/*                                                                            */
/*----------------------------------------------------------------------------*/
  parse source . . s
  group = .TestGroup~new(s)

  -- To have set up and tear down run once for an entire set of test cases, all
  -- the test cases have to be added to a single TestSuite.  The test group
  -- will do this for us if we tell it which TestSuite class object goes with
  -- which set of test cases.  To do this we use the addWithSuite() method.
  group~addWithSuite(.SimpleOneTimeSetup, .OneTimeSuite)

  if group~isAutomatedTest then return group

  testResult = group~suite~execute~~print

return testResult
-- End of entry point.

::requires 'ooTest.frm'

/* class: OneTimeSuite - - - - - - - - - - - - - - - - - - - - - - - - - - - -*\
   The SimpleWithSomeSetup.testGroup file has one flaw.  When the tests are
   finished, the .local environment retains the outArray and outQueue objects.

   We fix that with this test suite that puts the objects in .local before any
   tests are run and then removes the objects after all the tests are run.
\* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*/
::class 'OneTimeSuite' public subclass ooTestSuite

::method setUp
  .local~outArray = .array~new
  .local~outQueue = .queue~new

  -- To see this method invoked, remove the comment.
  -- say 'OneTimeSuite::setUp()'

::method tearDown
  .local~setEntry(outArray)
  .local~setEntry(outQueue)

  -- To see this method invoked, remove the comment.
  -- say 'OneTimeSuite::tearDown()'


/* class: SimpleOneTimeSetup - - - - - - - - - - - - - - - - - - - - - - - - -*\

\* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*/
::class "SimpleOneTimeSetup" public subclass ooTestCase

  ::method init class
    forward class (super) continue
    self~ooTestType = self~FRAMEWORK_EXAMPLE_TEST
  -- End init() class

  ::method setUp

    do i = 1 to 50
      .outArray[i] = i
      .outQueue~queue(i)
    end

    -- If you want to execute this test and see the setUp() invocation
    -- un-comment the following:
    -- say 'In SimpleOneTimeSetup.testGroup::setUp()'

  ::method tearDown
    -- There really is nothing to do here.  You could just empty the objects in
    -- set up.  The tear down should just be skipped.  But, for the purpose of
    -- example we have this.
    .outArray~empty
    .outQueue~empty

    -- If you want to execute this test and see the tearDown() invocation
    -- un-comment the following:
    -- say 'In SimpleOneTimeSetup.testGroup::tearDown()'

  -- Now our test methods
  ::method test_YYY
    a = .outArray[6]
    b = .outQueue[6]
    self~assertTrue(a == b)

  ::method  test_XXX
    a = .outArray[18]
    do 3
      .outQueue~pull
    end
    b = .outQueue[18]
    self~assertNotSame(a, b)

  ::method test_ZZZ
    do i = 1 to .outArray~items
      self~assertSame(.outArray[i], .outQueue[i])
    end

-- End of class: SimpleOneTimeSetup
