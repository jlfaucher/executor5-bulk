#!/usr/bin/env rexx
/*----------------------------------------------------------------------------*/
/*                                                                            */
/* Copyright (c) 2021-2024 Rexx Language Association. All rights reserved.    */
/*                                                                            */
/* This program and the accompanying materials are made available under       */
/* the terms of the Common Public License v1.0 which accompanies this         */
/* distribution. A copy is also available at the following address:           */
/* https://www.oorexx.org/license.html                                        */
/*                                                                            */
/* Redistribution and use in source and binary forms, with or                 */
/* without modification, are permitted provided that the following            */
/* conditions are met:                                                        */
/*                                                                            */
/* Redistributions of source code must retain the above copyright             */
/* notice, this list of conditions and the following disclaimer.              */
/* Redistributions in binary form must reproduce the above copyright          */
/* notice, this list of conditions and the following disclaimer in            */
/* the documentation and/or other materials provided with the distribution.   */
/*                                                                            */
/* Neither the name of Rexx Language Association nor the names                */
/* of its contributors may be used to endorse or promote products             */
/* derived from this software without specific prior written permission.      */
/*                                                                            */
/* THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS        */
/* "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT          */
/* LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS          */
/* FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT   */
/* OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,      */
/* SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED   */
/* TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA,        */
/* OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY     */
/* OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING    */
/* NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS         */
/* SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.               */
/*                                                                            */
/*----------------------------------------------------------------------------*/

parse source . . s
group = .TestGroup~new(s)
group~add(.SysTextScreenRead.testGroup)
group~restrictOS("WINDOWS")
if group~isAutomatedTest then
  return group
else
  return group~suite~execute~~print

::requires 'ooTest.frm'
::class SysTextScreenRead.testgroup subclass ooTestCase public

-- Note: we cannot use ::constant for the next two helper methods as these
-- SysXxx functions are restricted to Windows

-- if stdout is redirected, we have no console
::method noConsole
  return SysCurPos()~length = 0
::method rowsConsole
   return SysTextScreenSize()~word(1)

::method test_screenread_args_none
  self~expectSyntax(88.901) -- Missing argument; argument 1 is required
  call SysTextScreenRead

::method test_screenread_args_one
  self~expectSyntax(88.901) -- Missing argument; argument 2 is required
  call SysTextScreenRead 0

::method test_screenread_args_too_many
  self~expectSyntax((88.922, 3)) -- Too many arguments in invocation; 3 expected
  call SysTextScreenRead 0, 0, 0, 0

::method test_screenread_row_negative
  if self~noConsole -- this test requires a console
    then return
  self~expectSyntax((88.907, "row")) -- Argument row must be in the range 0 to ..
  call SysTextScreenRead -1, 0

::method test_screenread_row_too_high
  if self~noConsole -- this test requires a console
    then return
  self~expectSyntax((88.907, "row")) -- Argument row must be in the range 0 to ..
  call SysTextScreenRead self~rowsConsole, 0 -- range is 0 .. rowsConsole - 1

::method test_screenread_col_negative
  if self~noConsole -- this test requires a console
    then return
  self~expectSyntax((88.907, "col")) -- Argument col must be in the range 0 to ..
  call SysTextScreenRead 0, -1

::method test_screenread_col_too_high
  if self~noConsole -- this test requires a console
    then return
  self~expectSyntax((88.907, "col")) -- Argument row must be in the range 0 to ..
  call SysTextScreenRead 0, self~rowsConsole -- range is 0 .. rowsConsole - 1

::method test_screenread_len_negative
  if self~noConsole -- this test requires a console
    then return
  self~expectSyntax((88.907, 3)) -- Argument 3 must be in the range 0 to ..
  call SysTextScreenRead 0, 0, -1

::method test_screenread_null
  if self~noConsole -- the following tests require a console
    then return
  self~assertSame("", SysTextScreenRead(0, 0, 0))  -- zero length
  parse value SystextScreenSize() with rows cols
  self~assertSame("", SysTextScreenRead(rows - 1, cols - 1, 0))

::method test_screenread
  if self~noConsole -- the following tests require a console
    then return
  parse value SystextScreenSize() with rows cols
  lastRow = rows - 1
  lastCol = cols - 1

  -- note that these tests may fail with slightly larger lengths
  -- returned when Unicode symbols are read from screen
  -- (this is especially true for the full-length reads which may
  -- return many thousands of screen buffer lines)
  msg = "this may fail with a slightly larger length when Unicode symbols are read"

  -- no length specified
  self~assertSame(1, SysTextScreenRead(lastRow, lastCol)~length, msg)
  self~assertSame(2, SysTextScreenRead(lastRow, lastCol - 1)~length, msg)
  self~assertSame(11, SysTextScreenRead(lastRow, lastCol - 10)~length, msg)
  self~assertSame(cols, SysTextScreenRead(lastRow, 0)~length, msg)
  self~assertSame(cols - 1, SysTextScreenRead(lastRow, 1)~length, msg)
  self~assertSame(cols + 1, SysTextScreenRead(lastRow - 1, lastCol)~length, msg)
  self~assertSame(rows * cols, SysTextScreenRead(0, 0)~length, msg)
  self~assertSame(rows * cols - 3 * cols - 5, SysTextScreenRead(3, 5)~length, msg)

  -- length specified
  self~assertSame(1, SysTextScreenRead(0, 0, 1)~length, msg)
  self~assertSame(10, SysTextScreenRead(0, 0, 10)~length, msg)
  self~assertSame(100, SysTextScreenRead(9, 9, 100)~length, msg)
  self~assertSame(rows * cols - 1, SysTextScreenRead(0, 0, rows * cols - 1)~length, msg)
  self~assertSame(rows * cols, SysTextScreenRead(0, 0, rows * cols)~length, msg)


::options all syntax
