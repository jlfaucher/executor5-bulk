#!/usr/bin/rexx
/*
   name:             ooRexx.Base.Assignments.testUnit
   author:           Rony G. Flatscher
   date:             2007-06-16
   version:          1.0.0
   changed:          ---

-- line commented lines are ignored, when building the directory of infos from this header

   languageLevel:    6.0
   purpose:          Test assignments
   remark:           Initially uses documentation examples only


   license:          CPL 1.0 (Common Public License v1.0, see below)
   link:

   category1:        ooRexx
   category2:        Base
   category3:        Assignments
*/
/*----------------------------------------------------------------------------*/
/*                                                                            */
/* Copyright (c) 2007 Rexx Language Association. All rights reserved.         */
/*                                                                            */
/* This program and the accompanying materials are made available under       */
/* the terms of the Common Public License v1.0 which accompanies this         */
/* distribution. A copy is also available at the following address:           */
/* http://www.ibm.com/developerworks/oss/CPLv1.0.htm                          */
/*                                                                            */
/* Redistribution and use in source and binary forms, with or                 */
/* without modification, are permitted provided that the following            */
/* conditions are met:                                                        */
/*                                                                            */
/* Redistributions of source code must retain the above copyright             */
/* notice, this list of conditions and the following disclaimer.              */
/* Redistributions in binary form must reproduce the above copyright          */
/* notice, this list of conditions and the following disclaimer in            */
/* the documentation and/or other materials provided with the distribution.   */
/*                                                                            */
/* Neither the name of Rexx Language Association nor the names                */
/* of its contributors may be used to endorse or promote products             */
/* derived from this software without specific prior written permission.      */
/*                                                                            */
/* THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS        */
/* "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT          */
/* LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS          */
/* FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT   */
/* OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,      */
/* SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED   */
/* TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA,        */
/* OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY     */
/* OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING    */
/* NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS         */
/* SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.               */
/*                                                                            */
/*----------------------------------------------------------------------------*/



-------------------------------------------------------------------------------------------
-- ===> adapt the "testUnitList" to your testCase classes; each element in the list is <===
-- ===> an array object, the first element containing the testCase class object, the   <===
-- ===> second element is a list of test method names which are regarded to be         <===
-- ===> mandatory (if the list remains empty all test methods are mandatory)           <===

   /* list of array objects, each containing the testUnit class object and an
      optional list of mandatory test case methods name                       */

mandatoryTestMethods=.list~new   -- no mandatory tests for this testCase class
testUnitList=.list~of( .array~of(.ooRexx.Base.Assignments.testUnit,  mandatoryTestMethods) )


-------------------------------------------------------------------------------------------
-- ===> the following code needs not to be individualized                              <===

   -- read top comment, containing infos about this program
arrLines=.array~new
do i=1 to 150 until arrLines[i]="*/"
   arrLines[i]=sourceline(i)
end
      -- supply information for the testClass(es) in this file; the class attribute
      -- "TestCaseInfo" (a directory object, index points to a queue) will store
      -- the parsed infos
aTestUnitClass=testUnitList~at(testUnitList~first)[1] -- get first testClass

   -- will parse the array lines and store result in class object
call makeDirTestInfo aTestUnitClass, arrLines
tmpDir=aTestUnitClass~TestCaseInfo
parse source s   -- op_sys invocationType fullPathToThisFile
tmpDir~setentry("test_Case-source", s)

   -- now add this directory to other testCase classes, if any left
do arr over testUnitList
   if arr[1]=aTestUnitClass then iterate  -- already handled
   arr[1]~TestCaseInfo=tmpDir             -- save info in class object
end

-- if this file is CALLed or REQUIRED then define an entry "bRunTestLocally" in .local
-- and set it to .false; this way the independent local invocation of the tests is inhibited
if .local~hasentry("bRunTestsLocally")=.false then
   .local~bRunTestsLocally=.true -- if this file is executed directly, then run tests for debugging

if .bRunTestsLocally=.true then  -- run ALL tests in this test unit
do
   ts=.testSuite~new             -- create a testSuite
   do arr over testUnitList
      -- create a testSuite for the given test case class, use all its testmethods
      ts~addTest( .testSuite~new(arr[1]))
   end
   -- testResult=.testSuite~new(testUnitClass)~run
   testResult=ts~run       -- now run all the tests

   call simpleDumpTestResults testResult
end

   /* return list of array objects containing test case classes and
      optionally list of mandatory test methods                      */
return testUnitList



::requires ooRexxUnit.cls     -- load the ooRexxUnit classes

::class "ooRexx.Base.Assignments.testUnit" subclass TestCase public



::method "test_+="

  i1=0
  i2=0
  do i=-5 to +5 by 0.1
     i1 =i1+1.01+i
     i2+=   1.01+i
  end
  self~assertEquals("subtest_01", i1, i2)

  i3=0
  i3+=10
  self~assertEquals("subtest_02", 10, i3)


::method "test_-="

  i1=0
  i2=0
  do i=-5 to +5 by 0.1
     i1 =i1-1.01-i
     i2-=   1.01-i
  end
  self~assertEquals("subtest_01", i1, i2)

  i3=0
  i3-=10
  self~assertEquals("subtest_02", -10, i3)


::method "test_/="

  i1=1
  i2=1
  do i=-5 to +5 by 0.1
     i1 =i1/(1.01+i)
     i2/=   (1.01+i)
  end
  self~assertEquals("subtest_01", i1, i2)

  i3=13
  i3/=10
  self~assertEquals("subtest_02", 1.3, i3)


::method "test_//="

  i1=13
  i2=13
  i1 =i1//(1.01+2)
  i2//=   (1.01+2)
  self~assertEquals("subtest_01", i1, i2)

  i3=13
  i3//=10
  self~assertEquals("subtest_02", 3, i3)



::method "test_%="

  i1=13
  i2=13
  i1 =i1%(1.01+2)
  i2%=   (1.01+2)
  self~assertEquals("subtest_01", i1, i2)

  i3=13
  i3%=10
  self~assertEquals("subtest_02", 1, i3)


::method "test_*="

  i1=13
  i2=13
  i1 =i1*(1.01+2)
  i2*=   (1.01+2)
  self~assertEquals("subtest_01", i1, i2)

  i3=13
  i3*=10
  self~assertEquals("subtest_02", 130, i3)


::method "test_**="

  i1=13
  i2=13
  i1 =i1**(1+2)
  i2**=   (1+2)
  self~assertEquals("subtest_01", i1, i2)

  i3=3
  i3**=4
  self~assertEquals("subtest_02", 81, i3)


::method "test_|="

  i1=.false
  i2=.false
  i1 =i1|.true
  i2|=   .true
  self~assertEquals("subtest_01", i1, i2)


::method "test_&="

  i1=.false
  i2=.false
  i1 =i1&.true
  i2&=   .true
  self~assertEquals("subtest_01", i1, i2)


::method "test_&&="

  i1=.false
  i2=.false
  i1 =i1&&.true
  i2&&=   .true
  self~assertEquals("subtest_01", i1, i2)


::method "test_||="

  i1=.false
  i2=.false
  i1 =i1||.true
  i2||=   .true
  self~assertEquals("subtest_01", i1, i2)

