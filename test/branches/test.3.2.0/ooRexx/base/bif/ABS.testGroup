#!/usr/bin/rexx
/*
   name:             String.testGroup
   author:           W. David Ashley
   date:             2007-12-05
   version:          1.0.1

-- line commented lines are ignored, when building the directory of infos from this header
   changed:          2007-12-05, ---wda, initial creation

   languageLevel:    6.02
   purpose:          Test the ABS functions
   remark:           Initial test unit for demonstration purposes, needs to be completed.

   license:          CPL 1.0 (Common Public License v1.0, see below)
   link:

   category1:        ooRexx
   category2:        Base
   category3:        BIF
*/
/*----------------------------------------------------------------------------*/
/*                                                                            */
/* Copyright (c) 2005-2008 Rexx Language Association. All rights reserved.    */
/*                                                                            */
/* This program and the accompanying materials are made available under       */
/* the terms of the Common Public License v1.0 which accompanies this         */
/* distribution. A copy is also available at the following address:           */
/* http://www.ibm.com/developerworks/oss/CPLv1.0.htm                          */
/*                                                                            */
/* Redistribution and use in source and binary forms, with or                 */
/* without modification, are permitted provided that the following            */
/* conditions are met:                                                        */
/*                                                                            */
/* Redistributions of source code must retain the above copyright             */
/* notice, this list of conditions and the following disclaimer.              */
/* Redistributions in binary form must reproduce the above copyright          */
/* notice, this list of conditions and the following disclaimer in            */
/* the documentation and/or other materials provided with the distribution.   */
/*                                                                            */
/* Neither the name of Rexx Language Association nor the names                */
/* of its contributors may be used to endorse or promote products             */
/* derived from this software without specific prior written permission.      */
/*                                                                            */
/* THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS        */
/* "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT          */
/* LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS          */
/* FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT   */
/* OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,      */
/* SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED   */
/* TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA,        */
/* OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY     */
/* OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING    */
/* NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS         */
/* SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.               */
/*                                                                            */
/*----------------------------------------------------------------------------*/



-------------------------------------------------------------------------------------------
-- ===> adapt the "testGroupList" to your testCase classes; each element in the list is <===
-- ===> an array object, the first element containing the testCase class object, the   <===
-- ===> second element is a list of test method names which are regarded to be         <===
-- ===> mandatory (if the list remains empty all test methods are mandatory)           <===

   /* list of array objects, each containing the testGroup class object and an
      optional list of mandatory test case methods name                       */

mandatoryTestMethods=.list~new   -- no mandatory tests for this testCase class
testGroupList=.list~of( .array~of(.ABS.testGroup,  mandatoryTestMethods) )


-------------------------------------------------------------------------------------------
-- ===> the following code needs not to be individualized                              <===

   -- read top comment, containing infos about this program
arrLines=.array~new
do i=1 to 150 until arrLines[i]="*/"
   arrLines[i]=sourceline(i)
end
      -- supply information for the testClass(es) in this file; the class attribute
      -- "TestCaseInfo" (a directory object, index points to a queue) will store
      -- the parsed infos
aTestGroupClass=testGroupList~at(testGroupList~first)[1] -- get first testClass

   -- will parse the array lines and store result in class object
call makeDirTestInfo aTestGroupClass, arrLines
tmpDir=aTestGroupClass~TestCaseInfo
parse source s   -- op_sys invocationType fullPathToThisFile
tmpDir~setentry("test_Case-source", s)

   -- now add this directory to other testCase classes, if any left
do arr over testGroupList
   if arr[1]=aTestGroupClass then iterate  -- already handled
   arr[1]~TestCaseInfo=tmpDir             -- save info in class object
end

-- if this file is CALLed or REQUIRED then define an entry "bRunTestLocally" in .local
-- and set it to .false; this way the independent local invocation of the tests is inhibited
if .local~hasentry("bRunTestsLocally")=.false then
   .local~bRunTestsLocally=.true -- if this file is executed directly, then run tests for debugging

if .bRunTestsLocally=.true then  -- run ALL tests in this test unit
do
   ts=.testSuite~new             -- create a testSuite
   do arr over testGroupList
      -- create a testSuite for the given test case class, use all its testmethods
      ts~addTest( .testSuite~new(arr[1]))
   end
   -- testResult=.testSuite~new(testGroupClass)~run
   testResult=ts~run       -- now run all the tests

   call simpleDumpTestResults testResult
end

   /* return list of array objects containing test case classes and
      optionally list of mandatory test methods                      */
return testGroupList



::requires ooRexxUnit.cls     -- load the ooRexxUnit classes

::class "ABS.testGroup" subclass TestCase public

   -- test the ABS BIF
::method "test_ABS"

   a=11111111
   b=-33333333
   n=-33333333
   self~assertSame("subTest1", ABS(5555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555), '5.55555556E+249')
   self~assertSame("subTest2", ABS(-99999%3), 33333)
   self~assertSame("subTest3", ABS(5555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555555/3), 1.85185185E+249)
   self~assertSame("subTest4", ABS(003), '3')
   self~assertSame("subTest5", ABS(-000003), '3')
   self~assertSame("subTest6", ABS(-.003E17), '3E+14')
   self~assertSame("subTest7", ABS(.03E2), '3')
   self~assertSame("subTest8", ABS(copies(' ',1000)'   3  '), '3')
   self~assertSame("subTest9", ABS('   3  '), '3')
   self~assertSame("subTest10", ABS(' + 31 '), '31')
   self~assertSame("subTest11", ABS(' - 31 '), '31')
   self~assertSame("subTest12", ABS( 0 + 31 ), '31')
   self~assertSame("subTest13", ABS( 0 + '  31 ' ), '31')
   -- subTest14 is in its own method
   self~assertSame("subTest15", ABS('300  '), '300')
   self~assertSame("subTest16", ABS('          300'), '300')
   self~assertSame("subTest17", ABS('2B303033'x), '3')
   self~assertSame("subTest18", ABS(a), '11111111')
   self~assertSame("subTest19", ABS(a+b), '22222222')
   self~assertSame("subTest20", "ABS"(2-9), '7')
   self~assertSame("subTest21", ABS('-'||2||'.'||'E+3'), '2000')
   -- subTest22 is in its own method
   self~assertSame("subTest23", 'ABS'('-4.2'), '4.2')
   self~assertSame("subTest24", '414253'x( -4.2), '4.2')
   Numeric Digits 5
   Numeric Form Engineering
   self~assertSame("subTest25", ABS(n), '33.333E+6')
   self~assertSame("subTest26", ABS(-n), '33.333E+6')
   self~assertSame("subTest27", "ABS"(1E-0234567890), 10E-234567891)
   self~assertSame("subTest28", "ABS"(10.5e12-1389004007), '10.499E+12')
   -- subTest29 is in its own method
   -- subTest30 is in its own method
   -- subTest31 is in its own method
   -- subTest32 is in its own method
   stem.=-18.000000000000000004
   self~assertSame("subTest33", ABs(stem.four ), '18.000')
   Numeric Digits 10000
   li=Copies('6',8000)
   self~assertSame("subTest34", ABs(li), copies('6', 8000))
   self~assertSame("subTest35", ABs('12.3'), '12.3')
   self~assertSame("subTest36", ABs('-0.307'), '0.307')
   self~assertSame("subTest37", 0, ABS(' -0'))
   self~assertSame("subTest38", 0, ABS(' +0'))
   self~assertSame("subTest39", 0, ABS(' - 0 '))
   self~assertSame("subTest40", 0, ABS(' + 0 '))
   self~assertSame("subTest41", 1, ABS(' -1'))
   self~assertSame("subTest42", 1, ABS(' +1'))
   self~assertSame("subTest43", 1, ABS(' - 1 '))
   self~assertSame("subTest44", 1, ABS(' + 1 '))

   -- rexxref documentation tests
   self~assertSame("subTest101", 12.3, ABS('12.3'))
   self~assertSame("subTest102", 0.307, ABS(' -0.307'))


   -- test the ABS BIF
::method "test_ABS_subTest14"
   self~expectSyntax(93.943)
   self~assertSame("subTest14", ABS(' 0 + 31 '), '31')

   -- test the ABS BIF
::method "test_ABS_subTest22"
   self~expectSyntax(93.943)
   self~assertSame("subTestss", ABS('a'), 'a')

   -- test the ABS BIF
::method "test_ABS_subTest29"
   self~expectSyntax(93.943)
   self~assertSame("subTestss", ABS(1E1234567890), 1)

   -- test the ABS BIF
::method "test_ABS_subTest30"
   self~expectSyntax(93.943)
   self~assertSame("subTestss", ABS(novalue), 1)

   -- test the ABS BIF
::method "test_ABS_subTest31"
   self~expectSyntax(93.943)
   three = 3
   self~assertSame("subTestss", ABS(stem.three), 3)

   -- test the ABS BIF
::method "test_ABS_subTest32"
   self~expectSyntax(93.943)
   self~assertSame("subTestss", ABS(invalid), 3)





