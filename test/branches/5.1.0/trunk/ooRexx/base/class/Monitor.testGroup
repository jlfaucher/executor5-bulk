#!/usr/bin/env rexx
/*----------------------------------------------------------------------------*/
/*                                                                            */
/* Copyright (c) 2007-2022 Rexx Language Association. All rights reserved.    */
/*                                                                            */
/* This program and the accompanying materials are made available under       */
/* the terms of the Common Public License v1.0 which accompanies this         */
/* distribution. A copy is also available at the following address:           */
/* https://www.oorexx.org/license.html                                        */
/*                                                                            */
/* Redistribution and use in source and binary forms, with or                 */
/* without modification, are permitted provided that the following            */
/* conditions are met:                                                        */
/*                                                                            */
/* Redistributions of source code must retain the above copyright             */
/* notice, this list of conditions and the following disclaimer.              */
/* Redistributions in binary form must reproduce the above copyright          */
/* notice, this list of conditions and the following disclaimer in            */
/* the documentation and/or other materials provided with the distribution.   */
/*                                                                            */
/* Neither the name of Rexx Language Association nor the names                */
/* of its contributors may be used to endorse or promote products             */
/* derived from this software without specific prior written permission.      */
/*                                                                            */
/* THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS        */
/* "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT          */
/* LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS          */
/* FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT   */
/* OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,      */
/* SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED   */
/* TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA,        */
/* OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY     */
/* OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING    */
/* NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS         */
/* SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.               */
/*                                                                            */
/*----------------------------------------------------------------------------*/
parse source . . fileSpec
group = .TestGroup~new(fileSpec)
group~add(.Monitor.testGroup)
if group~isAutomatedTest then
  return group
else
  return group~suite~execute~~print

::requires 'ooTest.frm' -- load the ooRexxUnit classes

::class Monitor.testGroup subclass ooTestCase public

::method test_current_one_arg
  self~expectSyntax((93.902, 0)) -- Too many arguments in invocation of method; 0 expected
  .Monitor~new~current(.nil)

::method test_destination_two_args
  self~expectSyntax((93.902, 1)) -- Too many arguments in invocation of method; 1 expected
  .Monitor~new~destination(.nil, .nil)

::method test_new_two_args
  self~expectSyntax((93.902, 1)) -- Too many arguments in invocation of method; 1 expected
  .Monitor~new(.nil, .nil)

::method test_simple
  -- empty Monitor returns .nil
  self~assertNull(.Monitor~new~current)

  self~assertNull(.Monitor~new(.nil)~current)

  -- must be exactly the same instance
  d = .Destination~new
  self~assertSame(d, .Monitor~new(d)~current)
  self~assertNotSame(d, .Monitor~new(.Destination~new)~current)

::method test_push_pop
  d1 = .Object~new
  d2 = .Destination~new("other")
  m = .Monitor~new
  self~assertNull(m~current)
  -- destination(d) returns the current 'd', not the previous one
  -- same for destination(), which pops, and then returns current
  self~assertSame(d1, m~destination(d1)) -- set d1 destination
  self~assertSame(d1, m~current)
  self~assertSame(d2, m~destination(d2)) -- set d2 destination
  self~assertSame(d2, m~current)
  self~assertSame(d1, m~destination) -- set to previous destination d1
  self~assertSame(d1, m~current)
  self~assertNull(m~destination)
  self~assertNull(m~current)

::method test_unknown_invalid
  -- we have no access to the Queue of destinations
  self~expectSyntax(97.1) -- Object .. does not understand message "ITEMS"
  .Monitor~new(.Destination~new)~items

::method test_unknown
  -- run methods that Monitor doesn't have, but Destination does
  m = .Monitor~new(.Destination~new("d"))
  self~assertSame("d", m~instance)
  self~assertTrue(m~threeArgs(1, 2, 3))


::class Destination
::attribute instance get
::method init
  expose instance
  use strict arg instance = .nil
::method threeArgs
  use strict arg one, two, three
  return .true


-- for tests of the .debuginput, .error, .input, .output, and .traceoutput
-- Monitors see environmentEntries.testGroup


::options all syntax
