#!/usr/bin/env rexx
/*----------------------------------------------------------------------------*/
/*                                                                            */
/* Copyright (c) 2019-2022 Rexx Language Association. All rights reserved.    */
/*                                                                            */
/* This program and the accompanying materials are made available under       */
/* the terms of the Common Public License v1.0 which accompanies this         */
/* distribution. A copy is also available at the following address:           */
/* https://www.oorexx.org/license.html                                        */
/*                                                                            */
/* Redistribution and use in source and binary forms, with or                 */
/* without modification, are permitted provided that the following            */
/* conditions are met:                                                        */
/*                                                                            */
/* Redistributions of source code must retain the above copyright             */
/* notice, this list of conditions and the following disclaimer.              */
/* Redistributions in binary form must reproduce the above copyright          */
/* notice, this list of conditions and the following disclaimer in            */
/* the documentation and/or other materials provided with the distribution.   */
/*                                                                            */
/* Neither the name of Rexx Language Association nor the names                */
/* of its contributors may be used to endorse or promote products             */
/* derived from this software without specific prior written permission.      */
/*                                                                            */
/* THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS        */
/* "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT          */
/* LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS          */
/* FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT   */
/* OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,      */
/* SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED   */
/* TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA,        */
/* OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY     */
/* OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING    */
/* NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS         */
/* SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.               */
/*                                                                            */
/*----------------------------------------------------------------------------*/
parse source . . fileSpec
group = .TestGroup~new(fileSpec)
group~add(.MutexSemaphore.testGroup)
if group~isAutomatedTest then
  return group
else
  return group~suite~execute~~print

::requires 'ooTest.frm' -- load the ooRexxUnit classes


::class "MutexSemaphore.testGroup" subclass ooTestCase public

::method test_new_one_arg
  self~expectSyntax((93.902, 0)) -- Too many arguments in invocation of method; 0 expected
  .MutexSemaphore~new(.nil)

::method test_acquire_two_args
  self~expectSyntax((93.902, 1)) -- Too many arguments in invocation of method; 1 expected
  .MutexSemaphore~new~acquire(.nil, .nil)

::method test_acquire_number
  self~expectSyntax((88.902, "timeout", "")) -- The timeout argument must be a number; found ""
  .MutexSemaphore~new~acquire("")

::method test_release_one_arg
  self~expectSyntax((93.902, 0)) -- Too many arguments in invocation of method; 0 expected
  .MutexSemaphore~new~release(.nil)

::method test_acquire_acquire_simple
  sem = .MutexSemaphore~new
  self~assertTrue(sem~acquire)
  self~assertTrue(sem~acquire, 0)
  self~assertTrue(sem~acquire, 0.01)
  self~assertTrue(sem~acquire, .TimeSpan~fromSeconds(0.05))
  self~assertTrue(sem~acquire, -3)

::method test_acquire_release_simple
  -- mutexes are created in the released state
  sem = .MutexSemaphore~new
  self~assertFalse(sem~release)

  -- an acquire with zero timeout returns immediately
  self~assertTrue(sem~acquire(0))
  self~assertTrue(sem~release)
  self~assertFalse(sem~release)

  -- wait with 1 ms timeout
  self~assertTrue(sem~acquire(0.001))
  self~assertTrue(sem~release)
  self~assertFalse(sem~release)

::method test_acquire_release_nested
  -- mutexes are created in the released state
  sem = .MutexSemaphore~new
  self~assertFalse(sem~release)

  -- acquire twice, release twice
  self~assertTrue(sem~acquire)
  self~assertTrue(sem~acquire(0.001))
  self~assertTrue(sem~release)
  self~assertTrue(sem~release)
  self~assertFalse(sem~release)

  -- acquire three times, and release
  self~assertTrue(sem~acquire(0.001))
  self~assertTrue(sem~acquire(-1))
  self~assertTrue(sem~acquire(.TimeSpan~fromSeconds(2)))
  self~assertTrue(sem~release)
  self~assertTrue(sem~release)
  self~assertTrue(sem~release)
  self~assertFalse(sem~release)

::method test_exclusion
  expose step

  sem = .MutexSemaphore~new

  step = 1
  self~run_test_exclusion(sem)

  -- give the other thread time to get started and request the semaphore
  guard off when step == 2
  -- request with zero timeout
  self~assertFalse(sem~acquire(0))
  -- request with a non-zero time out
  self~assertFalse(sem~acquire(0.05))
  -- wake the other thread up
  step = 3
  -- a long time out value that should wake up before the timeout
  self~assertTrue(sem~acquire(1))

  -- wake the other thread up again
  step = 4
  -- wait for the other thread to tell us to release
  guard off when step = 5
  self~assertTrue(sem~release)
  guard off when step == 6
  -- we should be able to acquire this once the secondary thread terminates
  self~assertTrue(sem~acquire(1))

::method run_test_exclusion unguarded
  expose step
  use strict arg sem

  reply

  self~assertTrue(sem~acquire)
  -- tell the main thread we have the semaphore
  step = 2

  guard off when step = 3

  self~assertTrue(sem~release)
  -- tests that we get false is not the owner
  self~assertFalse(sem~release)

  guard off when step = 4

  -- infinite wait request
  self~assertFalse(sem~acquire(0))
  self~assertFalse(sem~acquire(0.05))
  step = 5
  self~assertTrue(sem~acquire(-1))
  self~assertTrue(sem~release)
  -- wake up the main thread
  step = 6
  -- reacquire, wake up the main thread, then exit.
  -- the semaphore should be released when we terminate.
  self~assertTrue(sem~acquire)

