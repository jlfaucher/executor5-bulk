#!/usr/bin/env rexx
/*----------------------------------------------------------------------------*/
/*                                                                            */
/* Copyright (c) 2007-2025 Rexx Language Association. All rights reserved.    */
/*                                                                            */
/* This program and the accompanying materials are made available under       */
/* the terms of the Common Public License v1.0 which accompanies this         */
/* distribution. A copy is also available at the following address:           */
/* https://www.oorexx.org/license.html                                        */
/*                                                                            */
/* Redistribution and use in source and binary forms, with or                 */
/* without modification, are permitted provided that the following            */
/* conditions are met:                                                        */
/*                                                                            */
/* Redistributions of source code must retain the above copyright             */
/* notice, this list of conditions and the following disclaimer.              */
/* Redistributions in binary form must reproduce the above copyright          */
/* notice, this list of conditions and the following disclaimer in            */
/* the documentation and/or other materials provided with the distribution.   */
/*                                                                            */
/* Neither the name of Rexx Language Association nor the names                */
/* of its contributors may be used to endorse or promote products             */
/* derived from this software without specific prior written permission.      */
/*                                                                            */
/* THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS        */
/* "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT          */
/* LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS          */
/* FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT   */
/* OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,      */
/* SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED   */
/* TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA,        */
/* OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY     */
/* OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING    */
/* NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS         */
/* SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.               */
/*                                                                            */
/*----------------------------------------------------------------------------*/
  parse source . . fileSpec

  group = .TestGroup~new(fileSpec)
  group~add(.RexxContext.testGroup)

  if group~isAutomatedTest then return group

  testResult = group~suite~execute~~print

return testResult
-- End of entry point.

::requires 'ooTest.frm' -- load the ooRexxUnit classes


::class "RexxContext.testGroup" subclass ooTestCase public

::method TestNumeric01
  self~assertSame(digits(), .context~digits)
  self~assertSame(form(), .context~form)
  self~assertSame(fuzz(), .context~fuzz)
  numeric digits 100
  self~assertSame(digits(), .context~digits)
  numeric form engineering
  self~assertSame('ENGINEERING', .context~form)
  numeric form scientific
  self~assertSame('SCIENTIFIC', .context~form)
  numeric fuzz 1
  self~assertSame(1, .context~fuzz)

::method TestExecution01
  parse source . . name
  self~assertSame(name, .context~package~name)
  self~assertSame(self~class~method('TESTEXECUTION01'), .context~executable)
  self~assertSame(self~class~method('TESTEXECUTION01')~package, .context~package)

::method TestVariables01
  d = .context~variables
  self~assertSame(2, d~items)
  self~assertSame(self, d['SELF'])
  self~assertSame(self~class~superclass, d['SUPER'])

  a = "Fred"
  b.1 = "Foo"
  drop result
  drop d

  d = .context~variables

  self~assertSame(4, d~items)
  self~assertSame("Fred", d['A'])
  self~assertTrue(d['B.']~isA(.stem))
  self~assertSame("Foo", d['B.'][1])

::method TestArgs01
  a = testArgs()
  self~assertSame(0, a~size)
  self~assertSame(0, a~items)

  a = testArgs(1,2)
  self~assertSame(2, a~size)
  self~assertSame(2, a~items)
  self~assertSame(1, a[1])
  self~assertSame(2, a[2])

  a = testArgs(1,,2)
  self~assertSame(3, a~size)
  self~assertSame(2, a~items)
  self~assertSame(1, a[1])
  self~assertSame(2, a[3])
  self~assertSame(.nil, a[2])

::method TestCondition01
  self~assertSame(.nil, .context~condition)
  signal on syntax
  a = 1/0

  syntax:
  self~assertEquals(condition("o"), .context~condition)

  signal on nostring
  x = substr(.nil, 1, 2)

  nostring:
  self~assertEquals(condition("o"), .context~condition)

::method TestNew01
  self~expectSyntax(93.967)
  c = .RexxContext~new

::method TestCopy01
  self~expectSyntax(93.970)
  c = .context~copy

::method TestLine01
  self~assertSame(.line, .context~line)

::method TestRS01
  self~assertSame(.nil, .context~rs)
  address "" "echo foo" with output using (.Array~new)
  self~assertSame(.rs, .context~rs)

::method TestName01
  self~assertSame("INTERNAL", internal())
  self~assertSame("ROUTINE", routine())
  self~assertSame("METHODCALL", self~methodCall)
  exit   -- don't want to fall through to the internal routine

internal:  return .context~name

::method methodCall
  return .context~name

::routine testargs
  return .context~args

::routine routine
  return .context~name

::method test_sourceline_full_file
  parse source . . path
  thisFile = .Stream~new(path)
  thisFile~open("read shared")
  lines = thisFile~ArrayIn
  thisFile~close
  thisPackage = .context~package
  thisSource = thisPackage~source
  self~assertSame(lines~last, thisSource~last)
  do n = 1 to lines~last
    self~assertSame(lines[n], thisPackage~sourceLine(n))
  end

::method test_sourceline_first_line
  do first over "", .String~tab, "-- comment", "/* comment */"
    r = .Routine~new("", (first, "return .context~package~sourceLine(1)"))
    self~assertSame(first, r[])
  end
  first = "/* REXX"
  r = .Routine~new("", (first, "*/", "return .context~package~sourceLine(1)"))
  self~assertSame(first, r[])

::method test_sourceline_last_line
  do last over "", .String~tab, "-- comment", "/* comment */"
    r = .Routine~new("", ("return .context~package~sourceLine(sourceline())", last))
    self~assertSame(last, r[])
  end
  last = "*/"
  r = .Routine~new("", ("return .context~package~sourceLine(sourceline())", "/*", last))
  self~assertSame(last, r[])

::method test_sourceline_hashbang
  -- .Routine new() doesn't support a hashbang, so we create a temporary file
  hashbang = .TemporaryTestFile~new(self, "test_sourceline_hashbang.rex")
  do first over "#!", "#!/usr/bin/rexx"
    hashbang~create((first, "return .context~package~sourceLine(1)"))
    r = .Routine~newFile(hashbang~fullName)
    self~assertSame(first, r[])
  end

::method test_interpreter_thread_invocation

  self~assertTrue(Datatype(.context~interpreter,"Whole number"), "interpreter id must be a whole number, got:" .context~interpreter)
  self~assertTrue(Datatype(.context~thread     ,"Whole number"), "thread id must be a whole number, got:" .context~thread)
  self~assertTrue(Datatype(.context~invocation ,"Whole number"), "invocation id must be a whole number, got:" .context~invocation)

  currThread    =.context~thread
  currInvocation=.context~invocation

  r=.routine~new("test_new_context_values", .resources~context_thread_invocation)
  st=r~call

/* --
say "currThread    ="currThread
say "currInvocation="currInvocation
say

say "st~items:" st~items
do counter c idx over st~allindexes~sort
   say c~right(2)":" idx"=["st[idx]']'
end
-- */
  self~assertTrue(currThread=st~01_prologthread)
  self~assertTrue(currInvocation<st~02_prologinvocation)

    -- assert threads
  self~assertTrue(st~01_prologthread =  st~11_m3reply_before_thread, st~01_prologthread "=" st~11_m3reply_before_thread)
  self~assertTrue(st~01_prologthread <> st~13_m3reply_after_thread,  st~01_prologthread "<>" st~13_m3reply_after_thread)
  self~assertTrue(st~11_m3reply_before_thread <> st~13_m3reply_after_thread,  st~11_m3reply_before_thread "<>" st~13_m3reply_after_thread)

  self~assertTrue(st~01_prologthread <> st~21_m2start_thread, st~01_prologthread "<>" st~21_m2start_thread)
  self~assertTrue(st~01_prologthread =  st~31_m1_thread     , st~01_prologthread "="  st~31_m1_thread     )
  self~assertTrue(st~01_prologthread =  st~41_routine1thread, st~01_prologthread "="  st~41_routine1thread)

   -- assert invocatons
  self~assertTrue(st~02_prologInvocation < st~12_m3reply_before_invocation        , st~02_prologInvocation "<" st~12_m3reply_before_invocation        )
  self~assertTrue(st~02_prologInvocation < st~14_m3reply_after_invocation         , st~02_prologInvocation "<" st~14_m3reply_after_invocation         )
  self~assertTrue(st~12_m3reply_before_invocation = st~14_m3reply_after_invocation, st~12_m3reply_before_invocation "=" st~14_m3reply_after_invocation)

  self~assertTrue(st~02_prologInvocation < st~22_m2start_invocation               , st~02_prologInvocation "<" st~22_m2start_invocation)
  self~assertTrue(st~02_prologInvocation < st~32_m1_invocation                    , st~02_prologInvocation "<" st~32_m1_invocation     )
  self~assertTrue(st~02_prologInvocation < st~42_routine1invocation               , st~02_prologInvocation "<" st~42_routine1invocation)



::resource context_thread_invocation
-- test .context's 'thread' and 'invocation' return values
st=.stringTable~new
st~01_prologThread    =.context~thread
st~02_prologInvocation=.context~invocation

o=.test~new
o~m3reply(st)
msg=o~start("m2start",st)
o~m1(st)
call routine1 st
call syssleep 0.001  -- wait a bit for asynchroneous invocations to return
/* --
say "st~items:" st~items
do counter c idx over st~allindexes~sort
   say c~right(2)":" idx"=["st[idx]']'
end
-- */
return st

::class test

::method m3reply
  use arg st
  st~11_m3reply_before_Thread    =.context~thread
  st~12_m3reply_before_Invocation=.context~invocation
  reply
  st~13_m3reply_after_Thread    =.context~thread
  st~14_m3reply_after_Invocation=.context~invocation

::method m2start
  use arg st
  st~21_m2start_Thread    =.context~thread
  st~22_m2start_Invocation=.context~invocation

::method m1
  use arg st
  st~31_m1_Thread    =.context~thread
  st~32_m1_Invocation=.context~invocation

::routine routine1
  use arg st
  st~41_routine1Thread    =.context~thread
  st~42_routine1Invocation=.context~invocation

::END


::options novalue error
