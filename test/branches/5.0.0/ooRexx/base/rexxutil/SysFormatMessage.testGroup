#!/usr/bin/env rexx
/*----------------------------------------------------------------------------*/
/*                                                                            */
/* Copyright (c) 2007-2022 Rexx Language Association. All rights reserved.    */
/*                                                                            */
/* This program and the accompanying materials are made available under       */
/* the terms of the Common Public License v1.0 which accompanies this         */
/* distribution. A copy is also available at the following address:           */
/* https://www.oorexx.org/license.html                                        */
/*                                                                            */
/* Redistribution and use in source and binary forms, with or                 */
/* without modification, are permitted provided that the following            */
/* conditions are met:                                                        */
/*                                                                            */
/* Redistributions of source code must retain the above copyright             */
/* notice, this list of conditions and the following disclaimer.              */
/* Redistributions in binary form must reproduce the above copyright          */
/* notice, this list of conditions and the following disclaimer in            */
/* the documentation and/or other materials provided with the distribution.   */
/*                                                                            */
/* Neither the name of Rexx Language Association nor the names                */
/* of its contributors may be used to endorse or promote products             */
/* derived from this software without specific prior written permission.      */
/*                                                                            */
/* THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS        */
/* "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT          */
/* LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS          */
/* FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT   */
/* OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,      */
/* SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED   */
/* TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA,        */
/* OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY     */
/* OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING    */
/* NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS         */
/* SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.               */
/*                                                                            */
/*----------------------------------------------------------------------------*/
  parse source . . s

  group = .TestGroup~new(s)
  group~add(.SysFormatMessage.testGroup)

  if group~isAutomatedTest then return group

  testResult = group~suite~execute~~print

return testResult

::requires 'ooTest.frm'


::class "SysFormatMessage.testGroup" public subclass ooTestCase

::method test_no_args_too_few
  self~expectSyntax(88.901) -- Missing argument; argument 1 is required
  call SysFormatMessage

::method test_one_arg_too_few
  self~expectSyntax(88.901) -- Missing argument; argument 2 is required
  call SysFormatMessage ""

::method test_too_many_subs
  self~expectSyntax(40.1) -- External routine "SysFormatMessage" failed
  call sysformatmessage "&1", (1,2,3,4,5,6,7,8,9,0)

::method test_substitutions
  self~assertEquals("abc", SysFormatMessage("&1", "abc"))
  self~assertEquals("abc", SysFormatMessage("&1", ("abc", "def")))
  self~assertEquals("abc + def", SysFormatMessage("&1 + &2", ("abc", "def")))
  self~assertEquals("def", SysFormatMessage("&2", ("abc", "def")))
  self~assertEquals("def + abc", SysFormatMessage("&2 + &1", ("abc", "def")))
  self~assertEquals("def & abc", SysFormatMessage("&2 & &1", ("abc", "def")))
  self~assertEquals("abc +  + def", SysFormatMessage("&1 + &2 + &3", ("abc",, "def")))
  self~assertEquals("123", SysFormatMessage("123", ("abc", "def")))
  self~assertEquals("&0 abc", SysFormatMessage("&0 &1", "abc"))
  self~assertEquals("", SysFormatMessage("&1", .array~new))
  self~assertSame("", SysFormatMessage("", ""))
  self~assertSame("&&&", SysFormatMessage("&&1&", "&"))
  self~assertSame(" leading...", SysFormatMessage(" leading&1", "..."))
  self~assertSame(">>trailing  ", SysFormatMessage("&1trailing  ", ">>"))
  self~assertSame(" leading-trailing ", SysFormatMessage(" leading&1trailing ", "-"))
  self~assertSame("9 8 7 6 5 4 3 2 1 &0", SysFormatMessage("&9 &8 &7 &6 &5 &4 &3 &2 &1 &0", .Array~new(9)~fill(0)~allIndexes))

::method test_long_substitutions
  subs = .Array~new
  len = 32
  do 9
    subs~append(1~copies(len))
    len *= 2
  end
  self~assertSame(1~copies(32 * (2**9 - 1)), SysFormatMessage("&2&4&6&8&1&3&5&7&9", subs))


::options novalue error
