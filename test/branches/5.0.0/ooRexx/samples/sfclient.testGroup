#!/usr/bin/env rexx
/*----------------------------------------------------------------------------*/
/*                                                                            */
/* Copyright (c) 2007-2022 Rexx Language Association. All rights reserved.    */
/*                                                                            */
/* This program and the accompanying materials are made available under       */
/* the terms of the Common Public License v1.0 which accompanies this         */
/* distribution. A copy is also available at the following address:           */
/* https://www.oorexx.org/license.html                                        */
/*                                                                            */
/* Redistribution and use in source and binary forms, with or                 */
/* without modification, are permitted provided that the following            */
/* conditions are met:                                                        */
/*                                                                            */
/* Redistributions of source code must retain the above copyright             */
/* notice, this list of conditions and the following disclaimer.              */
/* Redistributions in binary form must reproduce the above copyright          */
/* notice, this list of conditions and the following disclaimer in            */
/* the documentation and/or other materials provided with the distribution.   */
/*                                                                            */
/* Neither the name of Rexx Language Association nor the names                */
/* of its contributors may be used to endorse or promote products             */
/* derived from this software without specific prior written permission.      */
/*                                                                            */
/* THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS        */
/* "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT          */
/* LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS          */
/* FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT   */
/* OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,      */
/* SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED   */
/* TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA,        */
/* OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY     */
/* OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING    */
/* NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS         */
/* SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.               */
/*                                                                            */
/*----------------------------------------------------------------------------*/

/*

  sfclient.rex simple socket client that uses the socket function package

  Purpose: make some tests for each ooRexx sample pgm distributed.

  Built from a template provided by Rony G. Flatscher 2018-02-25
  Amended based on month.testgroup for more complete testing.

  Where possible the assertions are done both ways to make sure we
  do not get to much or to little output.

*/

  parse source . . fileSpec;

  group = .TestGroup~new(fileSpec)
  group~add(.sfclient.testGroup)

  if locateSamplePrg("sfclient.rex") == .nil then
    group~markNoTests("This is a test of a sample ooRexx program, but the sample can not be located")

  if group~isAutomatedTest then return group

  testResult = group~suite~execute~~print

return testResult
-- End of entry point.

/* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*\
  Directives, Classes, or Routines.
\* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*/
::requires 'ooTest.frm'
::requires 'FileUtils.cls'

/* Class needed for Client-Server test, thanks Erich				*/
::class job

::method init
  expose prefix
  use strict arg prefix

::method server
  expose prefix
  use arg inArrS, myStemS.

  SELECT CASE .ooRexxUnit.OSName
    when 'WINDOWS' then address command "rexx" prefix || "server" with input using (inArrS) output stem myStemS.
    otherwise           address ""      "rexx" prefix || "server" with input using (inArrS) output stem myStemS.
  END /* Select */

::method client unguarded
  expose prefix
  use arg inArrC, myStemC.

  SELECT CASE .ooRexxUnit.OSName
    when 'WINDOWS' then address command "rexx" prefix || "client" with input using (inArrC) output stem myStemC.
    otherwise           address ""      "rexx" prefix || "client" with input using (inArrC) output stem myStemC.
  END /* Select */

::class "sfclient.testGroup" subclass ooTestCase public

  ::attribute lookup class
  ::method init class

    forward class (super) continue
    self~ooTestType = self~SAMPLE_TEST

    l = .directory~new
    l~shouldStop = .true
    l~prgPath = ""
    l~prgOut = .array~new
    self~lookup = l

  ::method test_01_sampleExists

    lookup = self~class~lookup
    lookup~shouldStop = .true
    lookup~prgOut = .array~new

    prgPath = locateSamplePrg("sfclient.rex")

    -- Assert that the sample was located and that it exists.
    self~assertNotNull(prgPath)
    self~assertNotSame("", .stream~new(prgPath)~query("EXISTS"))

    lookup~prgPath = prgPath
    lookup~shouldStop = .false

-- Not used here ::method test_02_executes
-- Not used here ::method test_03_badArg

::method "test_04_sfclient.rex"

  Line.1  = 'SockConnect failed: ECONNREFUSED'
  Line.0 =1

-- assuming the server is not running Connect failed will be the result

  inArr=('Hello World')

  prgPathName = locateSamplePrg("sfclient.rex")

  SELECT CASE .ooRexxUnit.OSName
    when 'WINDOWS' then address command 'rexx' '"'prgPathName'"' with input using (inArr) output stem myStem.
    otherwise           address ''      'rexx' prgPathName with input using (inArr) output stem myStem.
  END /* Select */

  DO i= 1 TO myStem.0
    self~assertEquals(Line.i,myStem.i)
  END i

--use arg prefix = "sc"
  use arg prefix = "sf"

-- now make a proper test of the client-server communication;
-- example code provided by Erich Steinb√∂ck, TNX!
-- passing result with help of Rony G. Flatscher, TNX!

  LineS.1 = 'Server listening at 0.0.0.0:50010'		--macOS
  --LineS.1 = 'Server listening at 192.168.1.10:50010'	--Win
  LineS.2 = 'Press [Enter] To Shutdown'
  --<3>
  LineS.3 = 'shutdown in 3 sec'
  LineS.0 = 3

  LineC.1 = 'type "X" to exit'
  LineC.2 = 'Send To Server: Server responded: Echo: 111'
  LineC.3 = 'Send To Server: Server responded: Echo: 222'
  LineC.4 = 'Send To Server: Server responded: Echo: 333'
  LineC.5 = 'Send To Server:'                              --"x" swallowed by client
  LineC.0 = 5

  LineC2.1 = 'type "X" to exit'
  LineC2.2 = 'Send To Server: Server responded: Echo: 444'
  LineC2.3 = 'Send To Server: Server responded: Echo: 555'
  LineC2.4 = 'Send To Server: Server responded: Echo: 666'
  LineC2.5 = 'Send To Server:'                              --"x" swallowed by client
  LineC2.0 = 5

  serverInArray      = (3)
  serveryOutputStem. = .stem~new

  clientInArray      = ('111','222','333', 'x')
  clientOutputStem.  = .stem~new

  clientInArray2      = ('444','555','666', 'x')
  clientOutputStem2.  = .stem~new

  currPath   = directory()
  samplePath = filespec('location',prgPathName)
-- on Windows we need the drive

  -- We need to move to where the sample is
  SELECT CASE .ooRexxUnit.OSName
    when 'WINDOWS' then address command 'cd' '"'samplePath'"'
    otherwise           address ''      'cd' samplePath
  END /* Select */

-- create an instance of the sc or sf client-Server test class
  job = .Job~new(prefix)

-- start the server
  job~start("server", serverInArray, serveryOutputStem.)

  call SysSleep 0.5 -- wait for server to become ready

-- start one client
  job~start("client", clientInArray, clientOutputStem.)

-- start a 2nd client
  job~start("client", clientInArray2, clientOutputStem2.)

  call SysSleep 5 -- wait to be sure the server is closed before evaluating results

  DO i= 1 TO LineS.0
    if i= 1 then self~assertEquals(LineS.i~subWord(1,3),serveryOutputStem.i~subWord(1,3))
    else         self~assertEquals(LineS.i,serveryOutputStem.i)
  END i

  DO i= 1 TO LineC.0
    self~assertEquals(LineC.i, clientOutputStem.i)
  END i

  DO i= 1 TO LineC2.0
    self~assertEquals(LineC2.i, clientOutputStem2.i)
  END i

  -- Then move back to where we were before
  SELECT CASE .ooRexxUnit.OSName
    when 'WINDOWS' then address command 'cd' '"'currPath'"'
    otherwise           address ''      'cd' currPath
  END /* Select */

--we are done in this testgroup
