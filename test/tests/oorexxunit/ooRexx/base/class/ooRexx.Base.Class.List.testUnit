#!/usr/bin/rexx
/*
   name:             ooRexx.Base.Class.List.testUnit
   author:           Rony G. Flatscher
   date:             2007-06-09
   version:          1.0.2

-- line commented lines are ignored, when building the directory of infos from this header
   changed:          ---

   languageLevel:    6.00, 6.02
   purpose:          Test the methods of the class List.
   remark:           2007-06-09, ---rgf, added testcase "test_section"
                     2007-06-10, ---rgf, added tests for syntax errors

   license:          CPL 1.0 (Common Public License v1.0, see below)
   link:

// category needs not to be numbered, importance is given by position (1=most important)
   category:         ooRexx
   category:         Base
   category:         Class
   category:         List
*/
/*----------------------------------------------------------------------------*/
/*                                                                            */
/* Copyright (c) 2007 Rexx Language Association. All rights reserved.         */
/*                                                                            */
/* This program and the accompanying materials are made available under       */
/* the terms of the Common Public License v1.0 which accompanies this         */
/* distribution. A copy is also available at the following address:           */
/* http://www.ibm.com/developerworks/oss/CPLv1.0.htm                          */
/*                                                                            */
/* Redistribution and use in source and binary forms, with or                 */
/* without modification, are permitted provided that the following            */
/* conditions are met:                                                        */
/*                                                                            */
/* Redistributions of source code must retain the above copyright             */
/* notice, this list of conditions and the following disclaimer.              */
/* Redistributions in binary form must reproduce the above copyright          */
/* notice, this list of conditions and the following disclaimer in            */
/* the documentation and/or other materials provided with the distribution.   */
/*                                                                            */
/* Neither the name of Rexx Language Association nor the names                */
/* of its contributors may be used to endorse or promote products             */
/* derived from this software without specific prior written permission.      */
/*                                                                            */
/* THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS        */
/* "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT          */
/* LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS          */
/* FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT   */
/* OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,      */
/* SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED   */
/* TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA,        */
/* OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY     */
/* OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING    */
/* NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS         */
/* SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.               */
/*                                                                            */
/*----------------------------------------------------------------------------*/

testUnitClass=.ooRexx.Base.Class.List.TestUnit  -- change accordingly
-------------------------------------------------------------------------------------------
-- ===> adapt the "testUnitList" to your testCase classes; each element in the list is <===
-- ===> an array object, the first element containing the testCase class object, the   <===
-- ===> second element is a list of test method names which are regarded to be         <===
-- ===> mandatory (if the list remains empty all test methods are mandatory)           <===

   /* list of array objects, each containing the testUnit class object and an
      optional list of mandatory test case methods name                       */
mandatoryTestMethods=.list~new   -- no mandatory tests for this testCase class
testUnitList=.list~of( .array~of(.ooRexx.Base.Class.List.testUnit,  mandatoryTestMethods) )


-------------------------------------------------------------------------------------------
-- ===> the following code needs not to be individualized                              <===

   -- read top comment, containing infos about this program
arrLines=.array~new
do i=1 to 150 until arrLines[i]="*/"
   arrLines[i]=sourceline(i)
end
      -- supply information for the testClass(es) in this file; the class attribute
      -- "TestCaseInfo" (a directory object, index points to a queue) will store
      -- the parsed infos
aTestUnitClass=testUnitList~at(testUnitList~first)[1] -- get first testClass

   -- will parse the array lines and store result in class object
call makeDirTestInfo aTestUnitClass, arrLines
tmpDir=aTestUnitClass~TestCaseInfo
parse source s   -- op_sys invocationType fullPathToThisFile
tmpDir~setentry("test_Case-source", s)

   -- now add this directory to other testCase classes, if any left
do arr over testUnitList
   if arr[1]=aTestUnitClass then iterate  -- already handled
   arr[1]~TestCaseInfo=tmpDir             -- save info in class object
end

-- if this file is CALLed or REQUIRED then define an entry "bRunTestLocally" in .local
-- and set it to .false; this way the independent local invocation of the tests is inhibited
if .local~hasentry("bRunTestsLocally")=.false then
   .local~bRunTestsLocally=.true -- if this file is executed directly, then run tests for debugging

if .bRunTestsLocally=.true then  -- run ALL tests in this test unit
do
   ts=.testSuite~new             -- create a testSuite
   do arr over testUnitList
      -- create a testSuite for the given test case class, use all its testmethods
      ts~addTest( .testSuite~new(arr[1]))
   end
   -- testResult=.testSuite~new(testUnitClass)~run
   testResult=ts~run       -- now run all the tests

   call simpleDumpTestResults testResult
end

   /* return list of array objects containing test case classes and
      optionally list of mandatory test methods                      */
return testUnitList



::requires ooRexxUnit.cls     -- load the ooRexxUnit classes

   -- class named exactly like file
::class "ooRexx.Base.Class.List.TestUnit" subclass TestCase public

::method init
  expose collDir clz bUserDefinedIndex bSingleItem o1 o2
  forward class (super) continue    -- let superclass initialize

  clz=.List                        -- collection class object to use
  bUserDefinedIndex=.false          -- user defined index? (determines what MAKEARRAY returns)
  bSingleItem      =.true           -- index associates only one item

  collDir=.directory~new            -- define directory containing predefined collections
  collDir~emptyColl=clz~new

  o1=.object~new
  o2=.object~new

  collDir~coll_1   =clz~of("1v", "2v", "2v", "2v", o1, o1)
  collDir~coll_2   =clz~of(            "2v", "2v", o1, o1, o2, o2, "3v")

  collDir~allIndexes1=.array~of(0, 1, 2, 3, 4, 5)
  collDir~allIndexes2=.array~of(0, 1, 2, 3, 4, 5, 6)

  collDir~allItems1=.array~of("1v", "2v", "2v", "2v", o1, o1)
  collDir~allItems2=.array~of(            "2v", "2v", o1, o1, o2, o2, "3v")


   /* determine resulting collection   */
  collDir~difference1 =clz~of("1v","2v")  /* coll_1~difference(coll_2) */
  collDir~difference2 =clz~of(o2, o2, "3v")       /* coll_2~difference(coll_1) */


  collDir~interSectionColl=clz~of("2v", "2v", o1, o1)

  collDir~unionColl1      =clz~of("1v", "2v", "2v", "2v", "2v", "2v", o1, o1, o1, o1, o2, o2, "3v")
  collDir~unionColl2      =clz~of("2v", "2v", "3v", "1v", "2v", "2v", "2v", o1, o1, o2, o2, o1, o1)

  collDir~xorColl1        =clz~of("1v", "2v", o2, o2, "3v")
  collDir~xorColl2        =clz~of(o2, o2, "3v", "1v", "2v")



::method "test_allIndexes"
  expose collDir clz bUserDefinedIndex bSingleItem

  if .ooRexxUnit.languageLevel<6.02 then
  do
     .error~say(self~string" - 'test_allIndexes': not carried out, needs language level 6.02, currently:" .ooRexxUnit.languageLevel)
     return
  end

  self~assertTrue("subtest1", sameContent(.bag~new~union(collDir~allIndexes1      ), -
                                          .bag~new~union(collDir~coll_1~allIndexes)))

  self~assertTrue("subtest2", sameContent(.bag~new~union(collDir~allIndexes2      ), -
                                          .bag~new~union(collDir~coll_2~allIndexes)))

  tmp=collDir~emptyColl~allIndexes
  self~assertTrue("subtest3", tmp~isA(.array))
  self~assertEquals("subtest4", 0, tmp~items)

  self~assertEquals("subtest5", collDir~allIndexes1, collDir~coll_1~allIndexes)
  self~assertEquals("subtest6", collDir~allIndexes2, collDir~coll_2~allIndexes)



::method "test_allItems"
  expose collDir clz bUserDefinedIndex bSingleItem

  if .ooRexxUnit.languageLevel<6.02 then
  do
     .error~say(self~string" - 'test_allItems': not carried out, needs language level 6.02, currently:" .ooRexxUnit.languageLevel)
     return
  end

  self~assertTrue("subtest1", sameContent(.bag~new~union(collDir~allItems1      ), -
                                          .bag~new~union(collDir~coll_1~allItems)))

  self~assertTrue("subtest2", sameContent(.bag~new~union(collDir~allItems2      ), -
                                          .bag~new~union(collDir~coll_2~allItems)))

  tmp=collDir~emptyColl~allItems
  self~assertTrue("subtest3", tmp~isA(.array))
  self~assertEquals("subtest4", 0, tmp~items)

  self~assertEquals("subtest5", collDir~allItems1, collDir~coll_1~allItems)
  self~assertEquals("subtest6", collDir~allItems2, collDir~coll_2~allItems)


::method "test_append"
  expose collDir clz bUserDefinedIndex bSingleItem

  if .ooRexxUnit.languageLevel<6.02 then
  do
     .error~say(self~string" - 'test_append': not carried out, needs language level 6.02, currently:" .ooRexxUnit.languageLevel)
     return
  end


  c1=collDir~coll_1
  c2=collDir~coll_2

  tmpC1=clz~new
  do o over c1
     tmpC1~append(o)
  end

  tmpC2=clz~new
  do o over c2
     tmpC2~append(o)
  end

  self~assertEquals("subtest01", c1, tmpC1)
  self~assertEquals("subtest02", c2, tmpC2)



::method "test_appendAll"
  expose collDir clz bUserDefinedIndex bSingleItem

  if .ooRexxUnit.languageLevel<6.02 then
  do
     .error~say(self~string" - 'test_appendAll': not carried out, needs language level 6.02, currently:" .ooRexxUnit.languageLevel)
     return
  end

  tmpColl=clz~new

  ce=collDir~emptyColl
  c1=collDir~coll_1
  c2=collDir~coll_2

  self~assertEquals("subtest01", ce, ce~copy~~appendAll(ce))

  self~assertEquals("subtest03", c1, c1~copy~~appendAll(ce))
  self~assertEquals("subtest04", c2, c2~copy~~appendAll(ce))

  self~assertEquals("subtest05", c1, clz~new~~appendAll(c1))
  self~assertEquals("subtest06", c2, clz~new~~appendAll(c2))
  self~assertEquals("subtest07", collDir~unionColl1, c1~copy~~appendAll(c2))
  self~assertEquals("subtest08", collDir~unionColl2, c2~copy~~appendAll(c1))




::method "test_AT"
  expose collDir clz bUserDefinedIndex bSingleItem

  coll=collDir~coll_1      -- get the prepared List
  self~assertEquals("subtest1", "1v", coll~at(0))
  self~assertEquals("subtest2", "2v", coll~at(1))
  self~assertNull(  "subtest3",       coll~at(10))


::method "test_[]"
  expose collDir clz bUserDefinedIndex bSingleItem

  coll=collDir~coll_1      -- get the prepared List
  self~assertEquals("subtest1", "1v", coll~"[]"("0"))
  self~assertEquals("subtest2", "2v", coll~"[]"("1"))
  self~assertNull(  "subtest3",      coll~"[]"("10"))



::method "test_difference"
  expose collDir clz bUserDefinedIndex bSingleItem

  ce=collDir~emptyColl
  c1=collDir~coll_1
  c2=collDir~coll_2

  self~assertEquals("subtest1", ce, ce~difference(ce))

  self~assertEquals("subtest2", c1, c1~difference(ce))
  self~assertEquals("subtest3", c2, c2~difference(ce))

  self~assertEquals("subtest4", collDir~difference1, c1~difference(c2))
  self~assertEquals("subtest5", collDir~difference2, c2~difference(c1))

  self~assertEquals("subtest6", ce, c1~difference(c1))
  self~assertEquals("subtest7", ce, c2~difference(c2))



::method "test_empty"
  expose collDir clz bUserDefinedIndex bSingleItem

  if .ooRexxUnit.languageLevel<6.02 then
  do
     .error~say(self~string" - 'test_empty': not carried out, needs language level 6.02, currently:" .ooRexxUnit.languageLevel)
     return
  end

  ce=collDir~emptyColl
  self~assertTrue("subtest01:", sameContent(ce, ce~copy~~empty))

  c1=collDir~coll_1~copy
  self~assertTrue("subtest02:", sameContent(ce, c1~copy~~empty))

  c2=collDir~coll_2~copy
  self~assertTrue("subtest03:", sameContent(ce, c2~copy~~empty))


::method "test_hasIndex"
  expose collDir clz bUserDefinedIndex bSingleItem

  coll=collDir~coll_1      -- get the prepared List
  self~assertTrue( "subtest1", coll~hasIndex("1"))
  self~assertTrue( "subtest2", coll~hasIndex("2"))
  self~assertFalse("subtest3", coll~hasIndex("10"))



::method "test_hasItem"
  expose collDir clz bUserDefinedIndex bSingleItem
  if .ooRexxUnit.languageLevel<6.02 then
  do
     .error~say(self~string" - 'test_hasItem': not carried out, needs language level 6.02, currently:" .ooRexxUnit.languageLevel)
     return
  end

  coll=collDir~coll_1      -- get the prepared List
  self~assertTrue( "subtest1", coll~hasItem("1v"))
  self~assertTrue( "subtest2", coll~hasItem("2v"))
  self~assertFalse("subtest3", coll~hasItem("3v"))


::method "test_index"
  expose collDir clz bUserDefinedIndex bSingleItem
  if .ooRexxUnit.languageLevel<6.02 then
  do
     .error~say(self~string" - 'test_index': not carried out, needs language level 6.02, currently:" .ooRexxUnit.languageLevel)
     return
  end

  coll=collDir~coll_1      -- get the prepared List
  self~assertEquals("subtest1", "0", coll~index("1v"))
  self~assertEquals("subtest2", "1", coll~index("2v"))
  self~assertNull(  "subtest3",      coll~index("3v"))



::method "test_interSection"
  expose collDir clz bUserDefinedIndex bSingleItem

  if .ooRexxUnit.languageLevel<6.02 then
  do
     .error~say(self~string" - 'test_subset': not carried out, needs language level 6.02, currently:" .ooRexxUnit.languageLevel)
     return
  end

  ce=collDir~emptyColl
  c1=collDir~coll_1
  c2=collDir~coll_2
  res=collDir~interSectionColl

  self~assertEquals("subtest1", ce, ce~interSection(ce))

  self~assertEquals("subtest2", ce, c1~interSection(ce))
  self~assertEquals("subtest3", ce, c2~interSection(ce))

  self~assertEquals("subtest4", res, c1~interSection(c2))
  self~assertEquals("subtest5", res, c2~interSection(c1))

  self~assertEquals("subtest6", c1, c1~interSection(c1))
  self~assertEquals("subtest7", c2, c2~interSection(c2))



::method "test_isEmpty"
  expose collDir clz bUserDefinedIndex bSingleItem

  if .ooRexxUnit.languageLevel<6.02 then
  do
     .error~say(self~string" - 'test_isEmpty': not carried out, needs language level 6.02, currently:" .ooRexxUnit.languageLevel)
     return
  end

  self~assertTrue( "subtest01", collDir~emptyColl~isEmpty)
  self~assertFalse("subtest02", collDir~coll_1~isEmpty)
  self~assertFalse("subtest03", collDir~coll_2~isEmpty)

  self~assertTrue( "subtest04", collDir~coll_1~copy~~empty~isEmpty)



::method "test_items"
  expose collDir clz bUserDefinedIndex bSingleItem

  self~assertEquals("subtest1", 0, collDir~emptyColl~items)
  self~assertEquals("subtest2", 6, collDir~coll_1~items)
  self~assertEquals("subtest3", 7, collDir~coll_2~items)



::method "test_makeArray"
  expose collDir clz bUserDefinedIndex bSingleItem

  tmp=collDir~coll_1~makeArray
  self~assertTrue("subtest1", tmp~isA(.array))
  if bUserDefinedIndex then
     self~assertTrue("subtest2", sameContent(.bag~new~union(tmp                ), -
                                             .bag~new~union(collDir~allIndexes1)))
  else
     self~assertTrue("subtest2", sameContent(.bag~new~union(tmp                ), -
                                             .bag~new~union(collDir~allItems1)))


  tmp=collDir~coll_2~makeArray
  self~assertTrue("subtest3", tmp~isA(.array))
  if bUserDefinedIndex then
     self~assertTrue("subtest4", sameContent(.bag~new~union(tmp                ), -
                                             .bag~new~union(collDir~allIndexes2)))
  else
     self~assertTrue("subtest4", sameContent(.bag~new~union(tmp                ), -
                                             .bag~new~union(collDir~allItems2)))


  tmp=collDir~emptyColl~makeArray
  self~assertTrue("subtest5", tmp~isA(.array))
  self~assertEquals("subtest6", 0, tmp~items)



::method "test_PUT"
  expose collDir clz bUserDefinedIndex bSingleItem

  tmpColl=clz~new
  dummy="test"
  self~assertEquals("subtest01", 0, tmpColl              ~items)

  idx=0
  ind=2
  item="1"
  items=1
  self~assertFalse( "subtest0"ind"a", tmpColl~hasindex(idx))
  self~assertFalse( "subtest0"ind"b", tmpColl~hasitem(item))
  tmpColl~insert(dummy)
  self~assertTrue(  "subtest0"ind"c", tmpColl~hasitem(dummy))
  self~assertTrue(  "subtest0"ind"d", tmpColl~hasindex(idx))
  self~assertEquals("subtest0"ind"e", 1, tmpColl~~put(item,idx)~items)
  self~assertTrue(  "subtest0"ind"f", tmpColl~hasindex(idx))
  self~assertTrue(  "subtest0"ind"g", tmpColl~hasitem(item))
  self~assertFalse( "subtest0"ind"h", tmpColl~hasitem(dummy))

  idx=1
  ind=3
  item="2"
  items=2
  self~assertFalse( "subtest0"ind"a", tmpColl~hasindex(idx))
  self~assertFalse( "subtest0"ind"b", tmpColl~hasitem(item))
  tmpColl~insert(dummy)
  self~assertTrue(  "subtest0"ind"c", tmpColl~hasitem(dummy))
  self~assertTrue(  "subtest0"ind"d", tmpColl~hasindex(idx))
  self~assertEquals("subtest0"ind"e", 2, tmpColl~~put(item,idx)~items)
  self~assertTrue(  "subtest0"ind"f", tmpColl~hasindex(idx))
  self~assertTrue(  "subtest0"ind"g", tmpColl~hasitem(item))
  self~assertFalse( "subtest0"ind"h", tmpColl~hasitem(dummy))

  idx=1
  ind=4
  item="3"
  items=2
  self~assertTrue(  "subtest0"ind"d", tmpColl~hasindex(idx))
  self~assertEquals("subtest0"ind"e", 2, tmpColl~~put(item,idx)~items)
  self~assertTrue(  "subtest0"ind"f", tmpColl~hasindex(idx))
  self~assertTrue(  "subtest0"ind"g", tmpColl~hasitem(item))
  self~assertFalse( "subtest0"ind"h", tmpColl~hasitem(dummy))

  idx=1
  ind=5
  item="2"
  items=2
  self~assertTrue(  "subtest0"ind"d", tmpColl~hasindex(idx))
  self~assertEquals("subtest0"ind"e", 2, tmpColl~~put(item,idx)~items)
  self~assertTrue(  "subtest0"ind"f", tmpColl~hasindex(idx))
  self~assertTrue(  "subtest0"ind"g", tmpColl~hasitem(item))
  self~assertFalse( "subtest0"ind"h", tmpColl~hasitem(dummy))



  idx=2
  ind=6
  item="3"
  items=3
  self~assertFalse( "subtest0"ind"a", tmpColl~hasindex(idx))
  self~assertFalse( "subtest0"ind"b", tmpColl~hasitem(item))
  tmpColl~insert(dummy)
  self~assertTrue(  "subtest0"ind"c", tmpColl~hasitem(dummy))
  self~assertTrue(  "subtest0"ind"d", tmpColl~hasindex(idx))
  self~assertEquals("subtest0"ind"e", 3, tmpColl~~put(item,idx)~items)
  self~assertTrue(  "subtest0"ind"f", tmpColl~hasindex(idx))
  self~assertTrue(  "subtest0"ind"g", tmpColl~hasitem(item))
  self~assertFalse( "subtest0"ind"h", tmpColl~hasitem(dummy))

  self~assertTrue("subtest07", sameContent(.bag~of("1","2","3"), tmpColl))



::method "test_[]="
  expose collDir clz bUserDefinedIndex bSingleItem

  tmpColl=clz~new
  dummy="test"
  self~assertEquals("subtest01", 0, tmpColl              ~items)

  idx=0
  ind=2
  item="1"
  items=1
  self~assertFalse( "subtest0"ind"a", tmpColl~hasindex(idx))
  self~assertFalse( "subtest0"ind"b", tmpColl~hasitem(item))
  tmpColl~insert(dummy)
  self~assertTrue(  "subtest0"ind"c", tmpColl~hasitem(dummy))
  self~assertTrue(  "subtest0"ind"d", tmpColl~hasindex(idx))
  self~assertEquals("subtest0"ind"e", 1, tmpColl~~"[]="(item,idx)~items)
  self~assertTrue(  "subtest0"ind"f", tmpColl~hasindex(idx))
  self~assertTrue(  "subtest0"ind"g", tmpColl~hasitem(item))
  self~assertFalse( "subtest0"ind"h", tmpColl~hasitem(dummy))

  idx=1
  ind=3
  item="2"
  items=2
  self~assertFalse( "subtest0"ind"a", tmpColl~hasindex(idx))
  self~assertFalse( "subtest0"ind"b", tmpColl~hasitem(item))
  tmpColl~insert(dummy)
  self~assertTrue(  "subtest0"ind"c", tmpColl~hasitem(dummy))
  self~assertTrue(  "subtest0"ind"d", tmpColl~hasindex(idx))
  self~assertEquals("subtest0"ind"e", 2, tmpColl~~"[]="(item,idx)~items)
  self~assertTrue(  "subtest0"ind"f", tmpColl~hasindex(idx))
  self~assertTrue(  "subtest0"ind"g", tmpColl~hasitem(item))
  self~assertFalse( "subtest0"ind"h", tmpColl~hasitem(dummy))

  idx=1
  ind=4
  item="3"
  items=2
  self~assertTrue(  "subtest0"ind"d", tmpColl~hasindex(idx))
  self~assertEquals("subtest0"ind"e", 2, tmpColl~~"[]="(item,idx)~items)
  self~assertTrue(  "subtest0"ind"f", tmpColl~hasindex(idx))
  self~assertTrue(  "subtest0"ind"g", tmpColl~hasitem(item))
  self~assertFalse( "subtest0"ind"h", tmpColl~hasitem(dummy))

  idx=1
  ind=5
  item="2"
  items=2
  self~assertTrue(  "subtest0"ind"d", tmpColl~hasindex(idx))
  self~assertEquals("subtest0"ind"e", 2, tmpColl~~"[]="(item,idx)~items)
  self~assertTrue(  "subtest0"ind"f", tmpColl~hasindex(idx))
  self~assertTrue(  "subtest0"ind"g", tmpColl~hasitem(item))
  self~assertFalse( "subtest0"ind"h", tmpColl~hasitem(dummy))



  idx=2
  ind=6
  item="3"
  items=3
  self~assertFalse( "subtest0"ind"a", tmpColl~hasindex(idx))
  self~assertFalse( "subtest0"ind"b", tmpColl~hasitem(item))
  tmpColl~insert(dummy)
  self~assertTrue(  "subtest0"ind"c", tmpColl~hasitem(dummy))
  self~assertTrue(  "subtest0"ind"d", tmpColl~hasindex(idx))
  self~assertEquals("subtest0"ind"e", 3, tmpColl~~"[]="(item,idx)~items)
  self~assertTrue(  "subtest0"ind"f", tmpColl~hasindex(idx))
  self~assertTrue(  "subtest0"ind"g", tmpColl~hasitem(item))
  self~assertFalse( "subtest0"ind"h", tmpColl~hasitem(dummy))

  self~assertTrue("subtest07", sameContent(.bag~of("1","2","3"), tmpColl))




::method "test_remove"
  expose collDir clz bUserDefinedIndex bSingleItem o1 o2

  ce=collDir~emptyColl~copy
  c1=collDir~coll_1~copy

  self~assertNull("subtest01", ce~remove(98))

  self~assertNull("subtest02", c1~remove(99))

  self~assertEquals("subtest03", "1v", c1~remove("0"))
  self~assertEquals("subtest04", "2v", c1~remove("1"))
  self~assertEquals("subtest05", "2v", c1~remove("2"))
  self~assertEquals("subtest06", "2v", c1~remove("3"))
  self~assertEquals("subtest07", o1, c1~remove("4"))
  self~assertEquals("subtest08", o1, c1~remove("5"))
  self~assertNull(  "subtest09", c1~remove("0"))
  self~assertEquals("subtest10", 0, c1~items)

  -- now doing it in reverse order
  c1=collDir~coll_1~copy

  self~assertEquals("subtest11", 6, c1~items)

  self~assertEquals("subtest12", o1, c1~remove("5"))
  self~assertNull(  "subtest13", c1~remove("5"))

  self~assertEquals("subtest14", o1, c1~remove("4"))
  self~assertNull(  "subtest15", c1~remove("4"))

  self~assertEquals("subtest16", "2v", c1~remove("3"))
  self~assertNull(  "subtest17", c1~remove("3"))

  self~assertEquals("subtest18", "2v", c1~remove("2"))
  self~assertNull(  "subtest19", c1~remove("2"))

  self~assertEquals("subtest20", "2v", c1~remove("1"))
  self~assertNull(  "subtest21", c1~remove("1"))

  self~assertEquals("subtest22", "1v", c1~remove("0"))
  self~assertNull(  "subtest23", c1~remove("0"))

  self~assertEquals("subtest24", 0, c1~items)



::method "test_removeItem"
  expose collDir clz bUserDefinedIndex bSingleItem o1 o2

  if .ooRexxUnit.languageLevel<6.02 then
  do
     .error~say(self~string" - 'test_removeItem': not carried out, needs language level 6.02, currently:" .ooRexxUnit.languageLevel)
     return
  end


  ce=collDir~emptyColl~copy
  c1=collDir~coll_1~copy

  self~assertNull("subtest01", ce~removeItem("1"))
  self~assertNull("subtest02", ce~removeItem(.nil))

  self~assertNull("subtest03", c1~removeItem("99"))

  self~assertEquals("subtest04", "1v", c1~removeItem("1v"))
  self~assertNull(  "subtest05", c1~removeItem("1v"))

  self~assertEquals("subtest06", "2v", c1~removeItem("2v"))
  self~assertEquals("subtest07", "2v", c1~removeItem("2v"))
  self~assertEquals("subtest08", "2v", c1~removeItem("2v"))

  self~assertEquals("subtest09", o1, c1~removeItem(o1))
  self~assertEquals("subtest10", o1, c1~removeItem(o1))

  self~assertNull(  "subtest09", c1~removeItem("2"))

  self~assertEquals("subtest10", 0, c1~items)



::method "test_section"
  expose collDir clz bUserDefinedIndex bSingleItem o1 o2

  a1=clz~of(1)
  a2=a1~section(0)
  self~assertTrue("subtest_01a", a2~items=1)

  a2=a1~section(0,1)
  self~assertTrue("subtest_01b", a2~items=1)

  a2=a1~section(0,5)
  self~assertTrue("subtest_01c", a2~items=1)

  a1=clz~of(1,2,3,4,5)
  a2=a1~section(1)
  self~assertTrue("subtest_02a", a2~items=4)
  self~assertTrue("subtest_02b", testSeq(a1, a2, 2))
  a2=a1~section(1,2)
  self~assertTrue("subtest_03a", a2~items=2)
  self~assertTrue("subtest_03b", testSeq(a1, a2, 2))

  return


testSeq: procedure
  use arg a1, a2, start

  do i=0 to a2~items-1
     if a1[i+start-1]<>a2[i] then return .false
  end
  return .true






::method "test_subset"
  expose collDir clz bUserDefinedIndex bSingleItem

  if .ooRexxUnit.languageLevel<6.02 then
  do
     .error~say(self~string" - 'test_subset': not carried out, needs language level 6.02, currently:" .ooRexxUnit.languageLevel)
     return
  end

  ce=collDir~emptyColl
  c1=collDir~coll_1
  c2=collDir~coll_2
  cu1=collDir~unionColl1
  cu2=collDir~unionColl2

  self~assertTrue("subtest01", ce~subset(ce))
  self~assertTrue("subtest02", ce~subset(c1))
  self~assertTrue("subtest03", ce~subset(c2))

  self~assertTrue("subtest04", c1~subset(c1))
  self~assertTrue("subtest05", c2~subset(c2))

  self~assertTrue("subtest06", ce~subset(cu))

  self~assertTrue("subtest07", c1~subset(cu1))
  self~assertTrue("subtest08", c1~subset(cu2))

  self~assertTrue("subtest09", c2~subset(cu1))
  self~assertTrue("subtest10", c2~subset(cu2))

  self~assertFalse("subtest11", c1~subset(ce))
  self~assertFalse("subtest12", c1~subset(c2))

  self~assertFalse("subtest13", c2~subset(ce))
  self~assertFalse("subtest14", c2~subset(c1))



::method "test_Supplier"
  expose collDir clz bUserDefinedIndex bSingleItem

  ce=collDir~emptyColl
  c1=collDir~coll_1

  self~assertEquals("subtest01", ce, makeColl(clz, ce))
  self~assertEquals("subtest02", c1, makeColl(clz, c1))

  return

makeColl: procedure
  use arg clz, tmpColl

  new=clz~new

  do tmpColl~items      -- queues accept PUT only, if entry exists with given index already
     new~insert("oha")   -- create entry
  end

  s=tmpColl~supplier
  do while s~available
     new~put(s~item, s~index)
     s~next
  end
  return new



::method "test_union"
  expose collDir clz bUserDefinedIndex bSingleItem

  ce=collDir~emptyColl
  c1=collDir~coll_1
  c2=collDir~coll_2

  self~assertEquals("subtest1", ce, ce~union(ce))

  self~assertEquals("subtest2", c1, c1~union(ce))
  self~assertEquals("subtest3", c2, c2~union(ce))

  self~assertEquals("subtest4", c1, ce~union(c1))
  self~assertEquals("subtest5", c2, ce~union(c2))

  self~assertEquals("subtest6", collDir~unionColl1, c1~union(c2))
  self~assertEquals("subtest7", collDir~unionColl2, c2~union(c1))



::method "test_xor"
  expose collDir clz bUserDefinedIndex bSingleItem

  ce=collDir~emptyColl
  c1=collDir~coll_1
  c2=collDir~coll_2
  res=collDir~xorColl

  self~assertEquals("subtest1", ce, ce~xor(ce))

  self~assertEquals("subtest2", c1, c1~xor(ce))
  self~assertEquals("subtest3", c2, c2~xor(ce))

  self~assertEquals("subtest4", collDir~xorColl1, c1~xor(c2))
  self~assertEquals("subtest5", collDir~xorColl2, c2~xor(c1))

  self~assertEquals("subtest6", ce, c1~xor(c1))
  self~assertEquals("subtest7", ce, c2~xor(c2))



/* ================= additional, Queue specific methods =============== */

::method "test_NEW"
  expose collDir clz bUserDefinedIndex bSingleItem o1 o2

  self~expectSyntax(93.902)
  a=clz~new(3)


::method "test_OF"
  expose collDir clz bUserDefinedIndex bSingleItem o1 o2

  a=clz~of
  self~assertEquals("subtest01c", 0, a~items)

  a=clz~of(1,2,3)
  self~assertEquals("subtest02d", 3, a~items)



::method "test_first_last_next_previous"
  expose collDir clz bUserDefinedIndex bSingleItem o1 o2

  a=clz~new
  self~assertNull("subtest_01a", a~first)
  self~assertNull("subtest_01b", a~last)

  a~append("1v")
  self~assertEquals("subtest_02a", 0, a~first)
  self~assertEquals("subtest_02b", 0, a~last)
  self~assertNull("subtest_02c", a~next(0))
  self~assertNull("subtest_02d", a~previous(0))

  n=14
  do n
     a~append(random(n)"v")
  end

  self~assertEquals("subtest_03a", 0, a~first)
  self~assertEquals("subtest_03b", 14, a~last)
  self~assertEquals("subtest_03c", 1, a~next(0))
  self~assertEquals("subtest_03d", 2, a~next(1))
  self~assertEquals("subtest_03e", 13,  a~previous(14))
  self~assertEquals("subtest_03f", 0,  a~previous(1))



/*  The index value of elements inserted into a list  will be incremented
    by one, but the order in which MAKEARRAY will deliver the index values
    will be in list order.
*/
::method "test_insert"
  expose collDir clz bUserDefinedIndex bSingleItem o1 o2

  a=clz~new
  a~insert("a")      -- insert at end
  self~assertTrue(  "subtest_01a", a~hasItem("a"))
  self~assertTrue(  "subtest_01b", a~hasIndex(0))
  self~assertEquals("subtest_01c", "a", a[0])

  a~insert("b")      -- insert at end
  self~assertTrue(  "subtest_02a", a~hasItem("b"))
  self~assertTrue(  "subtest_02b", a~hasIndex(1))
  self~assertEquals("subtest_02c", "a", a[0])
  self~assertEquals("subtest_02d", "b", a[1])

  a~insert("c",0)    -- insert after first element
  self~assertTrue(  "subtest_03a", a~hasItem("c"))
  self~assertTrue(  "subtest_03b", a~hasIndex(2))

  self~assertEquals("subtest_03c", "a", a[0])
  self~assertEquals("subtest_03d", "b", a[1])
  self~assertEquals("subtest_03e", "c", a[2])

  a~insert("d",.nil) -- insert as first


  self~assertTrue(  "subtest_04a", a~hasItem("c"))
  self~assertTrue(  "subtest_04b", a~hasIndex(2))
  self~assertEquals("subtest_04c", "a", a[0])
  self~assertEquals("subtest_04d", "b", a[1])
  self~assertEquals("subtest_04e", "c", a[2])
  self~assertEquals("subtest_04f", "d", a[3])






::method "test_firstItem"
  expose collDir clz bUserDefinedIndex bSingleItem o1 o2

  a=clz~new
  self~assertNull("subtest_01", a~firstItem)

  a~insert("a")      -- insert at end
  self~assertEquals("subtest_02", "a", a~firstItem)

  a~insert("b")      -- insert at end
  self~assertEquals("subtest_03", "a", a~firstItem)

  a~insert("c", .nil)-- insert as first
  self~assertEquals("subtest_03", "c", a~firstItem)


::method "test_lastItem"
  expose collDir clz bUserDefinedIndex bSingleItem o1 o2

  a=clz~new
  self~assertNull("subtest_01", a~lastItem)

  a~insert("a")      -- insert at end
  self~assertEquals("subtest_02", "a", a~lastItem)

  a~insert("b")      -- insert at end
  self~assertEquals("subtest_03", "b", a~lastItem)

  a~insert("c", .nil)-- insert as last
  self~assertEquals("subtest_03", "b", a~lastItem)




/* Test for syntax errors. <---  <---  <---   */

::method "test_insert_error1"
  expose collDir clz bUserDefinedIndex bSingleItem o1 o2

  a=clz~new
  self~expectSyntax(93.918)
  a~insert("a",0)       -- insert using an illegal index value


::method "test_insert_error2"
  expose collDir clz bUserDefinedIndex bSingleItem o1 o2

  a=clz~new
  self~expectSyntax(93.918)
  a~insert("a",99)      -- insert using an illegal index value


::method "test_first_error1"
  expose collDir clz bUserDefinedIndex bSingleItem o1 o2

  a=clz~new
  self~expectSyntax(93.902)
  a~first(.nil)


::method "test_last_error1"
  expose collDir clz bUserDefinedIndex bSingleItem o1 o2

  a=clz~new
  self~expectSyntax(93.902)
  a~last(.nil)


::method "test_next_error1"
  expose collDir clz bUserDefinedIndex bSingleItem o1 o2

  a=clz~new
  self~expectSyntax(93.903)
  a~next


::method "test_next_error2"
  expose collDir clz bUserDefinedIndex bSingleItem o1 o2

  a=clz~new
  self~expectSyntax(93.918)
  a~next(.nil)


::method "test_previous_error1"
  expose collDir clz bUserDefinedIndex bSingleItem o1 o2

  a=clz~new
  self~expectSyntax(93.903)
  a~previous


::method "test_previous_error2"
  expose collDir clz bUserDefinedIndex bSingleItem o1 o2

  a=clz~new
  self~expectSyntax(93.918)
  a~previous(.nil)


::method "test_put_error1"
  expose collDir clz bUserDefinedIndex bSingleItem o1 o2

  a=clz~new
  self~expectSyntax(93.918)
  a~put("a",0)


::method "test_put_error2"
  expose collDir clz bUserDefinedIndex bSingleItem o1 o2

  a=clz~new
  self~expectSyntax(93.903)
  a~put("a")

::method "test_[]=_error1"
  expose collDir clz bUserDefinedIndex bSingleItem o1 o2

  a=clz~new
  self~expectSyntax(93.918)
  a~"[]="("a",0)


::method "test_[]=_error2"
  expose collDir clz bUserDefinedIndex bSingleItem o1 o2

  a=clz~new
  self~expectSyntax(93.903)
  a~"[]="("a")


::method "test_remove_error1"
  expose collDir clz bUserDefinedIndex bSingleItem o1 o2

  a=clz~new
  self~expectSyntax(93.918)
  a~remove("a")


::method "test_section_error1"
  expose collDir clz bUserDefinedIndex bSingleItem o1 o2

  a=clz~new
  self~expectSyntax(93.918)
  a~section(0)


::method "test_section_error2"
  expose collDir clz bUserDefinedIndex bSingleItem o1 o2

  a=clz~new
  self~expectSyntax(93.918)
  a~section(0,19)


::method "test_section_error3"
  expose collDir clz bUserDefinedIndex bSingleItem o1 o2

  a=clz~new
  self~expectSyntax(93.902)
  a~section(0,19,99)


::method "test_section_error4"
  expose collDir clz bUserDefinedIndex bSingleItem o1 o2

  a=clz~new
  self~expectSyntax(93.903)
  a~section





/* Test whether both collections contain the same entries.
   returns .true, if the same, .false else
*/
::routine "sameContent"
  use arg coll_1, coll_2

  if coll_1~items<>coll_2~items then      -- not the same amount of objects
     return .false
   -- work on a copy
  tmp=coll_1~copy
  do o over coll_2
     if \tmp~hasindex(o) then             -- no entry anymore ?
        return .false

     tmp~remove(o)                        -- remove object
  end

  return (tmp~items=0)                    -- if the same, then no items left


::routine "dumpColl"
  use arg coll, title
  if arg(2, "o") then title=coll~class~id "object ("coll~items" entries)"
                 else title=coll~class~id":" title
  .output~charout(">>>" title": ")
  .output~charout(encode(coll))
  say " <<<"
  say


::routine encode
  use arg coll

  bFirst=.true
  tmpStr="("
  do o over coll
     if bFirst then bFirst=.false
               else tmpStr=tmpStr","
     tmpStr=tmpStr'"'o~string'"'
  end
  return tmpStr")"

