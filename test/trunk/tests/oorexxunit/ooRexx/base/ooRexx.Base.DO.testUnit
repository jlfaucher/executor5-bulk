#!/usr/bin/rexx
/*
   name:             ooRexx.Base.DO.testUnit
   author:           Rony G. Flatscher
   date:             2007-06-11
   version:          1.0.0

-- line commented lines are ignored, when building the directory of infos from this header
   changed:          ---

   languageLevel:    6.02
   purpose:          Test the DO (adding a label to DO, LOOP, SELECT, END)
   remark:

   license:          CPL 1.0 (Common Public License v1.0, see below)
   link:

   category1:        ooRexx
   category2:        Base
   category3:        DO
*/
/*----------------------------------------------------------------------------*/
/*                                                                            */
/* Copyright (c) 2007 Rexx Language Association. All rights reserved.         */
/*                                                                            */
/* This program and the accompanying materials are made available under       */
/* the terms of the Common Public License v1.0 which accompanies this         */
/* distribution. A copy is also available at the following address:           */
/* http://www.ibm.com/developerworks/oss/CPLv1.0.htm                          */
/*                                                                            */
/* Redistribution and use in source and binary forms, with or                 */
/* without modification, are permitted provided that the following            */
/* conditions are met:                                                        */
/*                                                                            */
/* Redistributions of source code must retain the above copyright             */
/* notice, this list of conditions and the following disclaimer.              */
/* Redistributions in binary form must reproduce the above copyright          */
/* notice, this list of conditions and the following disclaimer in            */
/* the documentation and/or other materials provided with the distribution.   */
/*                                                                            */
/* Neither the name of Rexx Language Association nor the names                */
/* of its contributors may be used to endorse or promote products             */
/* derived from this software without specific prior written permission.      */
/*                                                                            */
/* THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS        */
/* "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT          */
/* LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS          */
/* FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT   */
/* OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,      */
/* SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED   */
/* TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA,        */
/* OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY     */
/* OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING    */
/* NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS         */
/* SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.               */
/*                                                                            */
/*----------------------------------------------------------------------------*/



-------------------------------------------------------------------------------------------
-- ===> adapt the "testUnitList" to your testCase classes; each element in the list is <===
-- ===> an array object, the first element containing the testCase class object, the   <===
-- ===> second element is a list of test method names which are regarded to be         <===
-- ===> mandatory (if the list remains empty all test methods are mandatory)           <===

   /* list of array objects, each containing the testUnit class object and an
      optional list of mandatory test case methods name                       */

mandatoryTestMethods=.list~new   -- no mandatory tests for this testCase class
testUnitList=.list~of( .array~of(.ooRexx.Base.DO.testUnit,  mandatoryTestMethods) )

-------------------------------------------------------------------------------------------
-- ===> the following code needs not to be individualized                              <===

   -- read top comment, containing infos about this program
arrLines=.array~new
do i=1 to 150 until arrLines[i]="*/"
   arrLines[i]=sourceline(i)
end
      -- supply information for the testClass(es) in this file; the class attribute
      -- "TestCaseInfo" (a directory object, index points to a queue) will store
      -- the parsed infos
aTestUnitClass=testUnitList~at(testUnitList~first)[1] -- get first testClass

   -- will parse the array lines and store result in class object
call makeDirTestInfo aTestUnitClass, arrLines
tmpDir=aTestUnitClass~TestCaseInfo
parse source s   -- op_sys invocationType fullPathToThisFile
tmpDir~setentry("test_Case-source", s)

   -- now add this directory to other testCase classes, if any left
do arr over testUnitList
   if arr[1]=aTestUnitClass then iterate  -- already handled
   arr[1]~TestCaseInfo=tmpDir             -- save info in class object
end

-- if this file is CALLed or REQUIRED then define an entry "bRunTestLocally" in .local
-- and set it to .false; this way the independent local invocation of the tests is inhibited
if .local~hasentry("bRunTestsLocally")=.false then
   .local~bRunTestsLocally=.true -- if this file is executed directly, then run tests for debugging

if .bRunTestsLocally=.true then  -- run ALL tests in this test unit
do
   ts=.testSuite~new             -- create a testSuite
   do arr over testUnitList
      -- create a testSuite for the given test case class, use all its testmethods
      ts~addTest( .testSuite~new(arr[1]))
   end
   -- testResult=.testSuite~new(testUnitClass)~run
   testResult=ts~run       -- now run all the tests

   call simpleDumpTestResults testResult
end

   /* return list of array objects containing test case classes and
      optionally list of mandatory test methods                      */
return testUnitList



::requires ooRexxUnit.cls     -- load the ooRexxUnit classes

::class "ooRexx.Base.DO.testUnit" subclass TestCase public

::method "test_DO"
  i=0
  DO
     i=i+1
     if i>3 then leave
  end
  self~assertEquals("subtest_01", 1, i)


::method "test_DO_4"
  i=0
  DO 4
     i+=1
  end
  self~assertEquals("subtest_01", 4, i)

::method "test_DO_a=4"
  i=0
  a=4
  DO a
     i+=1
  end
  self~assertEquals("subtest_01", a, i)


::method "test_DO_WHILE_i<4"
  i=0
  DO while i<4
     i+=1
  end
  self~assertEquals("subtest_01", 4, i)


::method "test_DO_WHILE_.true, .true, i<4"
  i=0
  DO while .true, .true, i<4
     i+=1
  end
  self~assertEquals("subtest_01", 4, i)


::method "test_DO_UNTIL_i>3"
  i=0
  DO until i>3
     i+=1
  end
  self~assertEquals("subtest_01", 4, i)


::method "test_DO_UNTIL_.true, .true, i>3"
  i=0
  DO until .true, .true, i>3
     i+=1
  end
  self~assertEquals("subtest_01", 4, i)


::method "test_DO_i=1"
  DO i=1
     if i=4 then leave
  end
  self~assertEquals("subtest_01", 4, i)


::method "test_DO_i=1_to_3"
  DO i=1 to 3
     nop
  end
  self~assertEquals("subtest_01", 4, i)


::method "test_DO_i=0_to_3_by_2"
  DO i=0 to 3 by 2
     nop
  end
  self~assertEquals("subtest_01", 4, i)


::method "test_DO_i=0_by_2"
  DO i=0 by 2
     if i>=4 then leave
  end
  self~assertEquals("subtest_01", 4, i)


::method "test_DO_i=-4.04 by 1.01"
  DO i=-4.04 by 1.01
     if i>=4 then leave
  end
  self~assertEquals("subtest_01", 4.04, i)


::method "test_DO_i=4.04 by -1.01"
  DO i=4.04 by -1.01
     if i<=-4 then leave
  end
  self~assertEquals("subtest_01", -4.04, i)



::method "test_DO_LABEL"
      -- test 1
   i=0
   DO label aha1
      self~assertTrue("subtest_01: LABEL subkeyword on DO known", .true)
      i=i+1
      DO label aha2
         self~assertTrue("subtest_02: LABEL subkeyword on DO known", .true)
         leave aha2
         self~fail("subtest_03: 'leave aha2' did not work!")
      end
      i=i+1
      leave aha1
   END aha1

   self~assertEquals("subtest_04: leaving 'aha2'!", 2, i)

      -- test 2
   i=0
   DO label aha1
      self~assertTrue("subtest_05: LABEL subkeyword on DO known", .true)
      i=i+1
      DO label aha2
         self~assertTrue("subtest_06: LABEL subkeyword on DO known", .true)
         leave aha1
         self~fail("subtest_07: 'leave aha2' did not work!")
      end aha2
      i=i+1
   END aha1

   self~assertEquals("subtest_08: leaving 'aha1'!", 1, i)


      -- test 3
   i=0
   DO label aha1
      self~assertTrue("subtest_09: LABEL subkeyword on DO known", .true)
      i=i+1
      DO i=1 to 15
         self~assertTrue("subtest_10: LABEL subkeyword on DO known", .true)
         leave aha1
         self~fail("subtest_11: 'leave aha2' did not work!")
      end i
      i=i+1
      leave
   END aha1

   self~assertEquals("subtest_12: leaving 'aha1'!", 1, i)

     -- test 4
   str="i=0                                                                    ;" -
   "DO label aha1                                                              ;" -
   "   self~assertTrue(""subtest_15: LABEL subkeyword on DO known"", .true)    ;" -
   "   i=i+1                                                                   ;" -
   "   DO label aha2                                                           ;" -
   "      self~assertTrue(""subtest_16: LABEL subkeyword on DO known"", .true) ;" -
   "      leave aha2                                                           ;" -
   "      self~fail(""subtest_17: 'leave aha2' did not work!"")                ;" -
   "   end                                                                     ;" -
   "   i=i+1                                                                   ;" -
   "END aha1                                                                   ;" -
   "self~assertEquals(""subtest_18: leaving 'aha2'!"", 2, i)                   ;"

   interpret str


::method "test_DO_LABEL_error 10.2"

   str="i=0                                                                    ;" -
   "DO label aha1                                                              ;" -
   "   self~assertTrue(""subtest_01: LABEL subkeyword on DO known"", .true)    ;" -
   "   i=i+1                                                                   ;" -
   "   do label aha2                                                           ;" -
   "      self~assertTrue(""subtest_02: LABEL subkeyword on DO known"", .true) ;" -
   "      leave aha2                                                           ;" -
   "      self~fail(""subtest_03: 'leave aha2' did not work!"")                ;" -
   "   end                                                                     ;" -
   "   i=i+1                                                                   ;" -
   "   leave                                                                   ;" -
   "END nixi                                                                   ;" -
   "self~assertEquals(""subtest_04: leaving 'aha2'!"", 2, i)                   ;"

   self~expectSyntax( 10.2 )
   interpret str


