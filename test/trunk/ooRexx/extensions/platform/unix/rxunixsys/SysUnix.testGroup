#!/usr/bin/env rexx
/*
  SVN Revision: $Rev$
  Change Date:  $Date$
*/
/*----------------------------------------------------------------------------*/
/*                                                                            */
/* Copyright (c) 2010-2021 Rexx Language Association. All rights reserved.    */
/*                                                                            */
/* This program and the accompanying materials are made available under       */
/* the terms of the Common Public License v1.0 which accompanies this         */
/* distribution. A copy is also available at the following address:           */
/* http://www.oorexx.org/license.html                                         */
/*                                                                            */
/* Redistribution and use in source and binary forms, with or                 */
/* without modification, are permitted provided that the following            */
/* conditions are met:                                                        */
/*                                                                            */
/* Redistributions of source code must retain the above copyright             */
/* notice, this list of conditions and the following disclaimer.              */
/* Redistributions in binary form must reproduce the above copyright          */
/* notice, this list of conditions and the following disclaimer in            */
/* the documentation and/or other materials provided with the distribution.   */
/*                                                                            */
/* Neither the name of Rexx Language Association nor the names                */
/* of its contributors may be used to endorse or promote products             */
/* derived from this software without specific prior written permission.      */
/*                                                                            */
/* THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS        */
/* "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT          */
/* LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS          */
/* FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT   */
/* OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,      */
/* SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED   */
/* TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA,        */
/* OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY     */
/* OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING    */
/* NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS         */
/* SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.               */
/*                                                                            */
/*----------------------------------------------------------------------------*/
    parse source . . fileSpec

    group = .TestGroup~new(fileSpec)

    group~add(.SysUnix.testGroup)
    group~restrictOS("UNIX")

    .context~package~loadLibrary("rxunixsys")

    if group~isAutomatedTest then return group

    testResult = group~suite~execute~~print

return testResult

-- End of entry point.

::requires 'ooTest.frm'
::class 'SysUnix.testgroup' subclass ooTestCase public

-- these are file system entities wich we assume to always exist
-- this source file, our current directory, our rxapi socket, and /dev/null
::constant file (.context~package~name)
::constant directory (directory())
::constant socket ((value("XDG_RUNTIME_DIR", , "environment") "/tmp")~word(1) || "/.ooRexx-" || .RexxInfo~version || "-" || .RexxInfo~architecture || "-" || userid() || ".service")
::constant characterDevice "/dev/null"

::constant names (.Array~of(self~file, self~directory, self~socket, self~characterDevice))
::constant fileNotThere "/non-existing-directory/non-existing-file"

::method 'testSysUname_1'
    self~assertNotEquals("", SysUname("SYSTEM"))
    self~assertNotEquals("", SysUname("NODE"))
    self~assertNotEquals("", SysUname("RELEASE"))
    self~assertNotEquals("", SysUname("VERSION"))
    self~assertNotEquals("", SysUname("MACHINE"))
    self~assertNotEquals("", SysUname())

::method 'testSysGetpid_1'
    self~assertEquals("NUM", SysGetpid()~datatype())

::method 'testSysGetppid_1'
    self~assertEquals("NUM", SysGetppid()~datatype())

::method 'testSysGettid_1'
    self~assertEquals("NUM", SysGettid()~datatype())

::method 'testSysGetsid_1'
    pid = SysGetpid()
    self~assertEquals("NUM", SysGetsid(pid)~datatype())

::method 'testSysGetuid_1'
    self~assertEquals("NUM", SysGetuid()~datatype())

::method 'testSysGeteuid_1'
    self~assertEquals("NUM", SysGeteuid()~datatype())

::method 'testSysGetgid_1'
    self~assertEquals("NUM", SysGetgid()~datatype())

::method 'testSysGetegid_1'
    self~assertEquals("NUM", SysGetegid()~datatype())

::method 'testSysGetpwuid_1'
    uid = SysGetuid()
    self~assertNotEquals("", SysGetpwuid(uid, "NAME"))
    self~assertNotEquals("", SysGetpwuid(uid, "user"))
    self~assertNotEquals("", SysGetpwuid(uid, "Group"))
    -- at least on zLinux "realname" may be empty, so we just test for String
    self~assertIsA(SysGetpwuid(uid, "real name"), .String)
    self~assertNotEquals("", SysGetpwuid(uid, "dir"))
    self~assertNotEquals("", SysGetpwuid(uid, "shell"))
    self~assertEquals("", SysGetpwuid(uid, "PASSSWORD"))

::method 'testSysGetpwnam_1'
    nam = SysGetpwuid(SysGetuid(), 'name')
    self~assertNotEquals("", SysGetpwnam(nam, 'nam'))
    self~assertNotEquals("", SysGetpwnam(nam, 'usr'))
    self~assertNotEquals("", SysGetpwnam(nam, 'grp'))
    -- at least on zLinux "realname" may be empty, so we just test for String
    self~assertIsA(SysGetpwnam(nam, 'real'), .String)
    self~assertNotEquals("", SysGetpwnam(nam, 'shell'))
    self~assertNotEquals("", SysGetpwnam(nam, 'dir'))
    self~assertEquals("", SysGetpwnam(nam, 'pw'))

::method 'testSysGetgrgid_1'
    gid = SysGetgid()
    self~assertNotEquals("", SysGetgrgid(gid, 'name'))
    self~assertNotEquals("", SysGetgrgid(gid, 'group'))
    self~assertEquals("", SysGetgrgid(gid, 'password'))
    self~assertIsA(SysGetgrgid(gid, 'members'), .Array)

::method 'testSysGetgrnam_1'
    gid = SysGetgid()
    self~assertNotEquals("", SysGetgrnam(SysGetgrgid(gid, 'N'), 'NAME'))
    self~assertNotEquals("", SysGetgrnam(SysGetgrgid(gid, 'N'), 'GROUP'))
    self~assertEquals("", SysGetgrnam(SysGetgrgid(gid, 'N'), 'PW'))
    self~assertTrue(SysGetgrnam(SysGetgrgid(gid, 'N'), 'MEMBER')~isa(.array))


-- SysStat tests

::method test_sysstat_invalid_file
  self~expectSyntax((40.1, "SYSSTAT")) -- External routine "SYSSTAT" failed
  call SysStat "", "size"

::method test_sysstat_invalid_option_empty
  self~expectSyntax((40.1, "SYSSTAT")) -- External routine "SYSSTAT" failed
  call SysStat self~file, ""

::method test_sysstat_invalid_option
  self~expectSyntax((40.1, "SYSSTAT")) -- External routine "SYSSTAT" failed
  call SysStat self~file, "xyzzy"

::method test_sysstat_file_not_exists
  -- all options return the nullstring if the file doesn't exist
  do option over "dipnugrdsamc"~makeArray("")
    self~assertSame("", SysStat(self~fileNotThere, option))
  end

::method test_sysstat_device
  -- we expect a device ID to be a whole number
  -- (on Darwin Catalina 19.3.0 /dev/null device ID is returned as -783243024)
  numeric digits 20

  -- the file and the directory are assumed to reside on the same device
  self~assertSame(SysStat(self~file, "D"), SysStat(self~directory, "DEV"), "file and directory device IDs expected to be the same")

  -- /dev/null is a device of its own
  id = SysStat(self~characterDevice, "d")
  self~assertTrue(id~dataType("whole"), self~characterDevice "device ID should be a whole number; found" id)

  -- st_rdev, device ID (if special file)
  do name over self~names
    id = SysStat(name, "r")
    self~assertTrue(id~dataType("whole"), name "rdev should be a whole number; found" id)
  end

::method test_sysstat_inode
  -- we expect inodes to be unique positive whole numbers
  -- inode numbers may be very large like 12889391383 seen on Darwin
  numeric digits 20
  unique = .Set~new
  do name over self~names
    inode = SysStat(name, "i")
    self~assertTrue(inode~dataType("whole"), name "inode should be a whole number; found" inode)
    self~assertTrue(inode > 0, name "inode should be positive; found" inode)
    self~assertFalse(unique~hasItem(inode), name "inode should be unique; found" inode)
    unique~put(inode)
  end

::method test_sysstat_permissions
  do name over self~names
    permission = SysStat(name, "perm")
    self~assertSame(10, permission~length, name "permission should be 10 chars; found" permission)
    self~assertSame(0, permission~verify("dlscbp-rwxsStT"), name "permission should consist of chars 'dlscbp-rwxsStT' only; found" permission)
    select case name
      when self~directory       then self~assertSame("d", permission~left(1))
      when self~socket          then self~assertSame("s", permission~left(1))
      when self~characterDevice then self~assertSame("c", permission~left(1))
      otherwise nop
    end
  end

::method test_sysstat_numberlinks
  -- we expect the number of links to be a non-negative whole number
  do name over self~names
    links = SysStat(name, "NUMBERLINKS")
    self~assertTrue(links~dataType("whole"), name "links should be a whole number; found" links)
    self~assertTrue(links >= 0, name "links should not be negative; found" links)
  end

::method test_sysstat_ids
  -- we expect owner ID and group ID to be non-negative whole numbers
  numeric digits 20
  do name over self~names
    do option over "UID", "GID"
      id = SysStat(name, option)
      self~assertTrue(id~dataType("whole"), name "ID should be a whole number; found" id)
      self~assertTrue(id >= 0, name "ID should not be negative; found" id)
    end
  end

::method test_sysstat_size
  do name over self~names
    self~assertSame(.File~new(name)~length, SysStat(name, "size"), name "file size")
  end

::method test_sysstat_times
  -- last access time, last modification time, last change to inode time
  do name over self~names
    do option over "access", "modified", "changed"
      timestamp = SysStat(name, option)
      if timestamp~length > 0 then do           -- some time fields may be missing
        self~assertSame(19, timestamp~length)   -- e. g. 2019-10-08 23:01:52
        self~assertSame(2, timestamp~words)     -- must be two words
        self~assertSame(10, timestamp~wordLength(1))
        call date , timestamp~word(1), "ISO"    -- verify it's a valid date
        call time , timestamp~word(2), "Normal" -- verify it's a valid time
      end
    end
  end


::method 'testSysAccess_1'
    parse source . . file
    self~assertEquals("NUM", SysAccess(file, 0)~datatype())
    self~assertEquals("NUM", SysAccess(file, 1)~datatype())
    self~assertEquals("NUM", SysAccess(file, 2)~datatype())
    self~assertEquals("NUM", SysAccess(file, 4)~datatype())

::method test_wordexp
    -- wordexp is expected to be available at least on Linux and Darwin
    if .Rexxinfo~platform == "LINUX" | .Rexxinfo~platform == "DARWIN" then
      self~assertSame(0, rxfuncquery("SysWordExp"), "wordexp should be available on" .Rexxinfo~platform)
    if rxfuncquery("SysWordExp") then
      return

    -- expand all files in current directory
    -- note that wordexp() metachars won't match a leading period
    current = .File~new(".")~list
    do i = 1 to current~items
      if current[i]~startsWith(".") then
        current~remove(i)
    end
    self~assertEquivalentList(current, SysWordExp("*"))

::method test_xattr
    -- xattr functions are available on Linux and Darwin only
    if .Rexxinfo~platform == "LINUX" | .Rexxinfo~platform == "DARWIN" then
      self~assertSame(0, rxfuncquery("SysGetXattr"), "xattr functions should be available on" .Rexxinfo~platform)
    if rxfuncquery("SysGetXattr") then
      return

    -- we create a temporary file within our test tree, avoiding /tmp as it may
    -- be a tmpfs files system not supporting user.* extended attributes
    file = .TemporaryTestFile~new(self, "test_xattr")~create("test_xattr")
    f = file~string

    -- our test file may have been created with some initial extended
    -- attributes, e.g. "security.selinux" on Fedora
    -- we ignore them
    initial = SysListXattr(f)
    -- returns -1 instead of an Array if no extended attribute support
    self~assertIsA(initial, .Array, "may fail if file system has no support for extended attributes")
    self~assertSame("", SysGetXattr(f, "user.author"))
    self~assertSame(-1, SysRemoveXattr(f, "user.author"))

    -- add extended attributes
    self~assertSame(0, SysSetXattr(f, "user.author", "ooRexx"))
    self~assertSame(0, SysSetXattr(f, "user.mime_type", "text/x-oorexx"))

    -- list/get extended attributes
    list = SysListXattr(f)~difference(initial) -- ignore initial attributes
    self~assertIsA(list, .Array)
    self~assertSame(2, list~items)
    self~assertEquivalentList(("user.author", "user.mime_type"), list)
    self~assertSame("text/x-oorexx", SysGetXattr(f, "user.mime_type"))
    self~assertSame("ooRexx", SysGetXattr(f, "user.author"), "may fail on tmpfs:" f)
    self~assertSame("", SysGetXattr(f, "author")) -- does not exist

    -- remove extended attributes
    self~assertSame(-1, SysRemoveXattr(f, "author")) -- does not exist
    self~assertSame(0, SysRemoveXattr(f, "user.author"))
    list = SysListXattr(f)~difference(initial) -- ignore initial attributes
    self~assertSame(1, list~items)
    self~assertSame("user.mime_type", list[1])
    self~assertSame("text/x-oorexx", SysGetXattr(f, "user.mime_type"))

    self~assertSame(-1, SysRemoveXattr(f, "user.author")) -- already removed
    self~assertSame(0, SysRemoveXattr(f, "user.mime_type"))
    self~assertSame(0, SysListXattr(f)~difference(initial)~items)

    file~delete

::method test_SysGetservbyname
  -- we assume the following line exists in /etc/services
  -- http            80/tcp          www
  port = SysGetservbyname("http", "tcp", "port")
  self~assertEquals(80, port, "http port assumed to be 80, not" port);

  name = SysGetservbyname("http", "tcp", "name")
  self~assertEquals(80, port, "http name assumed to be 'http', not" name);
  name = SysGetservbyname("www", "tcp", "n")
  self~assertEquals(80, port, "www name assumed to be 'http', not" name);

::method test_SysGetservbyport
  -- we assume the following line exists in /etc/services
  -- http            80/tcp          www
  port = SysGetservbyport(80, "tcp", "port")
  self~assertEquals(80, port, "port 80 assumed to be 80, not" port);

  name = SysGetservbyport(80, "tcp", "name")
  self~assertEquals(80, port, "port 80 assumed to be 'http', not" name);


::method test_SysCrypt_DES
  salt = "3a"
  des = SysCrypt("passsw", salt)
  self~assertEquals(13, des~length, "13 chars DES-encoded password expected")
  self~assertTrue(des~startsWith(salt), "DES-encoded password should start with salt '3a'")
  self~assertEquals(0, des~verify(self~allowedCryptChars), "DES-encoded chars expected from set [a-zA-Z0-9./]")

::method test_SysCrypt_MD5

  -- Darwin not only doesn't support MD5,
  -- (https://en.wikipedia.org/wiki/Crypt_(C)#Support_in_operating_systems)
  -- it will also just happily return a 13-chars DES-encrypted string instead
  if .ooRexxUnit.OSName == "DARWIN" then
    return

  salt = "$1$salt"                     -- MD5 id is 1, salt may be up to 16 chars
  md5 = SysCrypt("passsw", salt)
  if md5~length \= 0 then do           -- MD5 might not be supported
    self~assertEquals(salt~length + 1 + 22, md5~length, salt~length + 1 + 22 "chars MD5-encoded password expected")
    self~assertTrue(md5~startsWith(salt), "MD5-encoded password should start with '"salt"'")
    self~assertEquals(0, md5~verify(self~allowedCryptChars, , salt~length + 2), "MD5-encoded chars expected from set [a-zA-Z0-9./]")
  end

::method allowedCryptChars
  -- set [a-zA-Z0-9./]
  return .String~alnum || "./"


::options all syntax
