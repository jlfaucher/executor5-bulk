#!/usr/bin/rexx
/*
  SVN Revision: $Rev$
  Change Date:  $Date$
*/
/*----------------------------------------------------------------------------*/
/*                                                                            */
/* Copyright (c) 2007-2017 Rexx Language Association. All rights reserved.    */
/*                                                                            */
/* This program and the accompanying materials are made available under       */
/* the terms of the Common Public License v1.0 which accompanies this         */
/* distribution. A copy is also available at the following address:           */
/* http://www.oorexx.org/license.html                                         */
/*                                                                            */
/* Redistribution and use in source and binary forms, with or                 */
/* without modification, are permitted provided that the following            */
/* conditions are met:                                                        */
/*                                                                            */
/* Redistributions of source code must retain the above copyright             */
/* notice, this list of conditions and the following disclaimer.              */
/* Redistributions in binary form must reproduce the above copyright          */
/* notice, this list of conditions and the following disclaimer in            */
/* the documentation and/or other materials provided with the distribution.   */
/*                                                                            */
/* Neither the name of Rexx Language Association nor the names                */
/* of its contributors may be used to endorse or promote products             */
/* derived from this software without specific prior written permission.      */
/*                                                                            */
/* THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS        */
/* "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT          */
/* LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS          */
/* FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT   */
/* OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,      */
/* SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED   */
/* TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA,        */
/* OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY     */
/* OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING    */
/* NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS         */
/* SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.               */
/*                                                                            */
/*----------------------------------------------------------------------------*/

  -- A test group is a container of all the test cases in a single file.  It
  -- knows how to do the rote chores of getting ready to execute tests.

  -- To instantiate a test group you need the complete name of the file the test
  -- group comes from (this file.)  By using parse source to get the file name
  -- rather than hard coding the name, you allow this file to be moved around in
  -- the file system without having to edit this file.
  parse source . . s

  -- Instantiate the test group.
  group = .TestGroup~new(s)

  -- Once we have the test group object, we can add all the test cases in this
  -- file to the group.  The simplest case is a single test case class, as in
  -- this file.  All we need to do is add the *class* to the group.  Note that
  -- we add the class object itself.
  group~add(.Assignments.testGroup)

  -- The test group knows if an automated test run is being conducted, we simply
  -- ask the object.  And, if it is an automated test, we merely return the
  -- group.
  if group~isAutomatedTest then return group

  -- It is aot an automated test, so we execute the tests now.  The steps are
  -- this:
  --
  -- 1.) Ask the test group to produce a test suite of its executable tests.
  --
  --   testSuite = group~suite
  --
  -- 2.) Ask the test suite to execute the tests and return a test result object
  --
  --   testResult = testSuite~execute
  --
  -- 3.) Ask the test result object to print the test results using its default
  -- formatter.
  --
  --   testResult~print
  --
  -- 4.) Return the test result object.  This is not currently used, but could
  -- be useful in the future, for some as yet unimagined purpose.  <grin>
  --
  -- return testResult
  --
  -- The above 4 steps can be condensed into the following two lines.
  testResult = group~suite~execute~~print

return testResult
-- End of entry point.

-- We are using the ooTest Framework.  This framework sits on top of the
-- ooRexxUnit framework.  All we need to do is require the ooTest.frm file.  We
-- do not do anything directly with the OOREXXUNIT.CLS file
::requires 'ooTest.frm'

-- Our test case class file has to subclass ooTestCase, not TestCase.
::class "Assignments.testGroup" subclass ooTestCase public

-- Now we just write our test case methods.  All methods starting with 'test'
-- will automagically be used as test case methods.

::method "test_+="

  i1=0
  i2=0
  do i=-5 to +5 by 0.1
     i1 =i1+1.01+i
     i2+=   1.01+i
  end

  -- Note this, the arguments to the assertEquals method are:
  --
  -- self~assertEquals(expected, actual <message>)
  --
  -- Message is optional and should *only* be used if it is a message of real
  -- value.  DO NOT use messages like "subtest__01" This adds absolutely no
  -- value.  If the assert fails, the file name, the class name, the method
  -- name, and the line number of the failure are all recorded.  This is more
  -- than enough information to quickly locate *exactly* where the failure is.
  self~assertEquals(i1, i2)

  i3=0
  i3+=10
  self~assertEquals(10, i3)

::method "test_-="

  i1=0
  i2=0
  do i=-5 to +5 by 0.1
     i1 =i1-1.01-i
     i2-=   1.01-i
  end
  self~assertEquals(i1, i2)

  i3=0
  i3-=10
  self~assertEquals(-10, i3)

::method "test_/="

  i1=1
  i2=1
  do i=-5 to +5 by 0.1
     i1 =i1/(1.01+i)
     i2/=   (1.01+i)
  end
  self~assertEquals(i1, i2)

  i3=13
  i3/=10
  self~assertEquals(1.3, i3)

::method "test_//="

  i1=13
  i2=13
  i1 =i1//(1.01+2)
  i2//=   (1.01+2)
  self~assertEquals(i1, i2)

  i3=13
  i3//=10
  self~assertEquals(3, i3)

::method "test_%="

  i1=13
  i2=13
  i1 =i1%(1.01+2)
  i2%=   (1.01+2)
  self~assertEquals(i1, i2)

  i3=13
  i3%=10
  self~assertEquals(1, i3)


::method "test_*="

  i1=13
  i2=13
  i1 =i1*(1.01+2)
  i2*=   (1.01+2)
  self~assertEquals(i1, i2)

  i3=13
  i3*=10
  self~assertEquals(130, i3)

::method "test_**="

  i1=13
  i2=13
  i1 =i1**(1+2)
  i2**=   (1+2)
  self~assertEquals(i1, i2)

  i3=3
  i3**=4
  self~assertEquals(81, i3)

::method "test_|="

  i1=.false
  i2=.false
  i1 =i1|.true
  i2|=   .true
  self~assertEquals(i1, i2)

::method "test_&="

  i1=.false
  i2=.false
  i1 =i1&.true
  i2&=   .true
  self~assertEquals(i1, i2)

::method "test_&&="

  i1=.false
  i2=.false
  i1 =i1&&.true
  i2&&=   .true
  self~assertEquals(i1, i2)

::method "test_||="

  i1=.false
  i2=.false
  i1 =i1||.true
  i2||=   .true
  self~assertEquals(i1, i2)


::options novalue error
