#!/usr/bin/rexx
/*
  SVN Revision: $Rev$
  Change Date:  $Date$
*/
/*----------------------------------------------------------------------------*/
/*                                                                            */
/* Copyright (c) 2007-2017 Rexx Language Association. All rights reserved.    */
/*                                                                            */
/* This program and the accompanying materials are made available under       */
/* the terms of the Common Public License v1.0 which accompanies this         */
/* distribution. A copy is also available at the following address:           */
/* http://www.oorexx.org/license.html                                         */
/*                                                                            */
/* Redistribution and use in source and binary forms, with or                 */
/* without modification, are permitted provided that the following            */
/* conditions are met:                                                        */
/*                                                                            */
/* Redistributions of source code must retain the above copyright             */
/* notice, this list of conditions and the following disclaimer.              */
/* Redistributions in binary form must reproduce the above copyright          */
/* notice, this list of conditions and the following disclaimer in            */
/* the documentation and/or other materials provided with the distribution.   */
/*                                                                            */
/* Neither the name of Rexx Language Association nor the names                */
/* of its contributors may be used to endorse or promote products             */
/* derived from this software without specific prior written permission.      */
/*                                                                            */
/* THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS        */
/* "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT          */
/* LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS          */
/* FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT   */
/* OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,      */
/* SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED   */
/* TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA,        */
/* OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY     */
/* OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING    */
/* NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS         */
/* SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.               */
/*                                                                            */
/*----------------------------------------------------------------------------*/
  parse source . . fileSpec

  group = .TestGroup~new(fileSpec)
  group~add(.LoopOther.testGroup)

  if group~isAutomatedTest then return group

  testResult = group~suite~execute~~print

return testResult
-- End of entry point.

::requires 'ooTest.frm' -- load the ooRexxUnit classes
::requires 'FileUtils.cls'

::class "LoopOther.testGroup" subclass ooTestCase public
::method test_forever

  count = 0
  loop forever
     count += 1
     if count > 2 then leave
  end

  self~assertEquals(3, count)

  count = 0
  loop label foo forever
     count += 1
     if count > 2 then leave foo
  end foo

  self~assertEquals(3, count)

  count = 0
  loop label foo forever while count < 3
     count += 1
  end foo

  self~assertEquals(3, count)

  count = 0
  loop label foo forever until count > 2
     count += 1
  end foo

  self~assertEquals(3, count)

::method test_count

  count = 0
  loop 3
     count += 1
  end

  self~assertEquals(3, count)

  count = 0
  loop label foo 0
     count += 1
  end foo

  self~assertEquals(0, count)

  count = 0
  loop label foo 4 while count < 3
     count += 1
  end foo

  self~assertEquals(3, count)

  count = 0
  loop label foo 4 until count > 2
     count += 1
  end foo

  self~assertEquals(3, count)

::method test_while

  count = 0
  loop label foo while count < 3
     count += 1
  end foo

  self~assertEquals(3, count)

  count = 0
  loop while count > 0
     count += 1
  end

  self~assertEquals(0, count)

  count = 0
  loop label foo while count > 0, .false
     count += 1
  end foo

  self~assertEquals(0, count)

  count = 0
  loop while .true, count < 3
     count += 1
  end

  self~assertEquals(3, count)

  count = 0
  loop label foo while .false, count < 3
     count += 1
  end foo

  self~assertEquals(0, count)

::method test_until

  count = 0
  loop label foo until count > 2
     count += 1
  end foo

  self~assertEquals(3, count)

  count = 0
  loop until count >= 0
     count += 1
  end

  self~assertEquals(1, count)

  count = 0
  loop label foo until count > 0, count > 2
     count += 1
  end foo

  self~assertEquals(3, count)

  count = 0
  loop until .true, count = 3
     count += 1
  end

  self~assertEquals(3, count)

  count = 0
  loop label foo until .false, count < 3
     count += 1
     if count = 3 then leave foo
  end foo

  self~assertEquals(3, count)

::method test_simple_loop

  count = 0
  loop
     count += 1
     if count > 2 then leave
  end

  self~assertEquals(3, count)

  count = 0
  loop label foo
     count += 1
     if count > 2 then leave foo
  end foo

  self~assertEquals(3, count)

::method test_forever_extra
  self~assertSyntaxError(27.901, ("loop forever foo", "end"))

::method test_forever_dup_while
  self~assertSyntaxError(27.1, ("loop forever while .true while .true", "end"))

::method test_forever_dup_until
  self~assertSyntaxError(27.1, ("loop forever until .true until .true", "end"))

::method test_forever_while_until
  self~assertSyntaxError(27.1, ("loop forever while .true until .true", "end"))

::method test_forever_until_while
  self~assertSyntaxError(27.1, ("loop forever until .true while .true", "end"))

::method test_count_dup_while
  self~assertSyntaxError(27.1, ("loop 3 while .true while .true", "end"))

::method test_count_dup_until
  self~assertSyntaxError(27.1, ("loop 3 until .true until .true", "end"))

::method test_count_while_until
  self~assertSyntaxError(27.1, ("loop 3 while .true until .true", "end"))

::method test_count_until_while
  self~assertSyntaxError(27.1, ("loop 3 until .true while .true", "end"))

::method test_for_negative
  self~expectSyntax(26.3)
  loop with index i over (1,2) for -1
  end

::method test_for_non_integer
  self~expectSyntax(26.3)
  loop with index i over (1,2) for 1.1
  end

::method test_for_non_numeric
  self~expectSyntax(26.3)
  loop with index i over (1,2) for 'a'
  end

::method test_count_negative
  self~expectSyntax(26.2)
  loop -1
  end

::method test_count_non_integer
  self~expectSyntax(26.2)
  loop 1.1
  end

::method test_count_non_numeric
  self~expectSyntax(26.2)
  loop 'a'
  end

::method test_forever_while_bad_truth
  self~expectSyntax(34.3)
  loop forever while 1.0
  end

::method test_forever_until_bad_truth
  self~expectSyntax(34.4)
  loop forever until 1.0
  end

::method test_count_while_bad_truth
  self~expectSyntax(34.3)
  loop 3 while 1.0
  end

::method test_count_until_bad_truth
  self~expectSyntax(34.4)
  loop 3 until 1.0
  end

::method test_while_bad_truth
  self~expectSyntax(34.3)
  loop while 1.0
  end

::method test_until_bad_truth
  self~expectSyntax(34.4)
  loop until 1.0
  end


::options novalue error
