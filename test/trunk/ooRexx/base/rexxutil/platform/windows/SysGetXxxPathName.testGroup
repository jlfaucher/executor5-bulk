#!/usr/bin/rexx
/*
  SVN Revision: $Rev$
  Change Date:  $Date: 2016-10-09 00:00:00 +0200
*/
/*----------------------------------------------------------------------------*/
/*                                                                            */
/* Copyright (c) 2016-2017 Rexx Language Association. All rights reserved.    */
/*                                                                            */
/* This program and the accompanying materials are made available under       */
/* the terms of the Common Public License v1.0 which accompanies this         */
/* distribution. A copy is also available at the following address:           */
/* http://www.oorexx.org/license.html                                         */
/*                                                                            */
/* Redistribution and use in source and binary forms, with or                 */
/* without modification, are permitted provided that the following            */
/* conditions are met:                                                        */
/*                                                                            */
/* Redistributions of source code must retain the above copyright             */
/* notice, this list of conditions and the following disclaimer.              */
/* Redistributions in binary form must reproduce the above copyright          */
/* notice, this list of conditions and the following disclaimer in            */
/* the documentation and/or other materials provided with the distribution.   */
/*                                                                            */
/* Neither the name of Rexx Language Association nor the names                */
/* of its contributors may be used to endorse or promote products             */
/* derived from this software without specific prior written permission.      */
/*                                                                            */
/* THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS        */
/* "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT          */
/* LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS          */
/* FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT   */
/* OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,      */
/* SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED   */
/* TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA,        */
/* OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY     */
/* OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING    */
/* NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS         */
/* SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.               */
/*                                                                            */
/*----------------------------------------------------------------------------*/

parse source . . fileSpec;

group = .TestGroup~new(fileSpec)
group~add(.SysGetXxxPathName.testGroup)
group~restrictOS("WINDOWS")

if group~isAutomatedTest then return group

testResult = group~suite~execute~~print

return testResult
-- End of entry point.

::requires 'ooTest.frm'
::class 'SysGetXxxPathName.testgroup' subclass ooTestCase public

::method test_long_no_args
  self~expectSyntax(88.901) -- Error 88.901:  Missing argument; the 1 argument is required
  call SysGetLongPathName

::method test_short_no_args
  self~expectSyntax(88.901) -- Error 88.901:  Missing argument; the 1 argument is required
  call SysGetShortPathName

::method test_long_two_args
  self~expectSyntax(88.922) -- Error 88.922:  Too many arguments in invocation; 1 expected
  call SysGetLongPathName "", ""

::method test_short_two_args
  self~expectSyntax(88.922) -- Error 88.922:  Too many arguments in invocation; 1 expected
  call SysGetShortPathName "", ""

::method test_arg_null
  self~assertEquals("", SysGetLongPathName(""))
  self~assertEquals("", SysGetShortPathName(""))

::method test_arg_invalid
  self~assertEquals("", SysGetLongPathName(":"))
  self~assertEquals("", SysGetShortPathName(":"))

::method test_arg_absolute
  do file over .File~new('.')~listFiles
    if \file~exists then -- ignore spurious temp files from other test groups
      iterate
    path = file~absolutePath
    long = SysGetLongPathName(path)
    short = SysGetLongPathName(path)
    self~assertEquals(path, long)
    self~assertNotEquals("", short)
    self~assertTrue(short~length <= long~length)
    self~assertEquals(path, SysGetLongPathName(SysGetShortPathName(long)))
  end

::method test_arg_relative
  do path over .File~new('.')~list
    if \.File~new(path)~exists then -- ignore spurious temp files from other test groups
      iterate
    long = SysGetLongPathName(path)
    short = SysGetLongPathName(path)
    self~assertEquals(path, long)
    self~assertNotEquals("", short)
    self~assertTrue(short~length <= long~length)
    self~assertEquals(path, SysGetLongPathName(SysGetShortPathName(long)))
  end

-- test the path size limit of 259 chars
::method test_arg_limit
  -- create a temporary file xx..xx so that currentDir\xx..xx has a length of 259
  longName = "x"~copies(259 - directory()~length - 1)
  longFile = .File~new(longName)
  longStream = .Stream~new(longFile)
  self~assertEquals("READY:", longStream~open("write replace"))
  longStream~close
  self~assertTrue(longFile~exists, "zero-length temp file should have been created")

  path = longFile~absolutePath
  long = SysGetLongPathName(path)
  short = SysGetShortPathName(path)
  self~assertEquals(path, long)
  self~assertEquals(259, long~length)
  self~assertEquals(SysGetShortPathName(directory())~length + 1 + 8, short~length)
  self~assertEquals(long, SysGetLongPathName(SysGetShortPathName(long)))

  -- remove temporary file
  longFile~delete


::options novalue error
