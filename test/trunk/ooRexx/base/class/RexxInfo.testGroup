#!/usr/bin/rexx
/*
  SVN Revision: $Rev: 8249 $
  Change Date:  $Date: 2012-08-25 00:57:30 +0200 (Sa, 25 Aug 2012) $
*/
/*----------------------------------------------------------------------------*/
/*                                                                            */
/* Copyright (c) 2007 Rexx Language Association. All rights reserved.         */
/*                                                                            */
/* This program and the accompanying materials are made available under       */
/* the terms of the Common Public License v1.0 which accompanies this         */
/* distribution. A copy is also available at the following address:           */
/* http://www.oorexx.org/license.html                                         */
/*                                                                            */
/* Redistribution and use in source and binary forms, with or                 */
/* without modification, are permitted provided that the following            */
/* conditions are met:                                                        */
/*                                                                            */
/* Redistributions of source code must retain the above copyright             */
/* notice, this list of conditions and the following disclaimer.              */
/* Redistributions in binary form must reproduce the above copyright          */
/* notice, this list of conditions and the following disclaimer in            */
/* the documentation and/or other materials provided with the distribution.   */
/*                                                                            */
/* Neither the name of Rexx Language Association nor the names                */
/* of its contributors may be used to endorse or promote products             */
/* derived from this software without specific prior written permission.      */
/*                                                                            */
/* THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS        */
/* "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT          */
/* LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS          */
/* FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT   */
/* OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,      */
/* SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED   */
/* TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA,        */
/* OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY     */
/* OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING    */
/* NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS         */
/* SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.               */
/*                                                                            */
/*----------------------------------------------------------------------------*/
  parse source . . fileSpec

  group = .TestGroup~new(fileSpec)
  group~add(.RexxInfo.testGroup)

  if group~isAutomatedTest then return group

  testResult = group~suite~execute~~print

return testResult
-- End of entry point.

::requires 'ooTest.frm' -- load the ooRexxUnit classes


::class "RexxInfo.testGroup" subclass ooTestCase public

::method Test_RexxInfo
  /*
  .RexxInfo:
  architecture: 64
  casesensitivefiles: 0
  date: 4 Oct 2014
  digits: 9
  endofline:
  form: SCIENTIFIC
  fuzz: 0
  internaldigits: 18
  languagelevel: 6.05
  majorversion: 5
  name: REXX-ooRexx_5.0.0(MT)_64-bit 6.05 4 Oct 2014
  pathseparator: ;
  platform: WindowsNT
  release: 0
  revision: 0
  version: 5.0.0

  parse version:
  REXX-ooRexx_5.0.0(MT)_64-bit 6.05 10 Oct 2014

  parse source:
  WindowsNT COMMAND ...
  */

  self~assertTrue(.nil\==.environment['REXXINFO'],'.environment must contain "REXXINFO"')

  MethodsShouldBe=.array~of(,            -- Should "NAME" also be included?
  'ARCHITECTURE','CASESENSITIVEFILES','DATE','DIGITS','DIRECTORYSEPARATOR','ENDOFLINE',,
  'FORM','FUZZ','INTERNALDIGITS','LANGUAGELEVEL','MAJORVERSION','NAME','PACKAGE',,
  'PATHSEPARATOR','PLATFORM','RELEASE','REVISION','VERSION')
  MethodsAre=.RexxInfo~instanceMethods(.RexxInfo~class)~allIndexes
  self~assertTrue(MethodsShouldBe~subset(MethodsAre),'.RexxInfo must provide at least' MethodsShouldBe~items 'instance methods; missing is' MethodsShouldBe~difference(MethodsAre)~sort~makeString(,','))

  self~assertTrue(.RexxInfo~package~isA(.Package),'.RexxInfo~package must return a .Package instance')

  self~assertTrue(.RexxInfo~digits~dataType=='NUM','.RexxInfo~digits must return a numeric value')
  self~assertTrue(.RexxInfo~digits==digits(),'.RexxInfo~digits is expected to be current digits()')

  self~assertTrue(.RexxInfo~fuzz~dataType=='NUM','.RexxInfo~fuzz must return a numeric value')
  self~assertTrue(.RexxInfo~fuzz==fuzz(),'.RexxInfo~fuzz is expected to be current fuzz()')

  self~assertTrue('ENGINEERING SCIENTIFIC'~wordPos(.RexxInfo~form)>0,'.RexxInfo~form must return either "ENGINEERING" or "SCIENTIFIC"')
  self~assertTrue(.RexxInfo~form==form(),'.RexxInfo~form is expected to be current form()')

  self~assertTrue(.RexxInfo~internalDigits~dataType=='NUM','.RexxInfo~internalDigits must return a numeric value')
  self~assertTrue(.RexxInfo~internalDigits>=digits(),'.RexxInfo~internalDigits is expected to be equal to or larger than digits()')

  self~assertTrue(.RexxInfo~name~length>0,'.RexxInfo~name is expected to return a non-null string')
  parse version Version
  self~assertTrue(.RexxInfo~name==Version,'.RexxInfo~name is expected to be equal to parse version')

  self~assertTrue(.RexxInfo~languageLevel~length>0,'.RexxInfo~languageLevel is expected to return a non-null string')

  self~assertTrue(.RexxInfo~version==.RexxInfo~majorVersion||'.'||.RexxInfo~release||'.'||.RexxInfo~revision,'.RexxInfo~version expected to be equal to .RexxInfo~majorVersion . .RexxInfo~release . .RexxInfo~revision')

  self~assertTrue(.RexxInfo~date~length>0,'.RexxInfo~date is expected to return a non-null string')
  self~assertTrue(.RexxInfo~date==.RexxInfo~name~right(.RexxInfo~date~length),'.RexxInfo~date is expected to be equal to the last tokens of .RexxInfo~name')

  self~assertTrue(.RexxInfo~platform~length>0,'.RexxInfo~platform is expected to return a non-null string')
  parse source Platform .
  self~assertTrue(.RexxInfo~platform==Platform,'.RexxInfo~platform is expected to be equal to parse source first token')
  parse value SysVersion() with Platform .
  self~assertTrue(.RexxInfo~platform==Platform,'.RexxInfo~platform is expected to be equal to SysVersion() first token')

  self~assertTrue(.RexxInfo~architecture~length>0,'.RexxInfo~architecture is expected to return a non-null string')

  self~assertTrue(.RexxInfo~endofline==.endofline,'.RexxInfo~endofline must be equal to .endofline')

  self~assertTrue(.rexxinfo~pathSeparator==.File~pathSeparator,'.RexxInfo~pathSeparator expected to be equal to .File~pathSeparator')

  self~assertTrue(.rexxinfo~directorySeparator==.File~separator,'.RexxInfo~directorySeparator expected to equal to be .File~separator')

  self~assertTrue('0 1'~wordPos(.RexxInfo~caseSensitiveFiles)>0,'.RexxInfo~caseSensitiveFiles must return .true or .false')
  self~assertTrue(.RexxInfo~caseSensitiveFiles==.File~isCaseSensitive,'.RexxInfo~caseSensitiveFiles must be equal to .File~isCaseSensitive')
