#!/usr/bin/rexx
/*
  SVN Revision: $Rev: 11306 $
  Change Date:  $Date: 2017-10-07 20:21:20 +0200 (Sa, 07 Okt 2017) $
*/
/*----------------------------------------------------------------------------*/
/*                                                                            */
/* Copyright (c) 2017 Rexx Language Association. All rights reserved.         */
/*                                                                            */
/* This program and the accompanying materials are made available under       */
/* the terms of the Common Public License v1.0 which accompanies this         */
/* distribution. A copy is also available at the following address:           */
/* http://www.oorexx.org/license.html                                         */
/*                                                                            */
/* Redistribution and use in source and binary forms, with or                 */
/* without modification, are permitted provided that the following            */
/* conditions are met:                                                        */
/*                                                                            */
/* Redistributions of source code must retain the above copyright             */
/* notice, this list of conditions and the following disclaimer.              */
/* Redistributions in binary form must reproduce the above copyright          */
/* notice, this list of conditions and the following disclaimer in            */
/* the documentation and/or other materials provided with the distribution.   */
/*                                                                            */
/* Neither the name of Rexx Language Association nor the names                */
/* of its contributors may be used to endorse or promote products             */
/* derived from this software without specific prior written permission.      */
/*                                                                            */
/* THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS        */
/* "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT          */
/* LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS          */
/* FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT   */
/* OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,      */
/* SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED   */
/* TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA,        */
/* OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY     */
/* OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING    */
/* NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS         */
/* SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.               */
/*                                                                            */
/*----------------------------------------------------------------------------*/
  parse source . . s
  group = .TestGroup~new(s)
  group~add(.String_moduloMethod.testGroup)

  if group~isAutomatedTest then return group

  testResult = group~suite~execute~~print

return testResult

::requires 'ooTest.frm' -- load the ooRexxUnit classes

::class "String_moduloMethod.testGroup" subclass ooTestCase public

::method test_dividend_non_numeric
  -- MODULO method target must be a number
  self~expectSyntax(93.943)
  "s"~modulo(1)

::method test_dividend_float
  -- MODULO method target must be a whole number
  self~expectSyntax(93.940)
  1.1~modulo(1)

::method test_no_arg
  -- Missing argument in method; argument 1 is required
  self~expectSyntax(93.903)
  0~modulo()

::method test_two_args
  -- Too many arguments in invocation of method; 1 expected
  self~expectSyntax(93.902)
  0~modulo(1, 1)

::method test_divisor_nil
  -- Method argument 1 must be a positive whole number
  self~expectSyntax(93.907)
  0~modulo(.nil)

::method test_divisor_null
  -- Method argument 1 must be a positive whole number
  self~expectSyntax(93.907)
  0~modulo("")

::method test_divisor_string
  -- Method argument 1 must be a positive whole number
  self~expectSyntax(93.907)
  0~modulo("a")

::method test_divisor_float
  -- Method argument 1 must be a positive whole number
  self~expectSyntax(93.907)
  0~modulo(1.1)

::method test_divisor_negative
  -- Method argument 1 must be a positive whole number
  self~expectSyntax(93.907)
  0~modulo(-1)

::method test_divisor_zero
  -- Method argument 1 must be a positive whole number
  self~expectSyntax(93.907)
  0~modulo(0)

::method test_range_ten
  do n = 1 to 10
    do a = -10 to 10
      r = a~modulo(n)
      -- our module is always 0 .. n-1 
      self~assertTrue(0 <= r & r < n)
      -- assertion a = n * q + r must hold
      self~assertSame(0, (a - r) // n)
    end
  end

::method test_large_dividend_powers_of_2
  numeric digits 25
  do p = 1 to 80
    power = 2 ** p
    self~assertSame(0, power~modulo(2))
    self~assertSame(0, (-power)~modulo(2))
  end

::method test_large_dividend_powers_of_3
  numeric digits 25
  do p = 1 to 50
    power = 3 ** p
    self~assertSame(0, power~modulo(3))
    self~assertSame(1, (power + 1)~modulo(3))
    self~assertSame(2, (power + 2)~modulo(3))
    self~assertSame(0, (-power)~modulo(3))
    self~assertSame(1, (-power + 1)~modulo(3))
    self~assertSame(2, (-power + 2)~modulo(3))
  end

::method test_large_divisor
  numeric digits 25
  do p = 1 to 24
    self~assertSame(1, 1~modulo(10 ** p))
    self~assertSame(10 ** p - 1, (-1)~modulo(10 ** p))
  end


::options novalue error
