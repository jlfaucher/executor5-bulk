#!/usr/bin/env rexx
/*
  svn, version 1.9.4 (r1740329)
  Change Date: Do  7 Feb 2019 08:19:22 CET
*/
/*----------------------------------------------------------------------------*/
/*                                                                            */
/* Copyright (c) 2007 - 2019 Rexx Language Association. All rights reserved.  */
/*                                                                            */
/* This program and the accompanying materials are made available under       */
/* the terms of the Common Public License v1.0 which accompanies this         */
/* distribution. A copy is also available at the following address:           */
/* http://www.oorexx.org/license.html                                         */
/*                                                                            */
/* Redistribution and use in source and binary forms, with or                 */
/* without modification, are permitted provided that the following            */
/* conditions are met:                                                        */
/*                                                                            */
/* Redistributions of source code must retain the above copyright             */
/* notice, this list of conditions and the following disclaimer.              */
/* Redistributions in binary form must reproduce the above copyright          */
/* notice, this list of conditions and the following disclaimer in            */
/* the documentation and/or other materials provided with the distribution.   */
/*                                                                            */
/* Neither the name of Rexx Language Association nor the names                */
/* of its contributors may be used to endorse or promote products             */
/* derived from this software without specific prior written permission.      */
/*                                                                            */
/* THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS        */
/* "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT          */
/* LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS          */
/* FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT   */
/* OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,      */
/* SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED   */
/* TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA,        */
/* OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY     */
/* OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING    */
/* NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS         */
/* SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.               */
/*                                                                            */
/*----------------------------------------------------------------------------*/

/*

  use the singleton.cls metaclass; a "metaclass" is always a subclass of "class"

  Purpose: make some tests for each ooRexx sample pgm distributed.

  Built from a template provided by Rony G. Flatscher 2018-02-25
  Amended based on month.testgroup for more complete testing.

  Where possible the assertions are done both ways to make sure we
  do not get to much or to little output.

*/

  parse source . . fileSpec;

  group = .TestGroup~new(fileSpec)
  group~add(.usesingleton.testGroup)

  if locateSamplePrg("usesingleton.rex") == .nil then
    group~markNoTests("This is a test of a sample ooRexx program, but the sample can not be located")

  if group~isAutomatedTest then return group

  testResult = group~suite~execute~~print

return testResult
-- End of entry point.

/* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*\
  Directives, Classes, or Routines.
\* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*/
::requires 'ooTest.frm'
::requires 'FileUtils.cls'

::class "usesingleton.testGroup" subclass ooTestCase public

  ::attribute lookup class
  ::method init class
    forward class (super) continue
    self~ooTestType = self~SAMPLE_TEST

    l = .directory~new
    l~shouldStop = .true
    l~prgPath = ""
    l~prgOut = .array~new
    self~lookup = l

  ::method test_01_sampleExists

    lookup = self~class~lookup
    lookup~shouldStop = .true
    lookup~prgOut = .array~new

    prgPath = locateSamplePrg("usesingleton.rex")

    -- Assert that the sample was located and that it exists.
    self~assertNotNull(prgPath)
    self~assertNotSame("", .stream~new(prgPath)~query("EXISTS"))

    lookup~prgPath = prgPath
    lookup~shouldStop = .false

  ::method "test_04_usesingleton.rex"

    -- typical response, identitzHash will change every time
    Line.1  =  "creating 3 objects of type: The TEST class                "
    Line.2  =  "   round # 1: a TEST[nr=1,identityHash=-54934281]         "
    Line.3  =  "   round # 2: a TEST[nr=2,identityHash=-54937801]         "
    Line.4  =  "   round # 3: a TEST[nr=3,identityHash=-54941321]         "
    Line.5  =  "                                                          "
    Line.6  =  "creating 3 objects of type: The TESTSINGLETON class       "
    Line.7  =  "   round # 1: a TESTSINGLETON[nr=1,identityHash=-54963305]"
    Line.8  =  "   round # 2: a TESTSINGLETON[nr=1,identityHash=-54963305]"
    Line.9  =  "   round # 3: a TESTSINGLETON[nr=1,identityHash=-54963305]"
    Line.10 =  "                                                          "
    Line.0  =  10

    currPath   = directory()
    prgPath    = locateSamplePrg("usesingleton.rex")
    samplePath = filespec('location',prgPath)  -- on Windows we need the drive letter as well!
    sampleName = filespec('name',prgPath)

    -- We need to move to where the sample is
    -- Execute the test
    -- Then move back to where we were before
    SELECT CASE .ooRexxUnit.OSName
      when 'WINDOWS' then do
        address command 'cd' '"'samplePath'"'
        address command 'rexx' '"'sampleName'"' with output stem myStem.
        address command 'cd' '"'currPath'"'
      end
      otherwise do
        address '' 'cd' samplePath
        address '' 'rexx' sampleName with output stem myStem.
        address '' 'cd' currPath
      end
    END /* Select */

    self~assertEquals(Line.0, myStem.0)
    self~assertEquals(Line.1, myStem.1)
    self~assertEquals(Line.5, myStem.5)
    self~assertEquals(Line.6, myStem.6)
    self~assertEquals(Line.10, myStem.10)

    do i=2 to 4
       parse var line.i leadin ",identityHash" .
       self~assertTrue(myStem.i~startsWith(leadin),"line."i "does not start with ["leadin"]")
    end

    do i=7 to 9
       parse var line.i leadin ",identityHash" .
       self~assertTrue(myStem.i~startsWith(leadin),"line."i "does not start with ["leadin"]")
    end

--we are done in this testgroup
