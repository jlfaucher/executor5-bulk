#!/usr/bin/env rexx
/*
  svn, version 1.9.4 (r1740329)
  Change Date: Do  7 Feb 2019 08:19:22 CET
*/
/*----------------------------------------------------------------------------*/
/*                                                                            */
/* Copyright (c) 2007 - 2021 Rexx Language Association. All rights reserved.  */
/*                                                                            */
/* This program and the accompanying materials are made available under       */
/* the terms of the Common Public License v1.0 which accompanies this         */
/* distribution. A copy is also available at the following address:           */
/* http://www.oorexx.org/license.html                                         */
/*                                                                            */
/* Redistribution and use in source and binary forms, with or                 */
/* without modification, are permitted provided that the following            */
/* conditions are met:                                                        */
/*                                                                            */
/* Redistributions of source code must retain the above copyright             */
/* notice, this list of conditions and the following disclaimer.              */
/* Redistributions in binary form must reproduce the above copyright          */
/* notice, this list of conditions and the following disclaimer in            */
/* the documentation and/or other materials provided with the distribution.   */
/*                                                                            */
/* Neither the name of Rexx Language Association nor the names                */
/* of its contributors may be used to endorse or promote products             */
/* derived from this software without specific prior written permission.      */
/*                                                                            */
/* THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS        */
/* "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT          */
/* LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS          */
/* FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT   */
/* OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,      */
/* SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED   */
/* TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA,        */
/* OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY     */
/* OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING    */
/* NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS         */
/* SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.               */
/*                                                                            */
/*----------------------------------------------------------------------------*/

/*

  rexxcps.rex  measuring REXX clauses/second

  Purpose: make some tests for each ooRexx sample pgm distributed.

  Built from a template provided by Rony G. Flatscher 2018-02-25
  Amended based on month.testgroup for more complete testing.

  Where possible the assertions are done both ways to make sure we
  do not get to much or to little output.

*/

  parse source . . fileSpec;

  group = .TestGroup~new(fileSpec)
  group~add(.rexxcps.testGroup)

  if locateSamplePrg("rexxcps.rex") == .nil then
    group~markNoTests("This is a test of a sample ooRexx program, but the sample can not be located")

  if group~isAutomatedTest then return group

  testResult = group~suite~execute~~print

return testResult
-- End of entry point.

/* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*\
  Directives, Classes, or Routines.
\* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*/
::requires 'ooTest.frm'
::requires 'FileUtils.cls'

::class "rexxcps.testGroup" subclass ooTestCase public

  ::attribute lookup class
  ::method init class
    forward class (super) continue
    self~ooTestType = self~SAMPLE_TEST

    l = .directory~new
    l~shouldStop = .true
    l~prgPath = ""
    l~prgOut = .array~new
    self~lookup = l

  ::method test_01_sampleExists

    lookup = self~class~lookup
    lookup~shouldStop = .true
    lookup~prgOut = .array~new

    prgPath = locateSamplePrg("rexxcps.rex")

    -- Assert that the sample was located and that it exists.
    self~assertNotNull(prgPath)
    self~assertNotSame("", .stream~new(prgPath)~query("EXISTS"))

    lookup~prgPath = prgPath
    lookup~shouldStop = .false

  ::method  test_02_executes

    lookup = self~class~lookup
    lookup~shouldStop = .true

    ret = execRexxPrg(lookup~prgPath, lookup~prgOut)

    -- Assert there was not an internal error, that output was produced, and
    -- that the return code is 0.
    self~assertNotSame(9999, ret)
    self~assertTrue(lookup~prgOut~items > 0)
    self~assertSame(0, ret)

    -- All okay, keep going.
    lookup~stopStop = .false

  ::method test_03_badArg

-- here an illegal input is actually noticed and produces an error 26.2

    lookup = self~class~lookup
    lookup~shouldStop = .true
    lookup~prgOut~empty

    ret = execRexxPrgWithArgs(lookup~prgPath, "dummyInput", lookup~prgOut)

    -- The program need a numeric argument, so we should get an error
    self~assertNotSame(0, ret)
    self~assertTrue(lookup~prgOut~items > 0)

    -- All okay, keep going.
    lookup~stopStop = .false

::method "test_04_rexxcps.rex"

  Line.1 = '----- REXXCPS 2.1 -- Measuring REXX clauses/second -----'
  Line.2 = ' REXX version is: REXX-ooRexx_5.0.0(MT)_64-bit 6.05 4 Sep 2018'
  Line.3 = '       System is: DARWIN'
  Line.4 = '       Averaging: 100 measures of 100 iterations'
  Line.5 = ''
  Line.6 = 'Total (full DO): 0.00960710 secs (average of 100 measures of 100 iterations)'
  Line.7 = 'Time for one iteration (1000 clauses) was: 0.000096071 seconds'
  Line.8 = ''
  Line.9 = '     Performance: 10408968 REXX clauses per second'
  Line.0 = 9

  prgPathName = locateSamplePrg("rexxcps.rex")

  SELECT CASE .ooRexxUnit.OSName
    when 'WINDOWS' then address command 'rexx' '"'prgPathName'"' with output stem myStem.
    otherwise           address ''      'rexx' prgPathName with output stem myStem.
  END /* Select */

  self~assertEquals(Line.1,myStem.1)
  self~assertEquals(Line.2~subword(1,3),myStem.2~subword(1,3))
  self~assertEquals(Line.3~subword(1,2),myStem.3~subword(1,2))
  self~assertEquals(Line.4~subword(1,1),myStem.4~subword(1,1))
  self~assertEquals(Line.4~subword(3,2),myStem.4~subword(3,2))
  self~assertEquals(Line.4~subword(6,1),myStem.4~subword(6,1))
  self~assertEquals(Line.5,myStem.5)
  self~assertEquals(Line.6~subword(1,3),myStem.6~subword(1,3))
  self~assertEquals(Line.6~subword(5,3),myStem.6~subword(5,3))
  self~assertEquals(Line.6~subword(9,2),myStem.6~subword(9,2))
  self~assertEquals(Line.6~subword(12,1),myStem.6~subword(12,1))
  self~assertEquals(Line.7~subword(1,7),myStem.7~subword(1,7))
  self~assertEquals(Line.7~subword(9,1),myStem.7~subword(9,1))
  self~assertEquals(Line.8,myStem.8)
  self~assertEquals(Line.9~subword(1,1),myStem.9~subword(1,1))
  self~assertEquals(Line.9~subword(3,3),myStem.9~subword(3,3))

--we are done in this testgroup
