#!/usr/bin/env rexx
/*
This tool creates the ooRexx class hierarchy for the "rexxpg" book, chapter "provide.xml".

The settings as is will create the Dcobook XML rendering.

To generate the ASCII text only, change the value to "pkgLocal~bCreateXML=.false"

Usage:

   createClassHierarchy > classHierarchy.xml

Then use "classHierarchy.txt" to replace the GENERATED section in "provide.xml"

Dependency on "rgf_util2.rex" which allows one to sort the classes by the (string)
           result of the "id" message to the class objects (independent of case);
           (can also be fetched from the BSF4ooRexx distribution)

Date:     2020-03-10
Version:  100.20200310
*/

dt="<!-- GENERATED by 'docs/trunk/tools/rexxpg/createClassHierarchy.rex' on [".dateTime~new"] -->"

pkgLocal=.context~package~local
pkgLocal~bCreateXML   = .true       -- .true->XML/Docbook, .false->string

--
pkgLocal~bShowAdditionalInfos=.true -- .false   -- true: show mixin and inherits
pkgLocal~bIgnoreMixinInherits=.true -- .false   -- true: show only mixin subclasses that directly specialize the mixin
pkgLocal~bShowAdded50 = .true
pkgLocal~bShowChanged50 = .true

pkgLocal~added50="&added50;"        -- from rexxpg.ent
pkgLocal~changed50="&changed50;"    -- from rexxpg.ent

pkgLocal~ignoreClasses=.set~of(                                      -
        "MessageComparator", "NumberComparator", "StringComparator", -  -- rgf_util2.rex classes
        "StringColumnComparator", "StringOfWords",                   -  -- intentionally not documented
        "BagMixin", "ManyItemMixin", "SetMixin", "SupplierMixin",    -  -- private classes for rexx.img-creation
        "ArgUtil", "LocalServer", "OLEObject", "OLEVariant" )

pkgLocal~classesAdded50=.set~of("StringTable", "AlarmNotification",  -
        "EventSemaphore", "MessageNotification", "MutexSemaphore",   -
        "RexxInfo", "Ticker", "Validate", "VariableReference")

pkgLocal~classesChanged50=.set~of("RegularExpression")

pkgLocal~crlf="0d0a"x
pkgLocal~mb=.MutableBuffer~new(,2048)

pkgLocal~itemizedListBegin = '<itemizedlist mark="none" spacing="compact">'
pkgLocal~itemizedListEnd   = '</itemizedlist>'
pkgLocal~blanks            = '    '    --

if .bCreateXML then .mb~append(.crlf, dt, .crlf, "<para>", .crlf)

call tree .object, 0                -- create the class tree, start out with level=0
if .bCreateXML then .mb~append(.crlf, "</para>", .crlf, dt, .crlf)
say .mb~string                      -- show as string


::requires "rgf_util2.rex"          -- get access to the message sorting ability

::routine tree
  use arg clz, level

  clzId=clz~id                      -- get class name
  if .ignoreClasses~hasIndex(clzId) then return    -- ignore this class

  indent=.blanks~copies(level)
  .mb~append(indent)
  if .bCreateXML then
  do
     if level=0 then
     do
        .mb~append(.crlf, indent, .itemizedListBegin, .crlf, indent, .blanks, "<listitem><para>")
     end
     else
     do
        .mb~append(.crlf, indent, .blanks, "<listitem><para>")
     end
     .mb~append("<classname>", clzId, "</classname>")
     if .bShowAdded50,   .classesAdded50~hasIndex(clzId) then .mb~append(" ", .added50)
     if .bShowChanged50, .classesChanged50~hasIndex(clzId) then .mb~append(" ", .changed50)
  end
  else
  do
     if .bShowAdded50,   .classesAdded50~hasIndex(clzId) then .mb~append(.added50)
     if .bShowChanged50, .classesChanged50~hasIndex(clzId) then .mb~append(.changeed50)
     .mb~append("<classname>", clzId, "</classname>")
  end

  mixinClass?=clz~queryMixinClass

  if clz<>.object then
  do
     if .bShowAdditionalInfos=.true then
     do
        if mixinClass? then
        do
           if .bCreateXML then
              .mb~append('<emphasis role="italic"> (mixin)</emphasis>')
           else
              .mb~append(" (mixin)")
        end

        superClasses=clz~superClasses
        if superClasses~items>1 then
        do
           if .bCreateXML then
              .mb~append('<emphasis role="italic"> (inherit<classname>')
           else
              .mb~append(" (inherit")
           do counter i sc over superClasses
              if i=1 then iterate   -- do not show immediate superclass as inherited class!
              .mb~append(" ", sc~id)
           end

           if .bCreateXML then
             .mb~append('</classname>)</emphasis>')
           else
              .mb~append(")")
        end
     end
  end

  if .bCreateXML then
     .mb~append("</para></listitem>",.crlf)  -- add cr-lf
  else
     .mb~append(.crlf)              -- add cr-lf

  bItemizedListCreated=.false       -- if subclasses listed, set to .true
  msgComparator=.messageComparator~new("id / i") -- use id value, sort case-independently
  do subClz over clz~subClasses~sortWith(msgComparator)
     -- if a mixin class only show the subclass if clz is the direct superclass
     if mixinClass?, .bIgnoreMixinInherits, subClz~superclasses[1]<>clz then return

     if .bCreateXML, bItemizedListCreated=.false then
     do
        .mb~append(.crlf, indent, .blanks, "<listitem>", .itemizedListBegin, .crlf)
        bItemizedListCreated=.true
     end

     call tree subClz, level+1      -- process subclass
  end

  if .bCreateXML, bItemizedListCreated then  -- close subclasses itemizedlist
     .mb~append(indent, .blanks, .itemizedListEnd, "</listitem>", .crlf, .crlf)

  if .bCreateXML, level=0 then      -- close root itemizedlist
     .mb~append(.itemizedListEnd, .crlf)  -- add cr-lf


::routine pp
  return "["arg(1)"]"
