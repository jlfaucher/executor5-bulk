#!/usr/bin/rexx
/*
   name:             drives.testGroup
   authors:          Mark Miesfeld
   date:             11/24/2007
   version:          1.0.0
   changed:

   languageLevel:    6.0
   purpose:          Tests that the sample program, drives.rex, that is shipped
                     with the release package works as expected.  This is a QA
                     test more than a collection of unit tests.

   remark:           This is a Windows only test group.  This test group has
                     more comment in it than needed so it can serve as an
                     example.

   license:          CPL 1.0 (Common Public License v1.0, see below)
   link:

   category1:        ooRexx
   category2:        Samples

*/
/*----------------------------------------------------------------------------*/
/*                                                                            */
/* Copyright (c) 2007-2008 Rexx Language Association. All rights reserved.    */
/*                                                                            */
/* This program and the accompanying materials are made available under       */
/* the terms of the Common Public License v1.0 which accompanies this         */
/* distribution. A copy is also available at the following address:           */
/* http://www.ibm.com/developerworks/oss/CPLv1.0.htm                          */
/*                                                                            */
/* Redistribution and use in source and binary forms, with or                 */
/* without modification, are permitted provided that the following            */
/* conditions are met:                                                        */
/*                                                                            */
/* Redistributions of source code must retain the above copyright             */
/* notice, this list of conditions and the following disclaimer.              */
/* Redistributions in binary form must reproduce the above copyright          */
/* notice, this list of conditions and the following disclaimer in            */
/* the documentation and/or other materials provided with the distribution.   */
/*                                                                            */
/* Neither the name of Rexx Language Association nor the names                */
/* of its contributors may be used to endorse or promote products             */
/* derived from this software without specific prior written permission.      */
/*                                                                            */
/* THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS        */
/* "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT          */
/* LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS          */
/* FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT   */
/* OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,      */
/* SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED   */
/* TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA,        */
/* OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY     */
/* OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING    */
/* NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS         */
/* SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.               */
/*                                                                            */
/*----------------------------------------------------------------------------*/

  -- Windows only test.
  if ooRexxUnit.getOSName() <> "WINDOWS" then return .list~new

  testGroupList = .list~of(.array~of(.drives.testGroup, .list~new))

  srcLines = .array~new
  do i = 1 to 150 until srcLines[i] = "*/"
     srcLines[i] = sourceline(i)
  end

  call makeDirTestInfo .drives.testGroup, srcLines

  parse source s
  .drives.testGroup~testCaseInfo~setentry("test_Case-source", s)

  if \ .local~hasentry("bRunTestsLocally") then
     .local~bRunTestsLocally = .true

  if .bRunTestsLocally == .true then do
    ts = .TestSuite~new
    ts~addTest(.TestSuite~new(.drives.testGroup))

    testResult = ts~run
    j = simpleFormatTestResults(testResult)
  end

return testGroupList
-- End of entry point.

/* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*\
  Directives, Classes, or Routines.
\* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*/
::requires OOREXXUNIT.CLS
::requires "FileUtils.cls"

/* class: drives.testGroup - - - - - - - - - - - - - - - - - - -*\
  The main purpose of this test group is to execute the sample program to assert
  that the sample: 1.) runs 2.) completes normally.  It is not the purpose to
  prove that SysDriveMap(), etc. are operating correctly.

  The test group is broken down into 3 tests.  1. ooRexx is installed correctly
  and the sample program exists.  2. The sample program executes.  3. It
  completes as expected.

  Note that each of these tests is dependent on the preceding test passing.
  This is *not* the usual case for unit tests, where normally each unit test
  is independent of the others.  Because of the dependence of each phase of the
  test group on the preceding phase, a control is used to check that the
  preceding phase passed.  And, the test methods are named such that they will
  be invoked in order.  (This is dependent on a featue of the framework that
  sorts the test method names alphabetically.)

  Also, state information is stored in class attributes of the test group.
  Again, this is not the usual case for unit tests.  It arises from the fact
  that this test group is not really a collection of unit tests; it is a quality
  assurance test.
\* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*/
::class drives.testGroup public subclass TestCase

  ::method test_000_Exists
    self~class~stop = .true

    fileSearch = value("REXX_HOME", , 'ENVIRONMENT')
    self~assertTrue("ooRexx must be installed normally with REXX_HOME set", fileSearch~length > 0)

    -- Look, recursively, for the drives.rex example under the samples directory.
    fileSearch = fileSearch || '\samples\drives.rex'
    j = SysFileTree(fileSearch, f., 'FOS')
    self~assertTrue("SysFileTree must not fail", j == 0)
    self~assertTrue("Sample program, drives.rex, must be located", f.0 == 1)

    prgPathName = f.1
    self~assertTrue("drives.rex must exist", .stream~new(prgPathName)~query("EXISTS")~length <> 0)
    self~class~prgPath = prgPathName
    self~class~stop = .false

  ::method  test_001_Executes
    if self~class~stop then self~fail("Failure of one test causes all tests to fail.")
    self~class~stop = .true

    -- Execute the sample program synchronously.
    --
    -- execRexxPrg() is a public routine in FileUtils.cls.  It handles executing
    -- a Rexx program, waiting for the program to finish, capturing the return
    -- code produced by executing the program, and capturing the output produced
    -- by the program.  The first arg to the routine is the Rexx program name.
    -- The second arg is an Array object.
    --
    -- The return code from execRexxPrg() is the return code from attempting to
    -- execute the program.  Note that this could be, for example, an OS error
    -- code if the program could not be executed.  It also could be 9999 which
    -- indicates an internal error.
    --
    -- The output produced by executing the program is returned in the array,
    -- each element of the array being one line of the output.  The array does
    -- not have to be empty, but of course if you only want to see what the
    -- output is, it should be empty.

    ret = execRexxPrg(self~class~prgPath, self~class~prgOutput)

    -- Check that there is not an internal error.
    self~assertNotSame('execRexxPrg() must be called correctly', 9999, ret)

    -- Check that some output was produced.
    self~assertTrue('drives.rex must produce some output', self~class~prgOutput~items > 0)

    -- Check that we got a 0 rc, which is what drives.rex returns on completion.
    self~assertSame('drives.rex should run and return 0', 0, ret)

    -- All okay, keep going.
    self~class~stop = .false

  ::method test_002_ExpectedCompletion
    if self~class~stop then self~fail("Failure of one test causes all tests to fail.")
    self~class~stop = .true

    -- The output of the executing drives.rex is in the class
    -- attribute: prgOutput

    -- An array of the start of the expected output lines, in reverse order
    expected = .array~new
    expected[1] = "Drive letters not used"
    expected[2] = "Remote drives"
    expected[3] = "RAM drives"
    expected[4] = "Local drives"
    expected[5] = "Used drives"
    expected[6] = "Bootdrive"
    expected[7] = "Operating System Version"

    out = self~class~prgOutput
    j = out~items

    -- Match the last lines of output with the exepected.  We just check the
    -- start of the line, because the actual value of the whole line will vary
    -- from computer to computer.
    do i = 1 to 7
      self~assertTrue('Line' j 'must start with:' expected[i], out[j]~abbrev(expected[i]))
      j -= 1
    end

    -- All okay, keep going.  This is the last test.  This is just here so that
    -- tests could be added.
    self~class~stop = .false

  -- The class attributes used as control and state information.
  ::method init class
    self~init:super
    self~prgPath = ""
    self~prgOutput = .array~new
    self~stop = .false

  ::method prgPath      attribute class
  ::method stop         attribute class
  ::method prgOutput    attribute class

-- End of class: drives.testGroup
