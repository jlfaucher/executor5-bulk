#!/usr/bin/rexx
/*
  SVN Revision: $Rev: 11313 $
  Change Date:  $Date: 2017-10-16 15:05:54 -0400 (Mon, 16 Oct 2017) $
*/
/*----------------------------------------------------------------------------*/
/*                                                                            */
/* Copyright (c) 2007-2017 Rexx Language Association. All rights reserved.    */
/*                                                                            */
/* This program and the accompanying materials are made available under       */
/* the terms of the Common Public License v1.0 which accompanies this         */
/* distribution. A copy is also available at the following address:           */
/* http://www.oorexx.org/license.html                                         */
/*                                                                            */
/* Redistribution and use in source and binary forms, with or                 */
/* without modification, are permitted provided that the following            */
/* conditions are met:                                                        */
/*                                                                            */
/* Redistributions of source code must retain the above copyright             */
/* notice, this list of conditions and the following disclaimer.              */
/* Redistributions in binary form must reproduce the above copyright          */
/* notice, this list of conditions and the following disclaimer in            */
/* the documentation and/or other materials provided with the distribution.   */
/*                                                                            */
/* Neither the name of Rexx Language Association nor the names                */
/* of its contributors may be used to endorse or promote products             */
/* derived from this software without specific prior written permission.      */
/*                                                                            */
/* THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS        */
/* "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT          */
/* LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS          */
/* FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT   */
/* OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,      */
/* SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED   */
/* TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA,        */
/* OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY     */
/* OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING    */
/* NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS         */
/* SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.               */
/*                                                                            */
/*----------------------------------------------------------------------------*/
  parse source . . s
  group = .TestGroup~new(s)
  group~add(.ADDRESS.WITH.testGroup)

  if group~isAutomatedTest then return group

  testResult = group~suite~execute~~print

return testResult



::requires 'ooTest.frm' -- load the ooRexxUnit classes

::class "ADDRESS.WITH.testGroup" subclass ooTestCase public

::method "test_no_with"
-- test that the address command works and the APIs don't crash
-- even if WITH is not specified. The commands we're invoking all will
-- be calling the APIs.

address test 'ISREDIRECTIONREQUESTED'
self~assertSame(.false, rc)

address test 'INPUTREDIRECTED'
self~assertSame(.false, rc)

address test 'OUTPUTREDIRECTED'
self~assertSame(.false, rc)

address test 'ERRORREDIRECTED'
self~assertSame(.false, rc)

address test 'AREOUTPUTERRORTHESAME'
self~assertSame(.false, rc)

address test 'INPUTOUTPUT'
self~assertSame(.false, rc)

address test 'INPUTERROR'
self~assertSame(.false, rc)

::method test_simple_with

address test 'ISREDIRECTIONREQUESTED' with input using "This is a test"
self~assertSame(.true, rc)

-- just to see if the redirect is reported
address test 'INPUTREDIRECTED' with input using "This is a test"
self~assertSame(.true, rc)

address test 'OUTPUTREDIRECTED' with input using "This is a test"
self~assertSame(.false, rc)

address test 'ERRORREDIRECTED' with input using "This is a test"
self~assertSame(.false, rc)

-- this will write a single line to the output...should not crash
address test 'INPUTOUTPUT' with input using "This is a test"
self~assertSame(1, rc)

address test 'INPUTERROR' with input using "This is a test"
self~assertSame(1, rc)

-- using an uninitialized stem variable
address test 'INPUTOUTPUT' with input using "This is a test" output stem a.
self~assertSame(1, rc)
self~assertSame(1, a.0)
self~assertSame("This is a test", a.1)

-- default is replace
address test 'INPUTOUTPUT' with input using "This is another test" output stem a.
self~assertSame(1, rc)
self~assertSame(1, a.0)
self~assertSame("This is another test", a.1)

-- explicit replace
address test 'INPUTOUTPUT' with input using "This is a third test" output replace stem a.
self~assertSame(1, rc)
self~assertSame(1, a.0)
self~assertSame("This is a third test", a.1)

-- and finally append
address test 'INPUTOUTPUT' with input using "This is a fourth test" output append stem a.
self~assertSame(1, rc)
self~assertSame(2, a.0)
self~assertSame("This is a third test", a.1)
self~assertSame("This is a fourth test", a.2)

-- and finally append with an uninitialized stem
address test 'INPUTOUTPUT' with input using "This is a fourth test" output append stem b.
self~assertSame(1, rc)
self~assertSame(1, b.0)
self~assertSame("This is a fourth test", b.1)

-- same tests using ERROR
address test 'INPUTERROR' with input using "This is a test" error stem a.
self~assertSame(1, rc)
self~assertSame(1, a.0)
self~assertSame("This is a test", a.1)

-- default is replace
address test 'INPUTERROR' with input using "This is another test" error stem a.
self~assertSame(1, rc)
self~assertSame(1, a.0)
self~assertSame("This is another test", a.1)

-- explicit replace
address test 'INPUTERROR' with input using "This is a third test" error replace stem a.
self~assertSame(1, rc)
self~assertSame(1, a.0)
self~assertSame("This is a third test", a.1)

-- and finally append
address test 'INPUTERROR' with input using "This is a fourth test" error append stem a.
self~assertSame(1, rc)
self~assertSame(2, a.0)
self~assertSame("This is a third test", a.1)
self~assertSame("This is a fourth test", a.2)

in.0 = 1
in.1 = "This is a test"

array = .array~new

address test 'INPUTOUTPUT' with input stem in. output using (array)

self~assertSame(1, rc)
self~assertSame(1, array~items)
self~assertSame("This is a test", array[1])

in.1 = "This is another test"

address test 'INPUTOUTPUT' with input stem in. output append using (array)

self~assertSame(1, rc)
self~assertSame(2, array~items)
self~assertSame("This is a test", array[1])
self~assertSame("This is another test", array[2])

::method test_file_input

temp = .TemporaryTestFile~new(self, 'testinput')
temp~create(("Line1","","Line3"))

array = .array~new
address test 'INPUTOUTPUT' with input stream (temp~fullName) output using (array)
self~assertSame(3, rc)
self~assertSame(3, array~items)
self~assertSame("Line1", array[1])
self~assertSame("", array[2])
self~assertSame("Line3", array[3])

temp~delete

-- test non-destructive buffering when source and target are the same
::method test_buffering


temp = .TemporaryTestFile~new(self, 'testinput')
temp~create(("Line1","","Line3","","Line5"))

-- this tests buffering on output when the same stream is used as both
address test 'NOBLANKOUTPUT' with input stream (temp~fullName) output stream(temp~fullName)

-- this should read 5 lines (indicated by the rc) and write 3 lines to the file, replacing the file
array = temp~arrayIn
self~assertSame(5, rc)
self~assertSame(3, array~items)
self~assertSame("Line1", array[1])
self~assertSame("Line3", array[2])
self~assertSame("Line5", array[3])

-- now again, with append specified
address test 'NOBLANKOUTPUT' with input stream (temp~fullName) output append stream (temp~fullName)
array = temp~arrayIn

self~assertSame(3, rc)
self~assertSame(6, array~items)
self~assertSame("Line1", array[1])
self~assertSame("Line3", array[2])
self~assertSame("Line5", array[3])
self~assertSame("Line1", array[4])
self~assertSame("Line3", array[5])
self~assertSame("Line5", array[6])

temp~delete

a.0 = 3
a.1 = "Line1"
a.2 = ""
a.3 = "Line3"

-- now same variants using a stem
address test 'NOBLANKOUTPUT' with input stem a. output stem a.
self~assertSame(3, rc)
self~assertSame(2, a.0)
self~assertSame("Line1", a.1)
self~assertSame("Line3", a.2)

-- same as above, with append
address test 'NOBLANKOUTPUT' with input stem a. output append stem a.
self~assertSame(2, rc)
self~assertSame(4, a.0)
self~assertSame("Line1", a.1)
self~assertSame("Line3", a.2)
self~assertSame("Line1", a.3)
self~assertSame("Line3", a.4)


-- now using a stem, but with the USING keyword
drop a.
a.0 = 3
a.1 = "Line1"
a.2 = ""
a.3 = "Line3"

-- now same variants using a stem
address test 'NOBLANKOUTPUT' with input using (a.) output using (a.)
self~assertSame(3, rc)
self~assertSame(2, a.0)
self~assertSame("Line1", a.1)
self~assertSame("Line3", a.2)

-- same as above, with append
address test 'NOBLANKOUTPUT' with input using (a.) output append using (a.)
self~assertSame(2, rc)
self~assertSame(4, a.0)
self~assertSame("Line1", a.1)
self~assertSame("Line3", a.2)
self~assertSame("Line1", a.3)
self~assertSame("Line3", a.4)


-- once more, but mixing up the variantes
drop a.
a.0 = 3
a.1 = "Line1"
a.2 = ""
a.3 = "Line3"

-- now same variants using a stem
address test 'NOBLANKOUTPUT' with input using (a.) output stem a.
self~assertSame(3, rc)
self~assertSame(2, a.0)
self~assertSame("Line1", a.1)
self~assertSame("Line3", a.2)

-- same as above, with append
address test 'NOBLANKOUTPUT' with input stem a. output append using (a.)
self~assertSame(2, rc)
self~assertSame(4, a.0)
self~assertSame("Line1", a.1)
self~assertSame("Line3", a.2)
self~assertSame("Line1", a.3)
self~assertSame("Line3", a.4)

::method test_input_output_stream

instream = .ArrayInputStream~new(("Line1", "Line2"))
outstream = .ArrayOutputStream~new

address test 'INPUTOUTPUT' with input using (instream) output using(outstream)

array = outstream~array

self~assertSame(2, rc)
self~assertSame(2, array~items)
self~assertSame("Line1", array[1])
self~assertSame("Line2", array[2])

-- some RexxQueue tests
::method test_rexxqueue

.rexxqueue~create('ADDRESSWITH')
queue = .rexxqueue~new('ADDRESSWITH')

address test 'INPUTOUTPUT' with input using 'This is a test' output using(queue)
self~assertSame(1, queue~queued)
self~assertSame("This is a test", queue~pull)

queue~queue('Line1')
array = .array~new

address test 'INPUTOUTPUT' with input using(queue) output using(array)
self~assertSame(0, queue~queued)
self~assertSame("Line1", array[1])

-- and the overwrite protection
queue~queue('Line1')
queue~queue('')
queue~queue('Line3')

address test 'NOBLANKOUTPUT' with input using(queue) output using(queue)
self~assertSame(2, queue~queued)
self~assertSame("Line1", queue~pull)
self~assertSame("Line3", queue~pull)

queue~delete


::method test_global_setting

-- The base mechanisms have been tested above, here we're going to test
-- that the global settings are used with commands and can be overridden on a base-by-case
-- basis.

signal off novalue

address test with input using "This is a test" output stem a. error stem b.
-- this will use the redirects
address test 'INPUTOUTPUT'
self~assertSame(1, a.0)
self~assertSame("This is a test", a.1)
self~assertSame(0, b.0)

drop a. b.
-- a command issued to this environment without using ADDRESS will also pick this up
'INPUTERROR'

self~assertSame(1, b.0)
self~assertSame("This is a test", b.1)
self~assertSame(0, a.0)

drop a. b.

-- override the INPUT source...this will use the global output ane error settings
address test 'INPUTOUTPUT' with input using 'This is another test'
self~assertSame(1, a.0)
self~assertSame("This is another test", a.1)
self~assertSame(0, b.0)

drop a. b.

-- now override OUTPUT and ERROR similarly
address test 'INPUTOUTPUT' with output stem c.
self~assertSame(1, c.0)
self~assertSame("This is a test", c.1)
self~assertSame(0, b.0)
self~assertSame('A.0', a.0)      -- should be unchanged

drop a. b. c.

address test 'INPUTOUTPUT' with output stem c.
self~assertSame(1, c.0)
self~assertSame("This is a test", c.1)
self~assertSame('A.0', a.0)
self~assertSame(0, b.0)      -- should be unchanged

drop a. b. c.

-- now test NORMAL overrides

address test 'INPUTOUTPUT' with input NORMAL
-- this has nothing to read, but the output and error stems should reflect nothing written
self~assertSame(0, a.0)
self~assertSame(0, b.0)

drop a. b.

address test 'INPUTOUTPUT' with output NORMAL
-- a. should be unchanged
self~assertSame('A.0', a.0)
self~assertSame(0, b.0)

drop a. b.

address test 'INPUTOUTPUT' with error NORMAL
-- b. should be unchanged
self~assertSame(1, a.0)
self~assertSame("This is a test", a.1)
self~assertSame('B.0', b.0)

-- test the address toggle maintains the settings
address command
address

drop a. b.

-- this will use the redirects
'INPUTOUTPUT'
self~assertSame(1, a.0)
self~assertSame("This is a test", a.1)
self~assertSame(0, b.0)

-- explicit switch back by name...still keeps the settings
address command
address test

drop a. b.

'INPUTOUTPUT'
self~assertSame(1, a.0)
self~assertSame("This is a test", a.1)
self~assertSame(0, b.0)

-- the address toggle only remembers two settings, but the override
-- information should still be maintained as long as the context
-- remains active.
address xedit
address command
address test

drop a. b.

'INPUTOUTPUT'
self~assertSame(1, a.0)
self~assertSame("This is a test", a.1)
self~assertSame(0, b.0)

drop a. b.
-- test that settings are saved on internal call return

self~assertSame('A.0', a.0)
self~assertSame('B.0', b.0)

-- this should have been restored to the original settings
'INPUTOUTPUT'
self~assertSame(1, a.0)
self~assertSame("This is a test", a.1)
self~assertSame(0, b.0)

return

subroutine:


address test with input using "This is another test" output stem c. error stem d.
-- this will use the new redirects
'INPUTOUTPUT'
self~assertSame(1, c.0)
self~assertSame("This is another test", c.1)
self~assertSame(0, d.0)
return

::method test_write_buffer

array = .array~new
crlf = '0d0a'x
lf = '0a'x
cr = '0d'x

-- one string, no linends
address test 'BUFFEROUTPUT' with input using "This is a test" output using (array)
self~assertSame(1, array~items)
self~assertSame("This is a test", array[1])

-- one string, crlf linend
address test 'BUFFEROUTPUT' with input using ("Line1"||crlf) output using (array)
self~assertSame(1, array~items)
self~assertSame("Line1", array[1])

-- one string, lf linend
address test 'BUFFEROUTPUT' with input using ("Line2"||lf) output using (array)
self~assertSame(1, array~items)
self~assertSame("Line2", array[1])

-- two strings, no linefeeds
address test 'BUFFEROUTPUT' with input using (("Line1", "Line2")) output using (array)
self~assertSame(1, array~items)
self~assertSame("Line1Line2", array[1])

-- three strings, no linefeeds
address test 'BUFFEROUTPUT' with input using (("Line1", "Line2", "Line3")) output using (array)
self~assertSame(1, array~items)
self~assertSame("Line1Line2Line3", array[1])

-- one string, crlf linend in the middle
address test 'BUFFEROUTPUT' with input using ("Line1"||crlf||"Line2") output using (array)
self~assertSame(2, array~items)
self~assertSame("Line1", array[1])
self~assertSame("Line2", array[2])

-- one string, lf linend in the middle
address test 'BUFFEROUTPUT' with input using ("Line3"||lf||"Line4") output using (array)
self~assertSame(2, array~items)
self~assertSame("Line3", array[1])
self~assertSame("Line4", array[2])

-- two strings, crlf linened split across two buffers
address test 'BUFFEROUTPUT' with input using (("Line1"||cr, lf||"Line2")) output using (array)
self~assertSame(2, array~items)
self~assertSame("Line1", array[1])
self~assertSame("Line2", array[2])

-- one string, naked cr  in the middle
address test 'BUFFEROUTPUT' with input using ("Line3"||cr||"Line4") output using (array)
self~assertSame(1, array~items)
self~assertSame("Line3"||cr||"Line4", array[1])

-- two strings, naked crlf at end if first buffter
address test 'BUFFEROUTPUT' with input using (("Line1"||cr, "Line2")) output using (array)
self~assertSame(1, array~items)
self~assertSame("Line1"||cr||"Line2", array[1])

-- two strings, multiple line feeds, no line feed at buffer boundary
address test 'BUFFEROUTPUT' with input using (("Line1"||crlf||"Line2"||crlf||"Li", "ne3"||crlf||"Line4")) output using (array)
self~assertSame(4, array~items)
self~assertSame("Line1", array[1])
self~assertSame("Line2", array[2])
self~assertSame("Line3", array[3])
self~assertSame("Line4", array[4])

-- two strings, multiple line feeds, no line feed at buffer boundary
address test 'BUFFEROUTPUT' with input using (("Line5"||lf||"Line6"||lf||"Li", "ne7"||crlf||"Line8")) output using (array)
self~assertSame(4, array~items)
self~assertSame("Line5", array[1])
self~assertSame("Line6", array[2])
self~assertSame("Line7", array[3])
self~assertSame("Line8", array[4])


-- one string, no linends
address test 'BUFFERERROR' with input using "This is a test" error using (array)
self~assertSame(1, array~items)
self~assertSame("This is a test", array[1])

-- one string, crlf linend
address test 'BUFFERERROR' with input using ("Line1"||crlf) error using (array)
self~assertSame(1, array~items)
self~assertSame("Line1", array[1])

-- one string, lf linend
address test 'BUFFERERROR' with input using ("Line2"||lf) error using (array)
self~assertSame(1, array~items)
self~assertSame("Line2", array[1])

-- two strings, no linefeeds
address test 'BUFFERERROR' with input using (("Line1", "Line2")) error using (array)
self~assertSame(1, array~items)
self~assertSame("Line1Line2", array[1])

-- three strings, no linefeeds
address test 'BUFFERERROR' with input using (("Line1", "Line2", "Line3")) error using (array)
self~assertSame(1, array~items)
self~assertSame("Line1Line2Line3", array[1])

-- one string, crlf linend in the middle
address test 'BUFFERERROR' with input using ("Line1"||crlf||"Line2") error using (array)
self~assertSame(2, array~items)
self~assertSame("Line1", array[1])
self~assertSame("Line2", array[2])

-- one string, lf linend in the middle
address test 'BUFFERERROR' with input using ("Line3"||lf||"Line4") error using (array)
self~assertSame(2, array~items)
self~assertSame("Line3", array[1])
self~assertSame("Line4", array[2])

-- two strings, crlf linened split across two buffers
address test 'BUFFERERROR' with input using (("Line1"||cr, lf||"Line2")) error using (array)
self~assertSame(2, array~items)
self~assertSame("Line1", array[1])
self~assertSame("Line2", array[2])

-- one string, naked cr  in the middle
address test 'BUFFERERROR' with input using ("Line3"||cr||"Line4") error using (array)
self~assertSame(1, array~items)
self~assertSame("Line3"||cr||"Line4", array[1])

-- two strings, naked crlf at end if first buffter
address test 'BUFFERERROR' with input using (("Line1"||cr, "Line2")) error using (array)
self~assertSame(1, array~items)
self~assertSame("Line1"||cr||"Line2", array[1])

-- two strings, multiple line feeds, no line feed at buffer boundary
address test 'BUFFERERROR' with input using (("Line1"||crlf||"Line2"||crlf||"Li", "ne3"||crlf||"Line4")) error using (array)
self~assertSame(4, array~items)
self~assertSame("Line1", array[1])
self~assertSame("Line2", array[2])
self~assertSame("Line3", array[3])
self~assertSame("Line4", array[4])

-- two strings, multiple line feeds, no line feed at buffer boundary
address test 'BUFFERERROR' with input using (("Line5"||lf||"Line6"||lf||"Li", "ne7"||crlf||"Line8")) error using (array)
self~assertSame(4, array~items)
self~assertSame("Line5", array[1])
self~assertSame("Line6", array[2])
self~assertSame("Line7", array[3])
self~assertSame("Line8", array[4])


::class ArrayInputStream public inherit InputStream
::method init
  expose currentLine lines
  use strict arg lines

  -- we want a copy of the array so that the content is
  -- isolated from changes. This also allows lists, queues, etc. to be
  -- converted to a stream
  lines = lines~makearray
  currentLine = 1

-- open will reset to the beginning
::method open
  expose currentLine
  use strict arg

  currentLine = 1

-- close will also reset to the beginning
::method close
  forward message('OPEN')

-- character methods not supported
::method charin
  raise syntax 93.963  -- not supported
::method chars
  raise syntax 93.963  -- not supported

::method linein
  expose lines currentLine
  -- we allow a line number to be specified.
  use strict arg lineNumber = (currentLine)

  -- are we trying to read beyond the end of the
  -- array? This is a notready condition with a null
  -- string return
  if lineNumber > lines~items then do
     raise notready additional(self) return("")
  end

  -- the next read is one line down
  currentLine = lineNumber + 1
  -- and complete the read
  return lines[lineNumber]

::method lines
  expose lines currentLine
  use strict arg  -- no arguments allowed

  -- calculate the default number of lines
  count = lines~items - currentLine + 1

  -- everything has been read, so lines is zero
  if count < 1 then do
      return 0
  end

  return count

::class ArrayOutputStream public inherit OutputStream
-- the default initial array size
::constant defaultArraySize 50
-- we can create a default output array or can accept an existing array
-- if given an array, we append any new items to the end.
::method init
  expose currentLine lines
  use strict arg lines = (.array~new(self~defaultArraySize))

  -- write this out following the last existing item
  currentLine = lines~items + 1

-- character operations not supported
::method charout
  raise syntax 93.963  -- not supported
::method chars
  raise syntax 93.963  -- not supported

-- write a line to the next sequetial position
::method lineout
  expose currentLine lines

  -- the line is optional, which is just a close operation
  if arg() == 0 then do
     return 0    -- this is a zero residual count
  end

  -- now get the line and do a full arg count check
  use strict arg line

  lines[currentLine] = line
  currentLine += 1
  return 0               -- the residual is always 0

-- retrieve the output array from the steam
::method array
  expose lines
  use strict arg

  return lines




::options novalue error
