#!/usr/bin/rexx
/*
   name:             ooRexx.framework.Assert.testUnit
   author:           Rony G. Flatscher
   date:             2006-12-27
   version:          1.0.0

-- line commented lines are ignored, when building the directory of infos from this header
   changed:          2006-12-30, ---rgf, added test methods for new "assertNotEquals(...)" method

   languageLevel:    6.0
   purpose:          Test the ooRexxUnit framework
   remark:           ---


   license:          CPL 1.0 (Common Public License v1.0, see below)
   link:

   category1:        ooRexx
   category2:        framework
*/
/*----------------------------------------------------------------------------*/
/*                                                                            */
/* Copyright (c) 2006 Rexx Language Association. All rights reserved.         */
/*                                                                            */
/* This program and the accompanying materials are made available under       */
/* the terms of the Common Public License v1.0 which accompanies this         */
/* distribution. A copy is also available at the following address:           */
/* http://www.ibm.com/developerworks/oss/CPLv1.0.htm                          */
/*                                                                            */
/* Redistribution and use in source and binary forms, with or                 */
/* without modification, are permitted provided that the following            */
/* conditions are met:                                                        */
/*                                                                            */
/* Redistributions of source code must retain the above copyright             */
/* notice, this list of conditions and the following disclaimer.              */
/* Redistributions in binary form must reproduce the above copyright          */
/* notice, this list of conditions and the following disclaimer in            */
/* the documentation and/or other materials provided with the distribution.   */
/*                                                                            */
/* Neither the name of Rexx Language Association nor the names                */
/* of its contributors may be used to endorse or promote products             */
/* derived from this software without specific prior written permission.      */
/*                                                                            */
/* THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS        */
/* "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT          */
/* LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS          */
/* FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT   */
/* OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,      */
/* SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED   */
/* TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA,        */
/* OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY     */
/* OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING    */
/* NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS         */
/* SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.               */
/*                                                                            */
/*----------------------------------------------------------------------------*/



-------------------------------------------------------------------------------------------
-- ===> adapt the "testUnitList" to your testCase classes; each element in the list is <===
-- ===> an array object, the first element containing the testCase class object, the   <===
-- ===> second element is a list of test method names which are regarded to be         <===
-- ===> mandatory (if the list remains empty all test methods are mandatory)           <===

   /* list of array objects, each containing the testUnit class object and an
      optional list of mandatory test case methods name                       */

mandatoryTestMethods=.list~new   -- no mandatory tests for this testCase class
testUnitList=.list~of( .array~of(.ooRexx.framework.Assert.testUnit,  mandatoryTestMethods) )


-------------------------------------------------------------------------------------------
-- ===> the following code needs not to be individualized                              <===

   -- read top comment, containing infos about this program
arrLines=.array~new
do i=1 to 150 until arrLines[i]="*/"
   arrLines[i]=sourceline(i)
end
      -- supply information for the testClass(es) in this file; the class attribute
      -- "TestCaseInfo" (a directory object, index points to a queue) will store
      -- the parsed infos
aTestUnitClass=testUnitList~at(testUnitList~first)[1] -- get first testClass

   -- will parse the array lines and store result in class object
call makeDirTestInfo aTestUnitClass, arrLines
tmpDir=aTestUnitClass~TestCaseInfo
parse source s   -- op_sys invocationType fullPathToThisFile
tmpDir~setentry("test_Case-source", s)

   -- now add this directory to other testCase classes, if any left
do arr over testUnitList
   if arr[1]=aTestUnitClass then iterate  -- already handled
   arr[1]~TestCaseInfo=tmpDir             -- save info in class object
end

-- if this file is CALLed or REQUIRED then define an entry "bRunTestLocally" in .local
-- and set it to .false; this way the independent local invocation of the tests is inhibited
if .local~hasentry("bRunTestsLocally")=.false then
   .local~bRunTestsLocally=.true -- if this file is executed directly, then run tests for debugging

if .bRunTestsLocally=.true then  -- run ALL tests in this test unit
do
   ts=.testSuite~new             -- create a testSuite
   do arr over testUnitList
      -- create a testSuite for the given test case class, use all its testmethods
      ts~addTest( .testSuite~new(arr[1]))
   end
   -- testResult=.testSuite~new(testUnitClass)~execute
   testResult=ts~execute       -- now run all the tests

   call simpleDumpTestResults testResult
end

   /* return list of array objects containing test case classes and
      optionally list of mandatory test methods                      */
return testUnitList



::requires ooRexxUnit.cls     -- load the ooRexxUnit classes

::class "ooRexx.framework.Assert.testUnit" subclass TestCase public

   -- test "assertEquals"
::method "test_assertEquals_00"
   self~assertEquals(1, 1)          -- no failure message

   -- test "assertEquals"
::method "test_assertEquals_01"
   self~assertEquals("subtest_01", 1, 1)
   self~assertEquals("subtest_02", 2, 2)
   self~assertEquals("subtest_03", .nil, .nil)
   o=.object~new
   self~assertEquals("subtest_04", o, o)


   -- test "assertEquals"
::method "test_assertEquals_02"
   bag=.bag~of("a", "a", "b", "c")
   set=.set~new~union(bag)

   self~assertEquals("subtest_01", bag, bag)
   self~assertEquals("subtest_02", set, set)
   self~assertEquals("subtest_03", .bag~new~union(set), .bag~new~union(set))

   -- test "assertEquals"
::method "test_assertEquals_03"
   b=.bag~of("a", "a", "b")
   r=.relation~new
   r["a"]="a"
   r["a"]="a"
   r["b"]="b"
   self~assertEquals("subtest_01", b, r)


   -- test "assertEquals"
::method "test_assertEquals_04"
   s=.set~of("a", "a", "b")
   t=.table~new
   t["a"]="a"
   t["a"]="a"
   t["b"]="b"
   self~assertEquals("subtest_01", s, t)


   -- test "assertEquals"
::method "test_assertEquals_05"
   s=.set~of("a", "a", "b")
   d=.directory~new
   d["a"]="a"
   d["a"]="a"
   d["b"]="b"
   self~assertEquals("subtest_01", s, d)


   -- test "assertEquals"
::method "test_assertEquals_06"
   a=.array~of("a", "a", "b")
   l=.list~of("a", "a", "b")
   q=.queue~new~~queue("a")~~queue("a")~~queue("b")
   c=.circularQueue~new(3)~~queue("a")~~queue("a")~~queue("b")

   self~assertEquals("subtest_01", l, q)
   self~assertEquals("subtest_02", a, l)
   self~assertEquals("subtest_03", a, q)
   self~assertEquals("subtest_04", a, c)
   self~assertEquals("subtest_05", l, c)
   self~assertEquals("subtest_06", q, c)



::method "test_assertEquals_07"
   s=.set~of("a", "a", "b")
   t=.table~new~union(s)
   self~assertEquals("subtest_01", s, t)


::method "test_assertEquals_08"
   s=.set~of("a", "a", "b")
   r=.relation~new~union(s)
   self~assertEquals("subtest_01", s, r)


::method "test_assertEquals_09"
   t=.table~new
   t["a"]="a"
   t["b"]="b"
   r=.relation~new~union(t)
   self~assertEquals("subtest_01", t, r)



   -- test "assertNotEquals"
::method "test_assertNotEquals_00"
   self~assertNotEquals(1, 2)          -- no failure message

   -- test "assertNotEquals"
::method "test_assertNotEquals_01"
   self~assertNotEquals("subtest_01", 1, 2)
   self~assertNotEquals("subtest_02", 2, 1)
   self~assertNotEquals("subtest_03", .nil, .object)
   o=.object~new
   self~assertNotEquals("subtest_04", .nil, o)


   -- test "assertNotEquals"
::method "test_assertNotEquals_02"
   bag=.bag~of("a", "a", "b", "c")
   set=.set~new~union(bag)

   self~assertNotEquals("subtest_01", bag, set)
   self~assertNotEquals("subtest_02", set, bag)
   self~assertNotEquals("subtest_03", .bag~new~union(set), .bag~new~union(set)~~put("c"))

   -- test "assertNotEquals"
::method "test_assertNotEquals_03"
   b=.bag~of("a", "a", "b")
   r=.relation~new
   r["a"]="a"
   r["a"]="a"
   r["b"]="b"
   r["c"]="c"
   self~assertNotEquals("subtest_01", b, r)


   -- test "assertNotEquals"
::method "test_assertNotEquals_04"
   s=.set~of("a", "a", "b")
   t=.table~new
   t["a"]="a"
   t["a"]="a"
   t["b"]="b"
   t["c"]="c"
   self~assertNotEquals("subtest_01", s, t)


   -- test "assertNotEquals"
::method "test_assertNotEquals_05"
   s=.set~of("a", "a", "b")
   d=.directory~new
   d["a"]="a"
   d["a"]="a"
   d["b"]="b"
   d["c"]="c"
   self~assertNotEquals("subtest_01", s, d)


   -- test "assertNotEquals"
::method "test_assertNotEquals_06"
   a=.array~of("a", "a", "b", "b")
   l=.list~of("a", "a", "b", "b", "c")
   q=.queue~new~~queue("a")~~queue("a")~~queue("b")~~queue("b")~~queue("c")~~queue("c")
   c=.circularQueue~new(3)~~queue("a")~~queue("a")~~queue("b")~~queue("b")~~queue("c")~~queue("c")~~queue("d")

   self~assertNotEquals("subtest_01", l, q)
   self~assertNotEquals("subtest_02", a, l)
   self~assertNotEquals("subtest_03", a, q)
   self~assertNotEquals("subtest_04", a, c)
   self~assertNotEquals("subtest_05", l, c)
   self~assertNotEquals("subtest_06", q, c)



::method "test_assertNotEquals_07"
   s=.set~of("a", "a", "b")
   t=.table~new~union(s)~~put("c","c")
   self~assertNotEquals("subtest_01", s, t)


::method "test_assertNotEquals_08"
   s=.set~of("a", "a", "b")
   r=.relation~new~union(s)~~put("c", "c")
   self~assertNotEquals("subtest_01", s, r)


::method "test_assertNotEquals_09"
   t=.table~new
   t["a"]="a"
   t["b"]="b"
   r=.relation~new~union(t)~~put("c", "c")
   self~assertNotEquals("subtest_01", t, r)



::method "test_assertFalse"
   self~assertFalse(.false)         -- no failure message
   self~assertFalse("should be .false", .false)



::method "test_assertNotNull"
   self~assertNotNull("2")          -- no failure message
   self~assertNotNull("message in case of failure", "2")


::method "test_assertNotSame"
   self~assertNotSame(1, "2")       -- no failure message
   self~assertNotSame("message in case of failure", 1, "2")


::method "test_assertNull"
   self~assertNull(.nil)            -- no failure message
   self~assertNull("message in case of failure", .nil)


::method "test_assertSame"
   self~assertSame(1, "1")          -- no failure message
   self~assertSame("message in case of failure", 1, "1")


::method "test_assertTrue"
   self~assertTrue(.true)           -- no failure message
   self~assertTrue("should be .true", .true)


::method "test_expectSyntax"
   self~expectSyntax(13.1)
   raise syntax 13.1


::method "test_expectCondition"
   self~expectCondition("NOVALUE")
   raise novalue


/* *** test methods which create failures and demonstrate the
       resulting failure messages */

/*
::method "test_fail_assertEquals_01"
   self~assertEquals(12, 13)

::method "test_fail_assertEquals_02"
   self~assertEquals("A message in case of a failure.", 12, 13)


::method "test_fail_assertFalse_01"
   self~assertFalse(.true)

::method "test_fail_assertFalse_02"
   self~assertFalse("A message in case of a failure.", .true)


::method "test_fail_assertNotNull_01"
   self~assertNotNull(.nil)

::method "test_fail_assertNotNull_02"
   self~assertNotNull("A message in case of a failure.", .nil)


::method "test_fail_assertNotSame_01"
   a=12
   self~assertNotSame(a, a)

::method "test_fail_assertNotSame_02"
   a=12
   self~assertNotSame("A message in case of a failure.", a, a)


::method "test_fail_assertNotEquals_01"
   self~assertNotEquals(12, 12)

::method "test_fail_assertNotEquals_02"
   self~assertNotEquals("A message in case of a failure.", 12, 12)



::method "test_fail_assertNull_01"
   self~assertNull(12)

::method "test_fail_assertNull_02"
   self~assertNull("A message in case of a failure.", 12)


::method "test_fail_assertSame_01"
   self~assertSame(12, " 12")

::method "test_fail_assertSame_02"
   self~assertSame("A message in case of a failure.", 12, "12 ")


::method "test_fail_assertTrue_01"
   self~assertTrue(.false)

::method "test_fail_assertTrue_02"
   self~assertTrue("A message in case of a failure.", .false)


::method "test_fail_expectSyntax"
   self~expectSyntax(13.1)          -- but do not raise it!


::method "test_fail_expectCondition"
   self~expectCondition("NOVALUE")  -- but do not raise it!


::method "test_fail_unexpectedSyntax"
   raise syntax 13.1 array("xyz", "01")


::method "test_fail_expectedCondition"
   raise novalue
*/
